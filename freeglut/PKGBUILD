################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('freeglut')
pkgver=3.2.0
pkgrel=1
pkgdesc='Provides functionality for small OpenGL programs'
url='http://freeglut.sourceforge.net/'
license=('MIT')
arch=('x86_64')


#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'gcc-libs'
	'glibc'
	'libgl'
	'libx11'
	'libxi'
	'libxkbcommon'
	'libxrandr'
	'libxxf86vm'
	#'wayland'
)
makedepends=(
	'cmake'
	'glu'
	'mesa'
)

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
provides=('glut')
conflicts=('glut')
replaces=('glut')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://downloads.sourceforge.net/freeglut/freeglut-${pkgver}.tar.gz"
	'glut.diff'
)
sha512sums=(
	'78da1eaa845607ddd97a6744f3a97ff26cdb562fdf990695c8ac97a60d9d692496e4bf3672acfc522add60823c2f3bc7435e19e8041fbbb5d95851c0b641a818'
	'6778538722707460a7c71eceee430f3945fcfcff351d0a05623417a183af4e8f85a497e24208b73756b2713bb5c11d39dbd4e6de7f06b8865fd81a1faa23b9a4'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Apply patches
	patch -Np1 -i $srcdir/glut.diff
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		"-DCMAKE_C_FLAGS='$CFLAGS'"
		"-DCMAKE_CXX_FLAGS='$CXXFLAGS'"
		"-DCMAKE_LD_FLAGS='$LDFLAGS'"
		'-DCMAKE_INSTALL_PREFIX=/usr'
		'-DCMAKE_BUILD_TYPE=Release'
		'-DFREEGLUT_BUILD_SHARED_LIBS=ON'
		'-DFREEGLUT_BUILD_STATIC_LIBS=OFF'
		'-DFREEGLUT_BUILD_DEMOS=OFF'
		'-DFREEGLUT_REPLACE_GLUT=ON'
		'-DFREEGLUT_GLES=OFF'
		'-DFREEGLUT_PRINT_ERRORS=ON'
		'-DFREEGLUT_PRINT_WARNINGS=ON'

	# 'wayland' related options
		#'-DFREEGLUT_WAYLAND=ON'
		'-DFREEGLUT_WAYLAND=OFF'
	)

	# Create build directory
	mkdir -p build && cd build

	# Run package configuration script
	cmake .. "${config_opts[@]}"

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver/build

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 ../COPYING $pkgdir/usr/share/licenses/freeglut/LICENSE

	# Change directory to target directory
	cd $pkgdir

	# Set symbolic link to glut.pc
	ln -s usr/lib/pkgconfig/glut.pc freeglut.pc
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
