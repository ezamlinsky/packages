################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('gdbm')
pkgver=1.18.1
pkgrel=2
pkgdesc='GNU database library'
url='http://www.gnu.org/software/gdbm/gdbm.html'
license=('GPL3')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'glibc'
	'readline'
	'sh'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://ftp.gnu.org/gnu/gdbm/gdbm-$pkgver.tar.gz"
	"https://ftp.gnu.org/gnu/gdbm/gdbm-$pkgver.tar.gz.sig"
)
md5sums=(
	'988dc82182121c7570e0cb8b4fcd5415'
	'SKIP'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-debug'
		'--enable-memory-mapped-io'
		'--enable-libgdbm-compat'
		'--enable-gdbm-export'

	# 'readline' related options
		'--with-readline'
		#'--without-readline'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Create symlinks for compatibility
	mkdir -p $pkgdir/usr/include/gdbm
	ln -sf ../gdbm.h $pkgdir/usr/include/gdbm/gdbm.h
	ln -sf ../ndbm.h $pkgdir/usr/include/gdbm/ndbm.h
	ln -sf ../dbm.h  $pkgdir/usr/include/gdbm/dbm.h
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
