################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('xkeyboard-config')
pkgver=2.27
pkgrel=1
pkgdesc='X keyboard configuration files'
url='https://www.freedesktop.org/wiki/Software/XKeyboardConfig'
license=('custom')
arch=('any')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
makedepends=(
	'intltool'
	'libxslt'
	'xorg-util-macros'
	'xorg-xkbcomp'
)

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
provides=('xkbdata')
conflicts=('xkbdata')
replaces=('xkbdata')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://xorg.freedesktop.org/archive/individual/data/xkeyboard-config/xkeyboard-config-$pkgver.tar.bz2"
	"https://xorg.freedesktop.org/archive/individual/data/xkeyboard-config/xkeyboard-config-$pkgver.tar.bz2.sig"
)
sha512sums=(
	'ae29177461f9d2c6ce1c56c0b8b147600e3f0ecd15e8754c9531811702d9a030a31f42679a88a9fb706bec458974c337ee2a0180f22573702caa7184eb94017e'
	'SKIP'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--enable-compat-rules'
		'--with-xkb-base=/usr/share/X11/xkb'
		'--with-xkb-rules-symlink=xorg'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 COPYING $pkgdir/usr/share/licenses/xkeyboard-config/COPYING

	# Create '/var/lib/xkb' directory
	mkdir -p $pkgdir/var/lib/xkb
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
