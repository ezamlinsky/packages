################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('libnghttp2')
pkgver=1.39.2
pkgrel=2
pkgdesc='Framing layer of HTTP/2 is implemented as a reusable C library'
url='https://nghttp2.org/'
license=('MIT')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=('glibc')
makedepends=(
	#'jemalloc'
)

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
conflicts=('nghttp2<1.20.0-2')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=("https://github.com/nghttp2/nghttp2/releases/download/v$pkgver/nghttp2-$pkgver.tar.xz")
sha512sums=('d8c971543e3e87736dfafebca55e9ecd0644e304c9731edaccba34170205824476595861a439077289b438ad489dd6008dedf2c6b2c111920300329be1b1bf34')

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd nghttp2-$pkgver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd nghttp2-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-werror'
		'--disable-debug'
		'--disable-assert'
		'--enable-threads'
		'--disable-app'
		'--disable-examples'
		'--disable-hpack-tools'
		'--disable-python-bindings'
		'--enable-failmalloc'
		'--enable-lib-only'
		'--enable-asio-lib'
		'--with-libxml2'
		'--with-systemd'
		'--with-boost=yes'

	# 'jemalloc' related options
		#'--with-jemalloc'
		'--without-jemalloc'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd nghttp2-$pkgver

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd nghttp2-$pkgver/lib

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 ../COPYING $pkgdir/usr/share/licenses/libnghttp2/COPYING
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
