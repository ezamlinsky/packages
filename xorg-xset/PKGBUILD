################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('xorg-xset')
pkgver=1.2.4
pkgrel=1
pkgdesc='User preference utility for X'
url='https://xorg.freedesktop.org/'
license=('custom')
arch=('x86_64')
groups=('xorg-apps' 'xorg')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'glibc'
	'libx11'
	'libxext'
	'libxmu'
)
makedepends=('xorg-util-macros')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://xorg.freedesktop.org/archive/individual/app/xset-$pkgver.tar.bz2"
	"https://xorg.freedesktop.org/archive/individual/app/xset-$pkgver.tar.bz2.sig"
)
sha512sums=(
	'f24714c9a82081a09d3054bbad98553de9366992f22eaf3e2bcadbb58fad1d3dad2547fef6fa9898d8a9df064573c29df9d82a5c801fa92248604c95f65dc83d'
	'SKIP'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd xset-$pkgver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd xset-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--without-xf86misc'
		'--without-fontcache'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd xset-$pkgver

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd xset-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 COPYING $pkgdir/usr/share/licenses/xorg-xset/LICENSE
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
