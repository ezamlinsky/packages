################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname='libxml2'
pkgver=2.9.10
pkgrel=1
pkgdesc='XML parsing library, version 2'
url='http://www.xmlsoft.org/'
license=('MIT')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'glibc'
	'icu'
	'readline'
	'sh'
	'xz'
	'zlib'
)
makedepends=(
	'python2'
	'python'
	'git'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_commit='41a34e1f4ffae2ce401600dbb5fe43f8fe402641'
source=(
	"git+https://gitlab.gnome.org/GNOME/libxml2.git#commit=$_commit"
	'libxml2-2.9.8-python3-unicode-errors.patch'
	'https://www.w3.org/XML/Test/xmlts20130923.tar.gz'
)
sha256sums=(
	'SKIP'
	'37eb81a8ec6929eed1514e891bff2dd05b450bcf0c712153880c485b7366c17c'
	'9b61db9f5dbffa545f4b8d78422167083a8568c59bd1129f94138f936cf6fc1f'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname

	# Apply patches
	patch -p1 -i $srcdir/libxml2-2.9.8-python3-unicode-errors.patch

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-static'
		'--enable-ipv6=yes'
		'--enable-rebuild-docs=no'
		'--without-minimum'
		'--without-debug'
		'--without-mem-debug'
		'--without-run-debug'
		'--without-thread-alloc'
		'--without-fexceptions'
		'--with-threads'
		'--with-c14n'
		'--with-catalog'
		'--with-docbook'
		'--with-history'
		'--with-ftp'
		'--with-html'
		'--with-http'
		'--with-output'
		'--with-regexps'
		'--with-schemas'
		'--with-schematron'
		'--with-valid'
		'--with-writer'
		'--with-xinclude'
		'--with-xpath'
		'--with-xptr'
		'--with-modules'
		'--with-pattern'
		'--with-push'
		'--with-reader'
		'--with-sax1'
		'--with-tree'
		'--with-legacy'
		'--with-iconv'
		'--with-iso8859x'

	# 'icu' related options
		'--with-icu'
		#'--without-icu'

	# 'python2' related options
		#'--with-python=/usr/bin/python2'
		'--without-python'

	# 'python' related options
		#'--with-python=/usr/bin/python3'
		'--without-python'

	# 'readline' related options
		'--with-readline'
		#'--without-readline'

	# 'xz' related options
		'--with-lzma'
		#'--without-lzma'

	# 'zlib' related options
		'--with-zlib'
		#'--without-zlib'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 COPYING $pkgdir/usr/share/licenses/libxml2/COPYING
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
