################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('pacman')
pkgver=5.1.3
pkgrel=1
pkgdesc='A library-based package manager with dependency support (new features testing)'
url='http://www.archlinux.org/pacman/'
license=('GPL')
arch=('x86_64')
groups=('base' 'base-devel')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'archlinux-keyring'
	'bash'
	'curl'
	'gcc-libs'
	'glibc'
	'gpgme'
	'libarchive'
	#'nettle'
	'openssl'
	'pacman-mirrorlist'
)
makedepends=('asciidoc')
checkdepends=(
	'python2'
	'fakechroot'
)
optdepends=(
	'perl-locale-gettext: translation support in makepkg-template'
	'xdelta3: delta support in repo-add'
)

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
provides=('pacman')
conflicts=('pacman')

#==============================================================================#
#       Package miscellaneous options                                          #
#==============================================================================#
backup=(
	'etc/pacman.conf'
	'etc/makepkg.conf'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://sources.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz"
	"https://sources.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz.sig"
	'pacman.conf'
	'makepkg.conf'
)
sha256sums=(
	'10db61a0928d619871340c3f93a677d1541d6c52353c516aec4f8d96e830d4eb'
	'SKIP'
	'b82e9c1073292766c2f49b5c771dddf37383f1151117ef9cd5e0c11047b4f3eb'
	'8442bbb3d1d83f09610e9d687f9121037d97c6acaa06c4e907fc6e9eb0b127ec'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--enable-git-version'
		'--disable-static'
		'--disable-debug'
		'--enable-doc'
		'--disable-doxygen'
		'--with-scriptlet-shell=/usr/bin/bash'
		'--with-ldconfig=/usr/bin/ldconfig'

	# 'curl' related options
		'--with-libcurl'
		#'--without-libcurl'

	# 'gpgme' related options
		'--with-gpgme'
		#'--without-gpgme'

	# 'openssl' related options
		'--with-crypto=openssl'

	# 'nettle' related options
		#'--with-crypto=nettle'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Change directory to target directory
	cd $pkgdir

	# Install Arch specific stuff
	install -Dm644 $srcdir/pacman.conf etc/pacman.conf
	install -Dm644 $srcdir/makepkg.conf etc/makepkg.conf
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
