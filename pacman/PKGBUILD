################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('pacman')
pkgver=5.2.1
pkgrel=4
pkgdesc='A library-based package manager with dependency support'
url='https://www.archlinux.org/pacman/'
license=('GPL')
arch=('x86_64')
groups=('base-devel')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'archlinux-keyring'
	'bash'
	'curl'
	'glibc'
	'gpgme'
	'libarchive'
	#'nettle'
	'openssl'
	'pacman-mirrorlist'
	'perl'
)
makedepends=('asciidoc')
checkdepends=(
	'python'
	'fakechroot'
)
optdepends=('perl-locale-gettext: translation support in makepkg-template')

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
provides=('libalpm.so')

#==============================================================================#
#       Package miscellaneous options                                          #
#==============================================================================#
backup=(
	'etc/pacman.conf'
	'etc/makepkg.conf'
)
options=('emptydirs')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://sources.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz"
	"https://sources.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz.sig"
	'pacman-5.2.1-fix-pactest-package-tar-format.patch::https://git.archlinux.org/pacman.git/patch/?id=b9faf652735c603d1bdf849a570185eb721f11c1'
	'makepkg-fix-one-more-file-seccomp-issue.patch'
	'pacman.conf'
	'makepkg.conf'
)
sha256sums=(
	'1930c407265fd039cb3a8e6edc82f69e122aa9239d216d9d57b9d1b9315af312'
	'SKIP'
	'SKIP'
	'e481a161bba76729cd434c97e0b319ddfcb1d93b2e4890d72b4e8a32982531d9'
	'3353f363088c73f1f86a890547c0f87c7473e5caf43bbbc768c2e9a7397f2aa2'
	'8c100b64450f5a19a16325dd05c143d49395bdeb96bd957f863cde4b95d3cb86'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Apply patches
	patch -p1 -i $srcdir/pacman-5.2.1-fix-pactest-package-tar-format.patch
	patch -p1 -i $srcdir/makepkg-fix-one-more-file-seccomp-issue.patch

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--enable-git-version'
		'--disable-static'
		'--disable-debug'
		'--enable-doc'
		'--disable-doxygen'
		'--with-file-seccomp=yes'
		'--with-scriptlet-shell=/usr/bin/bash'
		'--with-ldconfig=/usr/bin/ldconfig'

	# 'curl' related options
		'--with-libcurl'
		#'--without-libcurl'

	# 'gpgme' related options
		'--with-gpgme'
		#'--without-gpgme'

	# 'openssl' related options
		'--with-crypto=openssl'

	# 'nettle' related options
		#'--with-crypto=nettle'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Change directory to target directory
	cd $pkgdir

	# Install Arch specific stuff
	install -Dm644 $srcdir/pacman.conf etc/pacman.conf
	install -Dm644 $srcdir/makepkg.conf etc/makepkg.conf
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
