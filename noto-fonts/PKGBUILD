################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgbase='noto-fonts'
pkgname=('noto-fonts' 'noto-fonts-extra' 'ttf-croscore')
pkgver=20190926
pkgrel=4
url='https://www.google.com/get/noto/'
license=('custom:SIL')
arch=('any')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_commit='0fa1dfabd6e3746bb7463399e2813f60d3f1b256'
source=(
	"$pkgbase-$_commit.tar.gz::https://github.com/googlei18n/$pkgbase/archive/$_commit.tar.gz"
	'66-noto-sans.conf'
	'66-noto-serif.conf'
	'66-noto-mono.conf'
	'46-noto-sans.conf'
	'46-noto-serif.conf'
	'46-noto-mono.conf'
)
sha256sums=(
	'6ca2938b99e4316c6eabdab498f577efb979c4f2aeaf5a3a357ab4e3dcc2c2a5'
	'52684bebf6447be22618d2a04ff37623ec92f9d8ccf6b6f972e5bcbcfee90d69'
	'4459944b63dc083107280f5d7375c69746bf80a09416a4a4909a100e58e5a33a'
	'4526289f59654e2a81dc734669a1ae4e416f9a56d0896ec3741c6bf065baf8a8'
	'83a8faf6a47954075f97a2d555048e2a6689c38603b2ca00150157bf645f4593'
	'c94368b24506770767d003e5bcba589a8e402e489c240ee52453bf3ac7e9b5fa'
	'f5c09b37280d7569b6c99a78511639be4ae25b8c5406464422fe0421fe13a884'
)

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Noto-fonts                                                             #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_noto-fonts() {
	pkgdesc='Google Noto TTF fonts'
	optdepends=(
		'noto-fonts-cjk: CJK characters'
		'noto-fonts-emoji: Emoji characters'
		'noto-fonts-extra: additional variants (condensed, semi-bold, extra-light)'
	)
	provides=('ttf-font')

	# Change directory to source directory
	cd $pkgbase-$_commit

	# Install files
	mkdir -p $pkgdir/etc/fonts/conf.d
	install -Dm644 unhinted/Noto*/*.tt[fc] -t $pkgdir/usr/share/fonts/noto
	install -Dm644 hinted/Noto*/*.tt[fc] -t $pkgdir/usr/share/fonts/noto
	install -Dm644 $srcdir/*.conf -t $pkgdir/etc/fonts/conf.avail/
	ln -rs $pkgdir/etc/fonts/conf.avail/* $pkgdir/etc/fonts/conf.d

	# Install license file
	install -Dm644 LICENSE -t $pkgdir/usr/share/licenses/noto-fonts/LICENSE

	# Remove non required files
	rm -f $pkgdir/usr/share/fonts/noto/Noto*{Condensed,SemiBold,Extra}*.ttf
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Noto-fonts-extra                                                       #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_noto-fonts-extra() {
	pkgdesc='Google Noto TTF fonts - additional variants'
	depends=('noto-fonts')

	# Change directory to source directory
	cd $pkgbase-$_commit

	# Install files
	mkdir -p $pkgdir/usr/share/fonts/noto
	cp hinted/Noto*/*{Condensed,SemiBold,Extra}*.tt[fc] $pkgdir/usr/share/fonts/noto

	# Install license file
	install -Dm644 LICENSE -t $pkgdir/usr/share/licenses/noto-fonts-extra/LICENSE
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Ttf-croscore                                                           #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_ttf-croscore() {
	pkgdesc='Chrome OS core fonts'
	provides=('ttf-font')

	# Change directory to source directory
	cd $pkgbase-$_commit

	# Install files
	install -Dm644 hinted/{Arimo,Cousine,Tinos}/*.ttf -t $pkgdir/usr/share/fonts/croscore

	# Install license file
	install -Dm644 LICENSE -t $pkgdir/usr/share/licenses/ttf-croscore/LICENSE
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
