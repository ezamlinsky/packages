################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('gtk2')
pkgver=2.24.32
pkgrel=1
pkgdesc='GObject-based multi-platform GUI toolkit (legacy)'
url='https://www.gtk.org/'
license=('LGPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'atk'
	'cairo'
	'desktop-file-utils'
	'fontconfig'
	'gdk-pixbuf2'
	'glib2'
	'glibc'
	'gtk-update-icon-cache'
	#'libcups'
	'libx11'
	'libxcomposite'
	'libxcursor'
	'libxdamage'
	'libxext'
	'libxfixes'
	'libxi'
	'libxinerama'
	'libxrandr'
	'libxrender'
	'pango'
	'shared-mime-info'
)
makedepends=(
	'git'
	'gtk-doc'
	'python2'
)
optdepends=(
	'adwaita-icon-theme: Default icon theme'
	'gnome-themes-standard: Default widget theme'
)

#==============================================================================#
#       Package miscellaneous options                                          #
#==============================================================================#
install='gtk2.install'

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_commit='ed7d3e25f8b6debae6ccc8b50d1329155338cab8'  # tags/2.24.32^0
source=(
	"git+https://git.gnome.org/browse/gtk+#commit=$_commit"
	'gtkrc'
	'gtk-query-immodules-2.0.hook'
	'xid-collision-debug.patch'
)
sha256sums=(
	'SKIP'
	'bc968e3e4f57e818430130338e5f85a5025e21d7e31a3293b8f5a0e58362b805'
	'9656a1efc798da1ac2dae94e921ed0f72719bd52d4d0138f305b993f778f7758'
	'd758bb93e59df15a4ea7732cf984d1c3c19dff67c94b957575efea132b8fe558'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd gtk+

	# Apply patches
	patch -Np1 -i $srcdir/xid-collision-debug.patch

	# Fix python version
	sed -i '1s/python$/&2/' gtk/gtk-builder-convert

	# Fix automake version
	sed -i 's/1.15/1.16/g' autogen.sh

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd gtk+

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--enable-man'
		'--disable-gtk-doc'
		'--disable-papi'
		'--enable-xkb'
		'--enable-introspection=no'
		'--enable-debug=no'
		'--with-x'

	# 'libcups' related options
		#'--enable-cups'
		'--disable-cups'

	# 'libxinerama' related options
		'--enable-xinerama'
		#'--disable-xinerama'

	# 'libxi' related options
		'--with-xinput=yes'
		#'--with-xinput=no'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd gtk+

	# Install files
	make DESTDIR=$pkgdir install

	# Change directory to target directory
	cd $pkgdir

	# Install 'gtkrc' file
	install -Dm644 $srcdir/gtkrc usr/share/gtk-2.0/gtkrc

	# Add vendor's hooks
	install -Dm644 $srcdir/gtk-query-immodules-2.0.hook usr/share/libalpm/hooks/gtk-query-immodules-2.0.hook

	# Remove 'gtk-update-icon-cache' related files
	rm usr/bin/gtk-update-icon-cache
	rm usr/share/man/man1/gtk-update-icon-cache.1
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
