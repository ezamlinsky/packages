################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgbase='boost'
pkgname=('boost' 'boost-libs')
pkgver=1.71.0
pkgrel=3
url='https://www.boost.org/'
license=('custom')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
makedepends=(
	'bzip2'
	'gcc-libs'
	'glibc'
	'icu'
	#'python'
	#'python-numpy'
	'python2'
	'python2-numpy'
	'xz'
	'zlib'
	'zstd'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=("https://dl.bintray.com/boostorg/release/${pkgver}/source/boost_${pkgver//./_}.tar.bz2")
sha256sums=('d73a8da01e8bf8c7eda40b4c84915071a8c8a0df4a6734537ddde4a8580524ee')

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd ${pkgbase}_${pkgver//./_}

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--libdir=/usr/lib'
		'--includedir=/usr/include'
		'--with-toolset=gcc'
		'--with-libraries=all'

	# 'icu' related options
		'--with-icu'
		#'--without-icu'

	# 'python' related options
		#'--with-python=python3'

	# 'python2' related options
		'--with-python=python2'
	)

	# Run package configuration script
	./bootstrap.sh "${config_opts[@]}"

	# Enable support for OpenMPI
	echo 'using mpi ;' >> project-config.jam

	# Build options
	build_opts=(
		"cflags=${CPPFLAGS} ${CFLAGS} -fPIC"
		"cxxflags=${CPPFLAGS} ${CXXFLAGS} -fPIC -std=c++14"
		"linkflags=${LDFLAGS}"
		'debug-symbols=off'
		'variant=release'
		'threading=multi'
		'link=shared'
		'runtime-link=shared,static'
		'--stagedir=release'
		'--layout=system'
		'--build-dir=build'
		'--build-type=minimal'
	)

	# Get the number of parallel jobs from makeflags
	local JOBS="$(sed -e 's/.*\(-j *[0-9]\+\).*/\1/' <<< ${MAKEFLAGS})"

	# Build package
	./b2 stage $JOBS "${build_opts[@]}"
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Boost                                                                  #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_boost() {
	pkgdesc='Free peer-reviewed portable C++ source libraries - development headers'
	depends=("boost-libs=$pkgver")

	# Change directory to source directory
	cd ${pkgbase}_${pkgver//./_}

	# Get the number of parallel jobs from makeflags
	local JOBS="$(sed -e 's/.*\(-j *[0-9]\+\).*/\1/' <<< ${MAKEFLAGS})"

	# Install files
	./b2 install $JOBS --prefix=$pkgdir/usr

	# Install boostbook which is needed by quickbook
	mkdir -p $pkgdir/usr/share/boostbook
	cp -a tools/boostbook/{xsl,dtd} $pkgdir/usr/share/boostbook

	# Install license file
	install -Dm644 LICENSE_1_0.txt $pkgdir/usr/share/licenses/boost/LICENSE

	# Change directory to target directory
	cd $pkgdir

	# Split 'boost-libs' package
	mkdir -p $srcdir/boost-libs/usr/lib
	mv usr/lib/*.so* $srcdir/boost-libs/usr/lib || true
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Boost-libs                                                             #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_boost-libs() {
	pkgdesc='Free peer-reviewed portable C++ source libraries - runtime libraries'
	depends=(
		'bzip2'
		'gcc-libs'
		'glibc'
		'icu>=55.1'
		#'openmpi'
		'xz'
		'zlib'
		'zstd'
	)
	provides=(
		'libboost_atomic.so'
		'libboost_chrono.so'
		'libboost_container.so'
		'libboost_context.so'
		'libboost_contract.so'
		'libboost_coroutine.so'
		'libboost_date_time.so'
		'libboost_fiber.so'
		'libboost_filesystem.so'
		'libboost_graph.so'
		'libboost_graph_parallel.so'
		'libboost_iostreams.so'
		'libboost_locale.so'
		'libboost_log.so'
		'libboost_log_setup.so'
		'libboost_math_c99.so'
		'libboost_math_c99f.so'
		'libboost_math_c99l.so'
		'libboost_math_tr1.so'
		'libboost_math_tr1f.so'
		'libboost_math_tr1l.so'
		'libboost_mpi.so'
		#'libboost_numpy27.so'
		#'libboost_numpy38.so'
		'libboost_prg_exec_monitor.so'
		'libboost_program_options.so'
		#'libboost_python27.so'
		#'libboost_python38.so'
		'libboost_random.so'
		'libboost_regex.so'
		'libboost_serialization.so'
		'libboost_stacktrace_addr2line.so'
		'libboost_stacktrace_basic.so'
		'libboost_stacktrace_noop.so'
		'libboost_system.so'
		'libboost_thread.so'
		'libboost_timer.so'
		'libboost_type_erasure.so'
		'libboost_unit_test_framework.so'
		'libboost_wave.so'
		'libboost_wserialization.so'
	)

	# Copy files
	mv $srcdir/boost-libs/* $pkgdir

	# Install license file
	install -Dm644 ${pkgbase}_${pkgver//./_}/LICENSE_1_0.txt \
	$pkgdir/usr/share/licenses/boost-libs/LICENSE
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
