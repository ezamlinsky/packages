################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('xfce4-session')
pkgver=4.12.1
pkgrel=11
pkgdesc='A session manager for Xfce'
url='http://www.xfce.org/'
license=('GPL2')
arch=('x86_64')
groups=('xfce4')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'atk'
	'cairo'
	'dbus'
	'dbus-glib'
	'gdk-pixbuf2'
	'glib2'
	'glibc'
	'gtk2'
	'hicolor-icon-theme'
	'libice'
	'libsm'
	'libwnck'
	'libx11'
	'libxfce4ui'
	'libxfce4util'
	'pango'
	'polkit'
	'polkit-gnome'
	'upower'
	'which'
	'xfconf'
	'xorg-iceauth'
	'xorg-xinit'
	'xorg-xrdb'
)
makedepends=('intltool')
optdepends=(
	'gnome-keyring: for keyring support when GNOME compatibility is enabled'
	'xscreensaver: for locking screen with xflock4'
	'gnome-screensaver: for locking screen with xflock4'
	'xlockmore: for locking screen with xflock4'
	'slock: for locking screen with xflock4'
)

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
replaces=('xfce-utils')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://archive.xfce.org/src/xfce/xfce4-session/${pkgver%.*}/xfce4-session-$pkgver.tar.bz2"
	'0001-Pass-VT-number-to-xinit-if-XDG_VTNR-is-set.patch'
	'0002-Make-verbose-logging-conditional-Bug-11698.patch'
	'0003-source-system-xinitrc-scripts.patch'
	'0004-screenlockers.patch'
	'xfce-polkit-gnome-authentication-agent-1.desktop'
)
sha256sums=(
	'97d7f2a2d0af7f3623b68d1f04091e02913b28f9555dab8b0d26c8a1299d08fd'
	'7ea702a1d3391d36958cff57496ba2474592dd915cf7569a65501522c2410cf1'
	'6bbd714008f31198f9631cdfb3ed8aac1c780f00d15b81c32c00860ae37521a6'
	'a717a0ee6289c9ce06a8e18c6a7cb5a6d6f1769c05c456a6a84843df6a0111da'
	'1d4063889af073c3de221e0bbd3cacc11dfc95ee11f6e530c6e35f7714a6e295'
	'74c94c5f7893d714e04ec7d8b8520c978a5748757a0cdcf5128492f09f31b643'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Apply patches
	patch -Np1 -i ../0001-Pass-VT-number-to-xinit-if-XDG_VTNR-is-set.patch
	patch -Np1 -i ../0002-Make-verbose-logging-conditional-Bug-11698.patch
	patch -Np1 -i ../0003-source-system-xinitrc-scripts.patch
	patch -Np1 -i ../0004-screenlockers.patch

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-legacy-sm'
		'--disable-debug'
		'--enable-debug=no'
		'--with-backend=linux'
		'--with-x'

	# 'polkit-gnome' related options
		'--enable-polkit'
		#'--disable-polkit'

	# 'upower' related options
		'--enable-upower'
		#'--disable-upower'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Provide a default PolicyKit Authentication Agent (FS#42569)
	install -D $srcdir/xfce-polkit-gnome-authentication-agent-1.desktop \
	$pkgdir/etc/xdg/autostart/xfce-polkit-gnome-authentication-agent-1.desktop
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
