################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('exo')
pkgver=0.12.8
pkgrel=1
pkgdesc='Application library for Xfce'
url='http://www.xfce.org/'
license=('GPL2' 'LGPL')
arch=('x86_64')
groups=('xfce4')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'atk'
	'cairo'
	'gdk-pixbuf2'
	'glib2'
	'glibc'
	'gtk2'
	'gtk3'
	'hicolor-icon-theme'
	'libx11'
	'libxfce4ui'
	'libxfce4util'
	'pango'
	'perl'
	'sh'
)
makedepends=(
	'gtk-doc'
	'intltool'
	'perl-uri'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=("https://archive.xfce.org/src/xfce/exo/${pkgver%.*}/exo-$pkgver.tar.bz2")
sha256sums=('3400a2e64aa41ede25bedaca4909bde7ea4f8698d9598d1b80f02a40ac89fcde')

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--enable-gio-unix'
		'--disable-debug'
		'--enable-gtk-doc'
		'--with-x'

	# 'gtk2' related options
		'--enable-gtk2'
		#'--disable-gtk2'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
