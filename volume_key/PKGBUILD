################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('volume_key')
pkgver=0.3.11
pkgrel=2
pkgdesc='A library for manipulating storage volume encryption keys and storing them separately from volumes to handle forgotten passphrases'
url='https://pagure.io/volume_key'
license=('GPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'cryptsetup'
	'glib2'
	'glibc'
	'gpgme'
	'libutil-linux'
	'nspr'
	'nss'
)
makedepends=(
	'python'
	'swig'
)
optdepends=('python: for python bindings')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=("https://github.com/felixonmars/volume_key/archive/volume_key-$pkgver.tar.gz")
sha512sums=('594995cda12562a6beb6a404e2d6a152fcca6c08c1c6eae4d3c7ded8b4a55d43ddef1e45956075c7168c5f4919e38c87f79caf1af4e9cbc371eabc5228259f07')

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgname-$pkgver

	# Run automatic configure script
	autoreconf -fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname-$pkgname-$pkgver

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 COPYING $pkgdir/usr/share/licenses/volume_key/LICENSE

	# Remove python library
	rm -fr $pkgdir/usr/lib/python*
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
