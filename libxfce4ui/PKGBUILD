################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('libxfce4ui')
pkgver=4.14.1
pkgrel=2
pkgdesc='Commonly used Xfce widgets among Xfce applications'
url='https://www.xfce.org/'
license=('GPL2')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'atk'
	'cairo'
	'gdk-pixbuf2'
	'glade'
	'glib2'
	'glibc'
	'gtk2'
	'gtk3'
	'hicolor-icon-theme'
	'libice'
	'libsm'
	'libx11'
	'libxfce4util'
	'pango'
	'startup-notification'
	'xfconf'
)
makedepends=(
	'gobject-introspection'
	'gtk-doc'
	'intltool'
	'vala'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=("http://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2")
sha256sums=('c449075eaeae4d1138d22eeed3d2ad7032b87fb8878eada9b770325bed87f2da')

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--enable-keyboard-library'
		'--disable-static'
		'--disable-tests'
		'--disable-gtk-doc'
		'--disable-debug'
		'--enable-debug=no'
		'--enable-visibility'
		'--with-x'
		'--with-vendor-info=Arch_Linux'

	# 'glade' related options
		'--enable-gladeui'
		#'--disable-gladeui'

	# 'gtk2' related options
		'--enable-gtk2'
		#'--disable-gtk2'

	# 'startup-notification' related options
		'--enable-startup-notification'
		#'--disable-startup-notification'

	# 'gobject-introspection' related options
		#'--enable-introspection=yes'
		'--enable-introspection=no'

	# 'vala' related options
		#'--enable-vala=yes'
		'--enable-vala=no'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
