################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('procps-ng')
pkgver=3.3.16
pkgrel=1
pkgdesc='Utilities for monitoring your system and its processes'
url='https://gitlab.com/procps-ng/procps'
license=('GPL' 'LGPL')
arch=('x86_64')
groups=('base')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'glibc'
	#'libselinux'
	'ncurses'
	'systemd-libs'
)
makedepends=(
	'intltool'
	'systemd'
)

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
provides=(
	'procps'
	'sysvinit-tools'
)
conflicts=(
	'procps'
	'sysvinit-tools'
)
replaces=(
	'procps'
	'sysvinit-tools'
)

#==============================================================================#
#       Package miscellaneous options                                          #
#==============================================================================#
install='install'

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=("https://downloads.sourceforge.net/project/$pkgname/Production/${pkgname}-${pkgver}.tar.xz")
sha256sums=('925eacd65dedcf9c98eb94e8978bbfb63f5de37294cc1047d81462ed477a20af')

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Use ncurses instead of ncursesw
	sed 's:<ncursesw/:<:g' -i watch.c
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-w-from'
		'--disable-kill'
		'--enable-skill'
		'--enable-pidof'
		'--enable-numa'
		'--enable-watch8bit'
		'--enable-sigwinch'
		'--enable-modern-top'
		'--enable-wide-percent'
		'--enable-wide-memory'
		'--enable-w-from'
		'--disable-examples'
		'--with-elogind'

	# 'libsystemd' related options
		'--with-systemd'
		#'--without-systemd'

	# 'ncurses' related options
		'--with-ncurses'
		#'--without-ncurses'

	# 'libselinux' related options
		#'--enable-libselinux'
		'--disable-libselinux'
	)

	# Change compile flags
	export CFLAGS+=' -fno-lto'
	export CXXFLAGS="${CFLAGS}"

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
