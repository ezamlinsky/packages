################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('linux-api-headers')
pkgver=5.1
pkgrel=1
pkgdesc='Kernel headers sanitized for use in userspace'
url='http://www.gnu.org/software/libc'
license=('GPL2')
arch=('any')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://www.kernel.org/pub/linux/kernel/v${pkgver:0:1}.x/linux-$pkgver.tar.xz"
	"https://www.kernel.org/pub/linux/kernel/v${pkgver:0:1}.x/linux-$pkgver.tar.sign"
)
md5sums=(
	'15fbdff95ff98483069ac6e215b9f4f9'
	'SKIP'
)

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd linux-$pkgver

	# Build package
	make mrproper
	make headers_check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd linux-$pkgver

	# Install files
	make INSTALL_HDR_PATH=$pkgdir/usr headers_install

	# Use headers from libdrm instead of supplied by default
	rm -r $pkgdir/usr/include/drm

	# Clean-up unnecessary files generated during install
	find $pkgdir \( -name .install -o -name ..install.cmd \) -delete
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
