################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('glibc')
pkgver=2.31
pkgrel=1
pkgdesc='GNU C Library'
url='https://www.gnu.org/software/libc'
license=('GPL' 'LGPL')
arch=('x86_64')
groups=('base')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'filesystem'
	#'gd'
	#'libselinux'
	'linux-api-headers>=4.10'
	#'nss'
	'tzdata'
)
makedepends=(
	'git'
	'python'
)

#==============================================================================#
#       Package miscellaneous options                                          #
#==============================================================================#
backup=(
	'etc/gai.conf'
	'etc/locale.gen'
	'etc/nscd.conf'
)
options=('emptydirs' 'staticlibs')
install='glibc.install'

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_commit='a6aaabd036d735a1b412f441bf6c706832655598'
source=(
	"https://ftp.gnu.org/gnu/glibc/glibc-$pkgver.tar.xz"
	"https://ftp.gnu.org/gnu/glibc/glibc-$pkgver.tar.xz.sig"
	'locale.gen.txt'
	'locale-gen'
	'lib32-glibc.conf'
	'sdt.h'
	'sdt-config.h'
	'bz20338.patch'
)
md5sums=(
	'78a720f17412f3c3282be5a6f3363ec6'
	'SKIP'
	'07ac979b6ab5eeb778d55f041529d623'
	'476e9113489f93b348b21e144b6a8fcf'
	'6e052f1cb693d5d3203f50f9d4e8c33b'
	'91fec3b7e75510ae2ac42533aa2e695e'
	'680df504c683640b02ed4a805797c0b2'
	'430673eccc78e52c249aa4b0f1786450'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Apply patches
	patch -p1 -i $srcdir/bz20338.patch
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-werror'
		'--disable-profile'
		'--disable-multi-arch'
		'--enable-sanity-checks'
		'--enable-timezone-tools'
		'--enable-hidden-plt'
		'--enable-pt_chown'
		'--enable-stackguard-randomization'
		'--enable-stack-protector=strong'
		'--enable-lock-elision=yes'
		'--enable-tunables=yes'
		'--enable-add-ons'
		'--enable-bind-now'
		'--enable-static-pie'
		'--enable-mathvec'
		'--enable-crypt'
		'--enable-cet'
		'--enable-nscd'
		'--enable-build-nscd'
		'--with-headers=/usr/include'
		'--with-bugurl=https://bugs.archlinux.org'

	# 'gd' related options
		#'--with-gd'
		'--without-gd'

	# 'libselinux' related options
		#'--with-selinux'
		'--without-selinux'

	# 'nss' related options
		#'--enable-nss-crypt'
		'--disable-nss-crypt'
		#'--enable-static-nss'
		#'--disable-static-nss'
	)

	# Change directory to build directory
	mkdir -p build && cd build

	# Correct installation directories
	echo "slibdir=/usr/lib" >> configparms
	echo "rtlddir=/usr/lib" >> configparms
	echo "sbindir=/usr/bin" >> configparms
	echo "rootsbindir=/usr/bin" >> configparms

	# Change compile flags
	export CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}
	export CFLAGS=${CFLAGS/-fno-plt/}
	export CXXFLAGS=${CXXFLAGS/-fno-plt/}
	export LDFLAGS=${LDFLAGS/,-z,now/}
	export CFLAGS+=' -fno-lto'
	export CXXFLAGS+=' -fno-lto'

	# Run package configuration script
	../configure "${config_opts[@]}"

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname-$pkgver/build

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver/build

	# Install files
	make DESTDIR=$pkgdir install

	# Install nscd
	install -dm755 $pkgdir/var/db/nscd
	install -Dm644 ../nscd/nscd.conf $pkgdir/etc/nscd.conf
	install -Dm644 ../nscd/nscd.service $pkgdir/usr/lib/systemd/system/nscd.service
	install -Dm644 ../nscd/nscd.tmpfiles $pkgdir/usr/lib/tmpfiles.d/nscd.conf

	# Install 'gai.conf'
	install -Dm644 ../posix/gai.conf $pkgdir/etc/gai.conf

	# Install 'locale-gen'
	install -dm755 $pkgdir/usr/lib/locale
	install -Dm644 $srcdir/locale.gen.txt $pkgdir/etc/locale.gen
	install -Dm755 $srcdir/locale-gen $pkgdir/usr/bin/locale-gen

	# Provide tracing probes to libstdc++ for exceptions, possibly for other
	# libraries too. Useful for gdb's catch command.
	install -Dm644 $srcdir/sdt.h $pkgdir/usr/include/sys/sdt.h
	install -Dm644 $srcdir/sdt-config.h $pkgdir/usr/include/sys/sdt-config.h

	# Generate list of supported locales
	sed	-e '1,3d' \
		-e 's|/| |g' \
		-e 's|\\| |g' \
		-e 's|^|#|g' \
		../localedata/SUPPORTED >> $pkgdir/etc/locale.gen

	# Remove 'ld.so.cache'
	rm -f $pkgdir/etc/ld.so.{cache,conf}

	# Remove files are shipped in tzdata
	rm -f $pkgdir/usr/bin/{tzselect,zdump,zic}
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
