################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('libva')
pkgver=2.7.0
pkgrel=1
pkgdesc='Video Acceleration (VA) API for Linux'
url='https://01.org/linuxmedia/vaapi'
license=('MIT')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'glibc'
	'libdrm'
	'libgl'
	'libx11'
	'libxext'
	'libxfixes'
	#'wayland'
)
makedepends=(
	'git'
	'libglvnd'
	'mesa'
	'meson'
)
optdepends=(
	'intel-media-driver: backend for Intel GPUs (>= Broadwell)'
	'libva-intel-driver: backend for Intel GPUs (<= Haswell)'
	'libva-vdpau-driver: backend for Nvidia and AMD GPUs'
)

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
provides=(
	'libva-drm.so'
	'libva-glx.so'
	'libva-wayland.so'
	'libva-x11.so'
	'libva.so'
)

#==============================================================================#
#       Package miscellaneous options                                          #
#==============================================================================#
backup=('etc/libva.conf')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=("git+https://github.com/intel/libva.git#tag=c2be378312b0a17c796509defae42afba7351272")
sha256sums=('SKIP')

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname

	# Default configure options
	config_opts=(
		'-Denable_docs=true'
		'-Denable_va_messaging=true'

	# 'libdrm' related options
		'-Ddisable_drm=false'
		#'-Ddisable_drm=true'

	# 'libgl' related options
		'-Dwith_glx=yes'

	# 'libx11 && libxext && libxfixes && libdrm' related options
		'-Dwith_x11=yes'

	# 'wayland' related options
		#'-Dwith_wayland=yes'
		'-Dwith_wayland=no'
	)

	# Change compile flags
	export CFLAGS+=' -DENABLE_VA_MESSAGING'

	# Run package configuration script
	arch-meson build "${config_opts[@]}"

	# Build package
	ninja -C build
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname

	# Install files
	DESTDIR=$pkgdir ninja -C build install

	# Install license file
	install -Dm644 COPYING $pkgdir/usr/share/licenses/libva/LICENSE

	# Change directory to target directory
	cd $pkgdir

	# Install 'libva.conf' config file
	mkdir -p etc
	echo 'LIBVA_MESSAGING_LEVEL=1' > etc/libva.conf
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
