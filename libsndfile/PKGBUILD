################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('libsndfile')
pkgver=1.0.28
pkgrel=3
pkgdesc='A C library for reading and writing files containing sampled sound'
url='http://www.mega-nerd.com/libsndfile'
license=('LGPL2.1')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'alsa-lib'
	'flac'
	'glibc'
	'libogg'
	'libvorbis'
	'sqlite'
)
checkdepends=('python2')

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
provides=('libsndfile.so')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"http://www.mega-nerd.com/$pkgname/files/$pkgname-$pkgver.tar.gz"
	"http://www.mega-nerd.com/$pkgname/files/$pkgname-$pkgver.tar.gz.asc"
)
sha512sums=(
	'890731a6b8173f714155ce05eaf6d991b31632c8ab207fbae860968861a107552df26fcf85602df2e7f65502c7256c1b41735e1122485a3a07ddb580aa83b57f'
	'SKIP'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--enable-octave'
		'--enable-full-suite'
		'--enable-cpu-clip'
		'--enable-external-libs'
		'--enable-stack-smash-protection'

	# 'alsa-lib' related options
		'--enable-alsa'
		#'--disable-alsa'

	# 'sqlite' related options
		'--enable-sqlite'
		#'--disable-sqlite'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
