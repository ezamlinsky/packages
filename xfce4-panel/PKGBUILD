################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('xfce4-panel')
pkgver=4.14.0
pkgrel=1
pkgdesc='Panel for the Xfce desktop environment'
url='http://www.xfce.org/'
license=('GPL2')
arch=('x86_64')
groups=('xfce4')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'atk'
	'cairo'
	'dbus'
	'dbus-glib'
	'desktop-file-utils'
	'exo'
	'garcon'
	'gcc-libs'
	'gdk-pixbuf2'
	'glib2'
	'glibc'
	'gtk2'
	'gtk3'
	'hicolor-icon-theme'
	'libwnck3'
	'libx11'
	'libxext'
	'libxfce4ui'
	'libxfce4util'
	'pango'
	'xfconf'
)
makedepends=(
	'gobject-introspection'
	'gtk-doc'
	'intltool'
	'vala'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=("http://archive.xfce.org/src/xfce/xfce4-panel/${pkgver%.*}/xfce4-panel-$pkgver.tar.bz2")
sha256sums=('d5f8f3bd7e142139987e5e62cda26135a424baabb5e47aa0c23b4169d0a66ef4')

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--enable-gio-unix'
		'--disable-gtk-doc'
		'--disable-debug'
		'--enable-debug=no'
		'--enable-introspection=no'	#Enable introspection for this build
		'--with-x'

	# 'gtk2' related options
		'--enable-gtk2'
		#'--disable-gtk2'

	# 'vala' related options
		#'--enable-vala=yes'
		'--enable-vala=no'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
