################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('snappy')
pkgver=1.1.7
pkgrel=1
pkgdesc='A fast compressor/decompressor library'
url='https://google.github.io/snappy/'
license=('BSD')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'gcc-libs'
	'glibc'
)
makedepends=(
	'clang'
	'cmake'
)
checkdepends=('zlib')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://github.com/google/snappy/archive/$pkgver.tar.gz"
	'snappy.pc'
)
md5sums=(
	'ee9086291c9ae8deb4dac5e0b85bf54a'
	'4b491e29a8142fedd11472306f5c1192'
)

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		"-DCMAKE_C_FLAGS='$CFLAGS'"
		"-DCMAKE_CXX_FLAGS='$CXXFLAGS'"
		"-DCMAKE_LD_FLAGS='$LDFLAGS'"
		'-DCMAKE_INSTALL_PREFIX=/usr'
		'-DCMAKE_INSTALL_LIBDIR=lib'
		'-DCMAKE_BUILD_TYPE=Release'
		'-DBUILD_SHARED_LIBS=ON'
		'-DSNAPPY_BUILD_TESTS:BOOL=ON'
	)

	# Create build directory
	mkdir -p build && cd build

	# Run package configuration script
	cmake .. "${config_opts[@]}"

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname-$pkgver/build

	# Run internal checks
	make test
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver/build

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 ../COPYING $pkgdir/usr/share/licenses/snappy/LICENSE

	# Change directory to target directory
	cd $pkgdir

	# Install missing 'snappy.pc' file
	install -Dm644 $srcdir/snappy.pc $pkgdir/usr/lib/pkgconfig/snappy.pc
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
