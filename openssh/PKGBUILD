################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('openssh')
pkgver=8.2p1
pkgrel=3
pkgdesc='Premier connectivity tool for remote login with the SSH protocol'
url='https://www.openssh.com/portable.html'
license=('custom:BSD')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	#'audit'
	'glibc'
	#'krb5'
	#'ldns'
	'libedit'
	#'libfido2'
	#'libselinux'
	'openssl'
	'pam'
	'sh'
	'zlib'
)
makedepends=('linux-headers')
optdepends=(
	'xorg-xauth: X11 forwarding'
	'x11-ssh-askpass: input passphrase in X'
)

#==============================================================================#
#       Package miscellaneous options                                          #
#==============================================================================#
backup=(
	'etc/pam.d/sshd'
	'etc/ssh/ssh_authorized_keys'
	'etc/ssh/ssh_banner'
	'etc/ssh/ssh_config'
	'etc/ssh/sshd_config'
)
install='install'

#==============================================================================#
#       Package miscellaneous options                                          #
#==============================================================================#
options=('emptydirs')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://ftp.openbsd.org/pub/OpenBSD/OpenSSH/portable/$pkgname-$pkgver.tar.gz"
	"https://ftp.openbsd.org/pub/OpenBSD/OpenSSH/portable/$pkgname-$pkgver.tar.gz.asc"
	'sshdgenkeys.service'
	'sshd.service'
	'sshd.conf'
	'sshd.pam'
	'glibc-2.31.patch'
)
sha256sums=(
	'43925151e6cf6cee1450190c0e9af4dc36b41c12737619edff8bcebdff64e671'
	'SKIP'
	'4031577db6416fcbaacf8a26a024ecd3939e5c10fe6a86ee3f0eea5093d533b7'
	'e40f8b7c8e5e2ecf3084b3511a6c36d5b5c9f9e61f2bb13e3726c71dc7d4fbc7'
	'4effac1186cc62617f44385415103021f72f674f8b8e26447fc1139c670090f6'
	'64576021515c0a98b0aaf0a0ae02e0f5ebe8ee525b1e647ab68f369f81ecd846'
	'25b4a4d9e2d9d3289ef30636a30e85fa1c71dd930d5efd712cca1a01a5019f93'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Apply patches
	patch -p1 -i $srcdir/glibc-2.31.patch

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib/ssh'
		'--sysconfdir=/etc/ssh'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--enable-strip'
		'--enable-pkcs11'
		'--enable-lastlog'
		'--enable-utmp'
		'--enable-utmpx'
		'--enable-wtmp'
		'--enable-wtmpx'
		'--enable-libutil'
		'--enable-pututline'
		'--enable-pututxline'
		'--enable-etc-default-login'
		'--with-pie'
		'--with-stackprotect'
		'--with-hardening'
		'--with-shadow'
		'--with-md5-passwords'
		'--with-mantype=man'
		'--with-sandbox=seccomp_filter'
		'--with-privsep-user=nobody'
		'--with-xauth=/usr/bin/xauth'
		'--with-maildir=/var/spool/mail'
		'--with-pid-dir=/run'
		'--with-default-path=/usr/local/sbin:/usr/local/bin:/usr/sbin:/usr/bin'
		'--without-ipaddr-display'
		'--without-4in6'

	# 'audit' related options
		#'--with-audit'
		'--without-audit'

	# 'krb5' related options
		#'--with-kerberos5'
		'--without-kerberos5'

	# 'ldns' related options
		#'--with-ldns'
		'--without-ldns'

	# 'libedit' related options
		'--with-libedit'
		#'--without-libedit'

	# 'libfido2' related options
		#'--enable-security-key'
		'--disable-security-key'
		#'--with-security-key-builtin'

	# 'libselinux' related options
		#'--with-selinux'
		'--without-selinux'

	# 'openssl' related options
		'--with-openssl'
		#'--without-openssl'
		'--with-ssl-engine'
		'--with-openssl-header-check'

	# 'pam' related options
		'--with-pam'
		#'--without-pam'

	# 'zlib' related options
		'--with-zlib'
		#'--without-zlib'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run internal checks
	make -k tests
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Instal vendor's files
	install -Dm644 $srcdir/sshdgenkeys.service $pkgdir/usr/lib/systemd/system/sshdgenkeys.service
	install -Dm644 $srcdir/sshd.service $pkgdir/usr/lib/systemd/system/sshd.service
	install -Dm644 $srcdir/sshd.conf $pkgdir/usr/lib/tmpfiles.d/sshd.conf
	install -Dm644 $srcdir/sshd.pam $pkgdir/etc/pam.d/sshd

	# Install other files
	install -Dm755 contrib/findssl.sh $pkgdir/usr/bin/findssl.sh
	install -Dm755 contrib/ssh-copy-id $pkgdir/usr/bin/ssh-copy-id
	install -Dm644 contrib/ssh-copy-id.1 $pkgdir/usr/share/man/man1/ssh-copy-id.1

	# Install license file
	install -Dm644 LICENCE $pkgdir/usr/share/licenses/openssh/LICENCE

	# Create empty file for authorized keys
	touch $pkgdir/etc/ssh/ssh_authorized_keys

	# Create banner file for ssh daemon
	echo '
                             Welcome to Arch Linux!
                    Please authenticate yourself to log in.' > $pkgdir/etc/ssh/ssh_banner

	# Change default 'sshd_config' file
	sed	-e '/^#ChallengeResponseAuthentication yes$/c ChallengeResponseAuthentication no' \
		-e '/^#PrintMotd yes$/c PrintMotd no # pam does that' \
		-e '/^#UsePAM no$/c UsePAM yes' \
		-i $pkgdir/etc/ssh/sshd_config

	# Set slogin man page to be symlink to ssh man
	ln -sf ssh.1.gz $pkgdir/usr/share/man/man1/slogin.1.gz
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
