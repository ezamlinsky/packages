################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('xorg-bdftopcf')
pkgver=1.1
pkgrel=1
pkgdesc='Convert X font from Bitmap Distribution Format to Portable Compiled Format'
url='https://xorg.freedesktop.org/'
license=('custom')
arch=('x86_64')
groups=('xorg' 'xorg-apps')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=('glibc')
makedepends=(
	'fontsproto'
	'xorg-util-macros'
	'xproto'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://xorg.freedesktop.org/archive/individual/app/bdftopcf-$pkgver.tar.bz2"
	"https://xorg.freedesktop.org/archive/individual/app/bdftopcf-$pkgver.tar.bz2.sig"
)
sha256sums=(
	'4b4df05fc53f1e98993638d6f7e178d95b31745c4568cee407e167491fd311a2'
	'SKIP'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd bdftopcf-$pkgver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd bdftopcf-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd bdftopcf-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 COPYING $pkgdir/usr/share/licenses/xorg-bdftopcf/COPYING
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
