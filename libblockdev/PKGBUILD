################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname='libblockdev'
pkgver=2.23
pkgrel=1
pkgdesc='A library for manipulating block devices'
url='https://github.com/rhinstaller/libblockdev'
license=('LGPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'cryptsetup'
	'device-mapper'
	#'dmraid'
	'glib2'
	'glibc'
	'kmod'
	'libbytesize'
	'libutil-linux'
	#'libyaml'
	#'ndctl'
	'nss'
	'parted'
	'systemd-libs'
	'volume_key'
)
makedepends=(
	'autoconf-archive'
	'gobject-introspection'
	'gtk-doc'
	'python'
	'python2'
	'systemd'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=("$pkgname-$pkgver.tar.gz::https://github.com/rhinstaller/libblockdev/archive/$pkgver-1.tar.gz")
sha512sums=('e1e9976d24bdd8775310c9b25c31eb3b0e2d06a295b75f0c281def694104664f42abbbec307fbeb7c960ba5059299d0da66aa7afb26850c3640a8e73ea777aaf')

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver-$pkgrel

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver-$pkgrel

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-tests'
		'--enable-debug=no'
		'--enable-introspection=no'
		'--with-gtk-doc'
		'--with-bcache'
		'--with-loop'
		'--with-swap'

	# 'cryptsetup && nss && volume_key' related options
		'--with-crypto'
		#'--without-crypto'
		'--with-escrow'
		#'--without-escrow'

	# 'device-mapper' related options
		'--with-dm'
		#'--without-dm'
		#'--with-lvm'
		'--without-lvm'
		#'--with-lvm_dbus'
		'--without-lvm_dbus'
		#'--with-mpath'
		'--without-mpath'

	# 'dmraid && device-mapper' related options
		#'--with-dmraid'
		'--without-dmraid'

	# 'libbytesize' related options
		'--with-tools'
		#'--without-tools'
		'--with-btrfs'
		#'--without-btrfs'
		'--with-mdraid'
		#'--without-mdraid'
		'--with-kbd'
		#'--without-kbd'

	# 'libyaml && libbytesize' related options
		#'--with-vdo'
		'--without-vdo'

	# 'ndctl' related options
		#'--with-nvdimm'
		'--without-nvdimm'

	# 'parted' related option
		'--with-part'
		#'--without-part'
		'--with-fs'
		#'--without-fs'

	# 'python' related options
		#'--with-python3'
		'--without-python3'

	# 'python2' related options
		#'--with-python2'
		'--without-python2'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname-$pkgver-$pkgrel

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver-$pkgrel

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 LICENSE $pkgdir/usr/share/licenses/libblockdev/LICENSE
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
