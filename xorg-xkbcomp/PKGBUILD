################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('xorg-xkbcomp')
pkgver=1.4.3
pkgrel=1
pkgdesc='X Keyboard description compiler'
url='https://xorg.freedesktop.org/'
license=('custom')
arch=('x86_64')
groups=('xorg-apps' 'xorg')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'glibc'
	'libx11'
	'libxkbfile'
)
makedepends=(
	'git'
	'xorg-util-macros'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://xorg.freedesktop.org/releases/individual/app/xkbcomp-$pkgver.tar.bz2"
	"https://xorg.freedesktop.org/releases/individual/app/xkbcomp-$pkgver.tar.bz2.sig"
)
sha512sums=(
	'827713c0413aecdcad2b61edb7b8c7c7a002e18505b9041f570e2f680907193cb5ff8a5b424695e21110b2d06c145cbf0e397e52347421ee946f06e2a51f135d'
	'SKIP'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd xkbcomp-$pkgver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd xkbcomp-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd xkbcomp-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 COPYING $pkgdir/usr/share/licenses/xorg-xkbcomp/COPYING
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
