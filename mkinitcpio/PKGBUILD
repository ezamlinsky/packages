################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('mkinitcpio')
pkgver=27
pkgrel=3
pkgdesc='Modular initramfs image creation utility'
url='https://projects.archlinux.org/mkinitcpio.git/'
license=('GPL')
arch=('any')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'awk'
	'bash'
	'coreutils'
	'filesystem>=2011.10-1'
	'findutils'
	'grep'
	'gzip'
	'kmod'
	'libarchive'
	'mkinitcpio-busybox>=1.19.4-2'
	'systemd'
	'util-linux>=2.23'
)
optdepends=(
	'bzip2: Use bzip2 compression for the initramfs image'
	'lz4: Use lz4 compression for the initramfs image'
	'lzop: Use lzo compression for the initramfs image'
	'mkinitcpio-nfs-utils: Support for root filesystem on NFS'
	'xz: Use lzma or xz compression for the initramfs image'
)

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
provides=('initramfs')

#==============================================================================#
#       Package miscellaneous options                                          #
#==============================================================================#
backup=('etc/mkinitcpio.conf')
install='mkinitcpio.install'

#==============================================================================#
#       Package miscellaneous options                                          #
#==============================================================================#
options=('emptydirs')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://sources.archlinux.org/other/mkinitcpio/mkinitcpio-$pkgver.tar.gz"
	"https://sources.archlinux.org/other/mkinitcpio/mkinitcpio-$pkgver.tar.gz.sig"
	'0001-mkinitcpio-remove-preset-pacsave.patch'
	'0002-mkinitcpio-fix-builtin.patch'
)
sha256sums=(
	'e6bff1cb78b677538eb9aace900b715fd59de8fc210b74fb9d899dfaa32bc354'
	'SKIP'
	'845569fa760f70c868ecb3dc8ae9667287970526dddaf403fdafcb716e8b3d51'
	'd097f3df15ba9fefd3771f9f784e1c31c8d5ff1d9d6885a46fc6b5188e419796'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Apply patches
	patch -Np1 -i $srcdir/0001-mkinitcpio-remove-preset-pacsave.patch
	patch -Np1 -i $srcdir/0002-mkinitcpio-fix-builtin.patch
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
