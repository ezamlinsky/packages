################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname='kicad'
pkgver=5.1.5
pkgrel=1
pkgdesc='Electronic schematic and printed circuit board (PCB) design tools'
url='http://kicad-pcb.org/'
license=('GPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'cairo'
	'desktop-file-utils'
	'gcc-libs'
	'glew'
	'glibc'
	'glu'
	'hicolor-icon-theme'
	'libglvnd'
	'ngspice'
	'openssl'
	'pixman'
	#'python'
	'wxgtk-common'
	'wxgtk3'
)
makedepends=(
	'boost'
	'cmake'
	'gettext'
	'glm'
	'mesa'
	'opencascade'
	'python-wxpython'
	'swig'
	'zlib'
)
optdepends=(
	'kicad-library: for footprints and symbols'
	'kicad-library-3d: for 3d models of components'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://launchpad.net/$pkgname/5.0/$pkgver/+download/$pkgname-$pkgver.tar.xz"
	"kicad-i18n.$pkgver.tar.gz::https://github.com/KiCad/$pkgname-i18n/archive/$pkgver.tar.gz"
)
sha512sums=(
	'5ca19e219da07a906f13e236849eb4497c9f827726a5cd13aceffe1d8c7dee480823bf3e949527f59a805eb3b8176fad51576ae663dd07b10e2822f2b37866a5'
	'68e6602fcc7e73c22a594b45ecf53a956af8a134e28e86242bfaa4087df21c04b24ae4df9928f00c2cee7eb0631a02a4de1cd09547b36a466fb324f983ea5363'
)

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to kicad source directory
	cd $srcdir/$pkgbase-$pkgver

	# Default configure options
	config_opts=(
		"-DCMAKE_C_FLAGS='$CFLAGS -fno-lto'"
		"-DCMAKE_CXX_FLAGS='$CXXFLAGS -fno-lto'"
		"-DCMAKE_LD_FLAGS='$LDFLAGS'"
		'-DCMAKE_INSTALL_PREFIX=/usr'
		'-DCMAKE_INSTALL_LIBDIR=lib'
		'-DKICAD_INSTALL_DEMOS=OFF'
		'-DKICAD_BUILD_QA_TESTS=ON'
		'-DCMAKE_BUILD_TYPE=Release'
		'-DBUILD_GITHUB_PLUGIN=OFF'
		'-DUSE_WX_GRAPHICS_CONTEXT=OFF'
		'-DUSE_WX_OVERLAY=ON'
		'-DBoost_INCLUDE_DIR=/usr/include/boost'
		'-DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-gtk3'

	# 'ngspice' related options
		'-DKICAD_SPICE=ON'

	# 'opencascade' related options
		#'-DKICAD_USE_OCC=ON'
		'-DKICAD_USE_OCC=OFF'
		#'-DKICAD_USE_OCE=ON'
		'-DKICAD_USE_OCE=OFF'

	# 'python2' related options
		#'-DKICAD_SCRIPTING=ON'
		'-DKICAD_SCRIPTING=OFF'
		#'-DKICAD_SCRIPTING_ACTION_MENU=ON'
		'-DKICAD_SCRIPTING_ACTION_MENU=OFF'
		#'-DKICAD_SCRIPTING_MODULES=ON'
		'-DKICAD_SCRIPTING_MODULES=OFF'
		#'-DKICAD_SCRIPTING_WXPYTHON=ON'
		'-DKICAD_SCRIPTING_WXPYTHON=OFF'
		#'-DKICAD_SCRIPTING_WXPYTHON_PHOENIX=ON'
		'-DKICAD_SCRIPTING_WXPYTHON_PHOENIX=OFF'

	# 'python3' related options
		#'-DKICAD_SCRIPTING_PYTHON3=ON'
		'-DKICAD_SCRIPTING_PYTHON3=OFF'
	)

	# Create build directory
	mkdir -p build && cd build

	# Run package configuration script
	cmake .. "${config_opts[@]}"

	# Build package
	make

	# Change directory to internationalization source directory
	cd $srcdir/$pkgname-i18n-$pkgver

	# Default configure options
	config_opts=(
		'-DCMAKE_INSTALL_PREFIX=/usr'
		'-DCMAKE_BUILD_TYPE=Release'
	)

	# Create build directory
	mkdir -p build && cd build

	# Run package configuration script
	cmake .. "${config_opts[@]}"

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to kicad source directory
	cd $srcdir/$pkgbase-$pkgver/build

	# Install kicad files
	make DESTDIR=$pkgdir install

	# Change directory to internationalization source directory
	cd $srcdir/$pkgname-i18n-$pkgver/build

	# Install internationalization files
	make DESTDIR=$pkgdir install
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
