################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgbase='libsigc++'
pkgname=('libsigc++' 'libsigc++-docs')
pkgver=2.10.3
pkgrel=1
url='http://libsigc.sourceforge.net/'
license=('LGPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
makedepends=(
	'doxygen'
	'git'
	'graphviz'
	'libxslt'
	'meson'
	'mm-common'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_commit='88fdb3a14ec67de233fed22646fc9b14c24367f5'
source=("git+https://github.com/libsigcplusplus/libsigcplusplus#commit=$_commit")
sha256sums=('SKIP')

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd libsigcplusplus

	# Default configure options
	config_opts=(
		'-Dwarnings=min'
		'-Dvalidation=false'
		'-Dmaintainer-mode=true'
		'-Dbuild-documentation=true'
		'-Dbuild-deprecated-api=true'
		'-Dbuild-pdf=false'
		'-Dbuild-examples=false'
		'-Dbenchmark=false'
	)

	# Run package configuration script
	arch-meson build "${config_opts[@]}"

	# Build package
	ninja -C build
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd libsigcplusplus

	# Run internal tests
	ninja -C build test
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Libsigc++                                                              #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_libsigc++ () {
	pkgdesc='Callback Framework for C++'
	depends=(
		'gcc-libs'
		'glibc'
	)
	provides=("libsigc++2.0=$pkgver")
	conflicts=('libsigc++2.0')
	replaces=('libsigc++2.0')

	# Change directory to source directory
	cd libsigcplusplus

	# Install files
	DESTDIR=$pkgdir ninja -C build install

	# Change directory to target directory
	cd $pkgdir

	# Split 'libsystemd' package
	mkdir -p $srcdir/libsig-docs/usr
	mv usr/share $srcdir/libsig-docs/usr || true
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Libsigc++-docs                                                         #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_libsigc++-docs() {
	pkgdesc='Callback Framework for C++ (documentation)'
	depends=('libsigc++')
	provides=("libsigc++2.0-docs=$pkgver")
	conflicts=('libsigc++2.0-docs')
	replaces=('libsigc++2.0-docs')

	# Copy files
	mv $srcdir/libsig-docs/* $pkgdir
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
