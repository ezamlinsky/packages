################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('file')
pkgver=5.38
pkgrel=3
pkgdesc='File type identification utility'
url='https://www.darwinsys.com/file/'
license=('custom')
arch=('x86_64')
groups=('base-devel')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'glibc'
	'libseccomp'
	'zlib'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"ftp://ftp.astron.com/pub/$pkgname/$pkgname-$pkgver.tar.gz"
	"ftp://ftp.astron.com/pub/$pkgname/$pkgname-$pkgver.tar.gz.asc"
	'file-5.38-seccomp-tcgets.patch'
	'0001-Revert-Don-t-depend-on-the-execute-bit-to-determine-.patch'
)
sha256sums=(
	'593c2ffc2ab349c5aea0f55fedfe4d681737b6b62376a9b3ad1e77b2cc19fa34'
	'SKIP'
	'da6197e89ca53bd4f0d9009fa3a18c6fdb66dc07eb92e6bd77207eadb4548cfe'
	'5636b444d147d6598c3defd0ce0a9b28056f2f64b09ef7f032337ed0308b8490'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Apply patches
	patch -p1 -i $srcdir/file-5.38-seccomp-tcgets.patch
	patch -p1 -i $srcdir/0001-Revert-Don-t-depend-on-the-execute-bit-to-determine-.patch

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Fix linking libmagic (vfork needs libpthread)
	CFLAGS+=" -pthread"

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--datadir=/usr/share/file'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--enable-elf'
		'--enable-elf-core'
		'--enable-fsect-man5'

	# 'libseccomp' related options
		'--enable-libseccomp'
		#'--disable-libseccomp'

	# 'zlib' related options
		'--enable-zlib'
		#'--disable-zlib'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 COPYING $pkgdir/usr/share/licenses/file/COPYING
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
