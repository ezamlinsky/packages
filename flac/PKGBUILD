################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgbase='flac'
pkgname=('flac' 'flac-doc')
pkgver=1.3.3
pkgrel=1
url=https://xiph.org/flac/
license=('BSD' 'GPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
makedepends=(
	'doxygen'
	'nasm'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=("https://downloads.xiph.org/releases/flac/flac-$pkgver.tar.xz")
sha256sums=('213e82bd716c9de6db2f98bcadbc4c24c7e2efe8c75939a1a84e28539c4e1748')

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgbase-$pkgver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgbase-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-debug'
		'--enable-asm-optimizations'
		'--enable-sse'
		'--enable-avx'
		'--enable-stack-smash-protection'
		'--enable-xmms-plugin'

	# 'gcc-libs' related option
		'--enable-cpplibs'
		#'--disable-cpplibs'

	# 'libogg' related option
		'--enable-ogg'
		#'--disable-ogg'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgbase-$pkgver

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Flac                                                                   #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_flac() {
	pkgdesc='Free Lossless Audio Codec'
	depends=(
		'gcc-libs'
		'glibc'
		'libogg'
	)

	# Change directory to source directory
	cd $pkgbase-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Change directory to target directory
	cd $pkgdir

	# Install license file
	install -Dm644 $srcdir/$pkgbase-$pkgver/COPYING.Xiph \
	$pkgdir/usr/share/licenses/flac/LICENSE

	# Split 'flac-doc' package
	mkdir -p $srcdir/flac-doc/usr/share
	mv usr/share/doc $srcdir/flac-doc/usr/share || true
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Flac-doc                                                               #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_flac-doc() {
	pkgdesc='Developer documentation for the Free Lossless Audio Codec'
	depends=('flac')

	# Copy files
	mv $srcdir/flac-doc/* $pkgdir

	# Install license file
	install -Dm644 $srcdir/$pkgbase-$pkgver/COPYING.Xiph \
	$pkgdir/usr/share/licenses/flac-doc/LICENSE
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
