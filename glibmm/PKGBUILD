################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgbase='glibmm'
pkgname=('glibmm' 'glibmm-docs')
pkgver=2.64.2
pkgrel=1
url='https://www.gtkmm.org/'
license=('LGPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
makedepends=(
	'clang'
	'git'
	'meson'
	'mm-common'
	'perl-xml-parser'
)
checkdepends=('glib-networking')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_commit='e775940669cb6d93f37ddc2b4cd7da446dfa482c'
source=("git+https://gitlab.gnome.org/GNOME/$pkgbase.git#commit=$_commit")
sha256sums=('SKIP')

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgbase

	# Default configure options
	config_opts=(
		'-Dmaintainer-mode=true'
		'-Dbuild-documentation=true'
		'-Dbuild-examples=false'
		'-Dbuild-deprecated-api=true'
		'-Ddebug-refcounting=false'
	)

	# Run package configuration script
	arch-meson build "${config_opts[@]}"

	# Build package
	ninja -C build
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgbase

	# Run internal tests
	ninja -C build test
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Glibmm                                                                #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package_glibmm() {
	pkgdesc='C++ bindings for GLib'
	depends=(
		'gcc-libs'
		'glib2'
		'glibc'
		'libsigc++'
		'perl'
	)

	# Change directory to source directory
	cd $pkgbase

	# Install files
	DESTDIR=$pkgdir ninja -C build install

	# Change directory to target directory
	cd $pkgdir

	# Split 'glibmm-docs' package
	mkdir -p $srcdir/glibmm-docs/usr
	mv usr/share $srcdir/glibmm-docs/usr || true
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Glibmm-docs                                                            #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_glibmm-docs() {
	pkgdesc='C++ bindings for GLib (documentation)'
	depends=('glibmm')
	options=('docs')

	# Copy files
	mv $srcdir/glibmm-docs/* $pkgdir
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
