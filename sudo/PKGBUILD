################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname='sudo'
pkgver=1.8.31.p1
pkgrel=1
pkgdesc='Give certain users the ability to run some commands as root'
url='https://www.sudo.ws/sudo/'
license=('custom')
arch=('x86_64')
groups=('base-devel')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'glibc'
	#'audit'
	#'krb5'
	'libgcrypt'
	#'libldap'
	#'libselinux'
	#'openssl'
	'pam'
	'zlib'
)
makedepends=(
	'libsasl'
	'sssd'
)

#==============================================================================#
#       Package miscellaneous options                                          #
#==============================================================================#
backup=(
	'etc/sudoers'
	'etc/sudo.conf'
	'etc/pam.d/sudo'
)
options=('emptydirs')
install='sudo.install'

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://www.sudo.ws/$pkgname/dist/$pkgname-${pkgver/.p/p}.tar.gz"
	"https://www.sudo.ws/$pkgname/dist/$pkgname-${pkgver/.p/p}.tar.gz.sig"
	'sudo.pam'
)
sha256sums=(
	'c73cfdfbc1c5cc259fcc3a355e1bacfed99c5580daeadec9704a24cd5e6d15d8'
	#'7ea8d97a3cee4c844e0887ea7a1bd80eb54cc98fd77966776cb1a80653ad454f'
	'SKIP'
	'd1738818070684a5d2c9b26224906aad69a4fea77aabd960fc2675aee2df1fa2'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-${pkgver/.p/p}

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-${pkgver/.p/p}

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--enable-authentication'
		'--enable-root-mailer'
		'--enable-root-sudo'
		'--enable-setreuid'
		'--enable-setresuid'
		'--enable-shadow'
		'--enable-log-host'
		'--enable-shell-sets-home'
		'--enable-path-info'
		'--enable-hardening'
		'--enable-poll'
		'--enable-tmpfiles.d'
		'--enable-shared-libutil'
		'--disable-static-sudoers'
		'--disable-noargs-shell'
		'--disable-env-debug'
		'--disable-env-reset'
		'--disable-admin-flag'
		'--disable-offensive-insults'
		'--enable-pie'
		'--with-man'
		'--with-mdoc'
		'--without-AFS'
		'--without-DCE'
		'--without-passwd'
		'--without-aixauth'
		'--without-fwtk'
		'--without-opie'
		'--without-skey'
		'--without-SecurID'
		'--with-tty-tickets'
		'--with-interfaces'
		'--with-fqdn'
		'--with-ignore-dot'
		'--with-logfac=auth'
		'--with-goodpri=notice'
		'--with-badpri=alert'
		'--with-mailto=root'
		'--with-mail-if-no-user'
		'--with-mail-if-no-host'
		'--with-mail-if-noperms'
		'--with-runas-default=root'
		'--with-timeout=5'
		'--with-passwd-tries=3'
		'--with-password-timeout=5'
		'--with-sudoers-mode=0440'
		'--with-sudoers-uid=0'
		'--with-sudoers-gid=0'
		'--with-umask=022'
		'--with-editor=mcedit'
		'--with-env-editor'
		'--with-insults'
		'--with-all-insults'
		'--without-devel'
		'--without-lecture'
		'--without-long-otp-prompt'
		'--without-umask-override'
		'--without-secure-path'
		'--without-sendmail'
		'--with-rundir=/run/sudo'
		'--with-vardir=/var/db/sudo'
		'--with-logging=file'

	# 'audit' related options
		#'--with-linux-audit'
		'--without-linux-audit'

	# 'krb5' related options
		#'--enable-gss-krb5-ccache-name'
		'--disable-gss-krb5-ccache-name'
		#'--with-kerb5'
		'--without-kerb5'

	# 'libgcrypt' related options
		'--enable-gcrypt'
		#'--disable-gcrypt'

	# 'libldap' related options
		#'--with-ldap'
		'--without-ldap'
		#'--enable-sasl'
		'--disable-sasl'

	# 'libselinux' related options
		#'--with-selinux'
		'--without-selinux'

	# 'openssl' related options
		#'--enable-openssl'
		'--disable-openssl'

	# 'pam' related options
		'--enable-pam-session'
		#'--disable-pam-session'
		'--with-pam'
		#'--without-pam'
		'--with-pam-login'
		#'--without-pam-login'

	# 'sssd' related options
		#'--with-sssd'
		'--without-sssd'

	# 'zlib' related options
		'--enable-zlib'
		#'--disable-zlib'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname-${pkgver/.p/p}

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-${pkgver/.p/p}

	# Install files
	make DESTDIR=$pkgdir install

	# Install PAM module
	install -Dm644 $srcdir/sudo.pam $pkgdir/etc/pam.d/sudo

	# Install license file
	install -Dm644 doc/LICENSE $pkgdir/usr/share/licenses/sudo/LICENSE

	# Create empty 'sudo.conf' file
	touch $pkgdir/etc/sudo.conf

	# Remove 'sudoers.dist' (not needed since pacman manages updates to sudoers)
	rm $pkgdir/etc/sudoers.dist

	# Remove '/run' directory (we will create it using systemd-tmpfiles)
	rm -fr $pkgdir/run
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
