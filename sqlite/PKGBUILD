################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgbase='sqlite'
pkgname=('sqlite' 'sqlite-tcl' 'sqlite-analyzer' 'sqlite-doc')
pkgver=3.31.1
pkgrel=1
pkgdesc='A C library that implements an SQL database engine'
url='https://www.sqlite.org/'
license=('custom:Public Domain')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
makedepends=(
	'readline'
	'tcl'
	'zlib'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_srcver=3310100
source=(
	"https://www.sqlite.org/2020/$pkgbase-src-$_srcver.zip"
	"https://www.sqlite.org/2020/$pkgbase-doc-$_srcver.zip"
	'license.txt'
)
sha1sums=(
	'b75b19eede97a65d78eba627cb92c93c203f1e03'
	'3b6241f788e568b6ae5a20b3630e67729860b2eb'
	'f34f6daa4ab3073d74e774aad21d66878cf26853'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd sqlite-src-$_srcver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd sqlite-src-$_srcver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-debug'
		'--disable-amalgamation'
		'--enable-memsys3'
		'--enable-memsys5'
		'--enable-fts3'
		'--enable-fts4'
		'--enable-fts5'
		'--enable-json1'
		'--enable-update-limit'
		'--enable-geopoly'
		'--enable-rtree'
		'--enable-session'
		'--enable-releasemode'
		'--enable-threadsafe'
		'--enable-load-extension'
		'--enable-tempstore=yes'

	# 'libedit' related options
		#'--enable-editline'
		'--disable-editline'

	# 'readline' related options
		'--enable-readline'
		#'--disable-readline'

	# 'tcl' related options
		'--enable-tcl'
		#'--disable-tcl'
	)

	# Change compile flags
	export CPPFLAGS+=" -DSQLITE_ENABLE_COLUMN_METADATA=1 \
	-DSQLITE_ENABLE_UNLOCK_NOTIFY \
	-DSQLITE_ENABLE_DBSTAT_VTAB=1 \
	-DSQLITE_ENABLE_FTS3_TOKENIZER=1 \
	-DSQLITE_SECURE_DELETE \
	-DSQLITE_MAX_VARIABLE_NUMBER=250000 \
	-DSQLITE_MAX_EXPR_DEPTH=10000"

	# Run package configuration script
	TCLLIBDIR=/usr/lib/sqlite${pkgver} ./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make

	# Build additional tools
	make showdb showjournal showstat4 showwal sqldiff sqlite3_analyzer
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Sqlite                                                                 #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_sqlite() {
	pkgdesc='A C library that implements an SQL database engine'
	depends=(
		'glibc'
		#'libedit'
		'readline'
		'zlib'
	)

	provides=("sqlite3=$pkgver")
	replaces=('sqlite3')

	# Change directory to source directory
	cd sqlite-src-$_srcver

	# Install files
	make DESTDIR=$pkgdir install

	# Install additional tools
	install -m755 showdb showjournal showstat4 showwal sqldiff $pkgdir/usr/bin/

	# Install manpage
	install -Dm644 sqlite3.1 $pkgdir/usr/share/man/man1/sqlite3.1

	# Install license file
	install -Dm644 $srcdir/license.txt $pkgdir/usr/share/licenses/sqlite/license.txt

	# Set links to *.so file
	cd $pkgdir/usr/lib/
	ln -s libsqlite3-$pkgver.so.0 libsqlite3.so.0
	ln -s libsqlite3-$pkgver.so.0.8.6 libsqlite3.so.0.8.6

	# Change directory to target directory
	cd $pkgdir

	# Split 'sqlite-tcl' package
	mkdir -p $srcdir/sqlite-tcl/usr/lib
	mv usr/lib/sqlite* $srcdir/sqlite-tcl/usr/lib
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Sqlite-tcl                                                             #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_sqlite-tcl() {
	pkgdesc='Sqlite Tcl Extension Architecture (TEA)'
	depends=(
		'glibc'
		'sqlite'
	)
	provides=("sqlite3-tcl=$pkgver")
	replaces=('sqlite3-tcl')

	# Copy files
	mv $srcdir/sqlite-tcl/* $pkgdir

	# Install manpage
	install -Dm644 $srcdir/sqlite-src-$_srcver/autoconf/tea/doc/sqlite3.n \
	$pkgdir/usr/share/man/mann/sqlite3.n

	# Install license file
	install -Dm644 $srcdir/license.txt $pkgdir/usr/share/licenses/sqlite-tcl/license.txt
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Sqlite-analyzer                                                        #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_sqlite-analyzer() {
	pkgdesc='An analysis program for sqlite3 database files'
	depends=(
		'glibc'
		'sqlite'
		'tcl'
	)

	# Install sqlite-analyzer
	install -Dm755 sqlite-src-$_srcver/sqlite3_analyzer \
	$pkgdir/usr/bin/sqlite3_analyzer || true
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Sqlite-doc                                                             #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_sqlite-doc() {
	pkgdesc='Sqlite documentation'
	depends=('sqlite')
	provides=("sqlite3-doc=$pkgver")
	replaces=('sqlite3-doc')

	# Create documentation directory
	mkdir -p $pkgdir/usr/share/doc/sqlite

	# Copy all docs
	cp -r $srcdir/sqlite-doc-$_srcver/* $pkgdir/usr/share/doc/sqlite/
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
