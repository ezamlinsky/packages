################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('findutils')
pkgver=4.6.0
pkgrel=4
pkgdesc='GNU utilities to locate files'
url='http://www.gnu.org/software/findutils'
license=('GPL3')
arch=('x86_64')
groups=('base' 'base-devel')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'glibc'
	#'libselinux'
	'sh'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"ftp://ftp.gnu.org/pub/gnu/findutils/findutils-$pkgver.tar.gz"
	'gnulib-fflush.patch'
	'gnulib-makedev.patch'
)
sha1sums=(
	'f18e8aaee3f3d4173a1f598001003be8706d28b0'
	'1bc1586f6a52083939c4cabc32f12e7aead97e61'
	'051382a2b0039438c2b143f2f9e5dc4bea130a09'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Apply patches
	patch -p1 -i $srcdir/gnulib-fflush.patch
	patch -p1 -i $srcdir/gnulib-makedev.patch

	# Run automatic configure script
	autoreconf -f -i

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-debug'
		'--disable-assert'
		'--enable-threads=posix'
		'--enable-leaf-optimisation'
		'--enable-d_type-optimization'
		'--without-included-regex'

	# 'libselinux' related options
		#'--with-selinux'
		'--without-selinux'
	)

	# Don't build or install locate because we use mlocate,
	# which is a secure version of locate.
	sed -i '/^SUBDIRS/s/locate//' Makefile.in

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Don't build locate, but the docs want a file in there
	make -C locate dblocation.texi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
