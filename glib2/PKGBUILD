################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgbase='glib2'
pkgname=('glib2' 'glib2-docs')
pkgver=2.64.0
pkgrel=1
url='https://wiki.gnome.org/Projects/GLib'
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
makedepends=(
	'attr'
	'dbus'
	'dbus'
	'gettext'
	'git'
	'gtk-doc'
	'libelf'
	'libffi'
	'libmount.so'
	'libutil-linux'
	'meson'
	'pcre'
	'python'
	'shared-mime-info'
	'zlib'
)
checkdepends=('desktop-file-utils')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_commit='369626e3105d688afaa316d89d34e8927a8a0171'
source=(
	"git+https://gitlab.gnome.org/GNOME/glib.git#commit=$_commit"
	'noisy-glib-compile-schemas.diff'
	'0001-tests-Move-memory_monitor_tests-under-installed_test.patch'
	'glib-compile-schemas.hook'
	'gio-querymodules.hook'
)
sha256sums=(
	'SKIP'
	'81a4df0b638730cffb7fa263c04841f7ca6b9c9578ee5045db6f30ff0c3fc531'
	'3f7f20c817ef970f9d1bc6606023eca44df5c31dd2334847bc8df2dbcf8e0e43'
	'64ae5597dda3cc160fc74be038dbe6267d41b525c0c35da9125fbf0de27f9b25'
	'557c88177f011ced17bdeac1af3f882b2ca33b386a866fdf900b35f927a2bbe8'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd glib

	# Apply patches
	patch -Np1 -i $srcdir/noisy-glib-compile-schemas.diff
	patch -Np1 -i $srcdir/0001-tests-Move-memory_monitor_tests-under-installed_test.patch
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd glib

	# Default configure options
	config_opts=(
		'-Dman=true'
		'-Ddtrace=false'
		'-Dsystemtap=false'
		'-Dgtk_doc=true'
		'-Diconv=auto'
		'-Dforce_posix_threads=false'
		'-Dinstalled_tests=false'
		'-Dnls=enabled'

	# 'attr' related options
		'-Dxattr=true'
		#'-Dxattr=false'

	# 'gamin' related options
		#'-Dfam=true'
		'-Dfam=false'

	# 'libmount.so' related options
		'-Dlibmount=enabled'
		#'-Dlibmount=disabled'

	# 'libselinux' related options
		#'-Dselinux=enabled'
		'-Dselinux=disabled'

	# 'pcre' related options
		#'-Dinternal_pcre=true'
		'-Dinternal_pcre=false'
	)

	# Run package configuration script
	arch-meson build "${config_opts[@]}"

	# Build package
	ninja -C build
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd glib

	# Run internal tests
	ninja -C build test
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Glib2                                                                  #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_glib2() {
	pkgdesc='Low level core library'
	license=('LGPL2.1')
	depends=(
		'attr'
		'bash'
		#'gamin'
		'glibc'
		'libelf'
		'libffi'
		'libmount.so'
		#'libselinux'
		'pcre'
		'zlib'
	)
	provides=(
		'libgio-2.0.so'
		'libglib-2.0.so'
		'libgmodule-2.0.so'
		'libgobject-2.0.so'
		'libgthread-2.0.so'
	)
	optdepends=(
		'python: gdbus-codegen, glib-genmarshal, glib-mkenums, gtester-report'
		'libelf: gresource inspection tool'
	)

	# Change directory to source directory
	cd glib

	# Install files
	DESTDIR=$pkgdir ninja -C build install

	# Change directory to target directory
	cd $pkgdir

	# Add vendor's hooks
	install -Dm644 -t usr/share/libalpm/hooks $srcdir/*.hook

	# Split 'glib2-docs' package
	mkdir -p $srcdir/glib2-docs/usr/share/gtk-doc
	mv usr/share/gtk-doc $srcdir/glib2-docs/usr/share || true
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Glib2-docs                                                             #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_glib2-docs() {
	pkgdesc="Documentation for GLib"
	license=('LGPL2.1' 'custom')
	depends=('glib2')
	options=('docs')

	# Copy files
	mv $srcdir/glib2-docs/* $pkgdir

	# Install license file
	install -Dm644 $srcdir/glib/docs/reference/COPYING $pkgdir/usr/share/licenses/glib2-docs/COPYING
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
