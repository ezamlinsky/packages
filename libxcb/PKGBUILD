################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('libxcb')
pkgver=1.14
pkgrel=1
pkgdesc='X11 client-side library'
url='https://xcb.freedesktop.org/'
license=('custom')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'glibc'
	'libxau'
	'libxdmcp'
	'xcb-proto>=1.12'
)
makedepends=(
	'libxslt'
	'python'
	'xorg-util-macros'
	'xorgproto'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://xorg.freedesktop.org/archive/individual/lib/$pkgname-$pkgver.tar.xz"
	"https://xorg.freedesktop.org/archive/individual/lib/$pkgname-$pkgver.tar.xz.sig"
)
sha512sums=(
	'b90a23204b0d2c29d8b115577edb01df0465e02d6a8876550fecd62375d24a5d5f872ddd5946772ddba077cadce75b12c7a6d218469dc30b5b92bc82188e8bc6'
	'SKIP'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-static'
		'--disable-devel-docs'
		'--enable-composite'
		'--enable-damage'
		'--enable-dpms'
		'--enable-dri2'
		'--enable-dri3'
		'--enable-ge'
		'--enable-glx'
		'--enable-present'
		'--enable-randr'
		'--enable-record'
		'--enable-render'
		'--enable-resource'
		'--enable-screensaver'
		'--enable-shape'
		'--enable-shm'
		'--enable-sync'
		'--enable-xevie'
		'--enable-xfixes'
		'--enable-xfree86-dri'
		'--enable-xinerama'
		'--enable-xinput'
		'--enable-xkb'
		'--enable-xprint'
		'--enable-selinux'
		'--enable-xtest'
		'--enable-xv'
		'--enable-xvmc'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 COPYING $pkgdir/usr/share/licenses/libxcb/COPYING
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
