################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname='audacious'
pkgver=3.10.1
pkgrel=1
pkgdesc='Lightweight, advanced audio player focused on audio quality'
url='http://audacious-media-player.org/'
license=('BSD')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'audacious-plugins'
	'cairo'
	'desktop-file-utils'
	'gcc-libs'
	'gdk-pixbuf2'
	'glib2'
	'glibc'
	'gnome-icon-theme'
	'gtk2'
	'hicolor-icon-theme'
	'pango'
	#'qt5-base'
)
makedepends=('python')
optdepends=('unzip: zipped skins support')

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
provides=('audacious-player')
conflicts=('audacious-player')
replaces=('audacious-player')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=("http://distfiles.audacious-media-player.org/audacious-$pkgver.tar.bz2")
sha256sums=('8366e840bb3c9448c2cf0cf9a0800155b0bd7cc212a28ba44990c3d2289c6b93')

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--enable-dbus'
		'--disable-valgrind'

	# 'gtk2' related options
		'--enable-gtk'
		#'--disable-gtk'

	# 'qt5-base' related options
		#'--enable-qt'
		'--disable-qt'
	)

	# Change compile flags
	export CFLAGS+=' -fno-stack-protector'
	export CXXFLAGS+="${CFLAGS}"

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 COPYING $pkgdir/usr/share/licenses/audacious/LICENSE
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
