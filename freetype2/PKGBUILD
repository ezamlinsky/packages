################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
pkgname='freetype2'
pkgver=2.10.1
pkgrel=2
pkgdesc='Font rasterization library'
url='https://www.freetype.org/'
license=('GPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'bzip2'
	'glibc'
	'harfbuzz'
	'libpng'
	'sh'
	'zlib'
)
makedepends=('libx11')

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
provides=('libfreetype.so')

#==============================================================================#
#       Package miscellaneous options                                          #
#==============================================================================#
backup=('etc/profile.d/freetype2.sh')
install='freetype2.install'

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://download-mirror.savannah.gnu.org/releases/freetype/freetype-$pkgver.tar.xz"
	"https://download-mirror.savannah.gnu.org/releases/freetype/freetype-$pkgver.tar.xz.sig"
	'0001-Enable-table-validation-modules.patch'
	'0002-Enable-infinality-subpixel-hinting.patch'
	'0003-Enable-long-PCF-family-names.patch'
	'freetype2.sh'
)
sha1sums=(
	'79874ef4eaa52025126b71d836453b8279bdd331'
	'SKIP'
	'283427030ef4b942f01e24ba022f2b84f1733fa8'
	'1efd1e1db64cb7705296b724920d6328ff5f1f14'
	'6b289e2ba54a4513af9aa3981b374fccb20ffc89'
	'bc6df1661c4c33e20f5ce30c2da8ad3c2083665f'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd freetype-$pkgver

	# Apply patches
	patch -Np1 -i $srcdir/0001-Enable-table-validation-modules.patch
	patch -Np1 -i $srcdir/0002-Enable-infinality-subpixel-hinting.patch
	patch -Np1 -i $srcdir/0003-Enable-long-PCF-family-names.patch

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd freetype-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--enable-freetype-config'

	# 'bzip2' related options
		'--with-bzip2=yes'
		#'--with-bzip2=no'

	# 'harfbuzz' related options
		'--with-harfbuzz=yes'
		#'--with-harfbuzz=no'

	# 'libpng' related options
		'--with-png=yes'
		#'--with-png=no'

	# 'zlib' related options
		'--with-zlib=yes'
		#'--with-zlib=no'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd freetype-$pkgver

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {


	# Change directory to source directory
	cd freetype-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Install 'freetype2.sh' script
	install -Dm644 ../freetype2.sh $pkgdir/etc/profile.d/freetype2.sh
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
