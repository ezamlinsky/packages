################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('libpsl')
pkgver=0.21.0
pkgrel=1
pkgdesc='Public Suffix List library'
url='https://github.com/rockdaboot/libpsl'
license=('MIT')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'gcc-libs'
	'glibc'
	#'libidn'
	#'libidn2'
	#'libunistring'
)
makedepends=(
	'gtk-doc'
	'libxslt'
	'publicsuffix-list'
	'python'
)

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
provides=('libpsl.so')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://github.com/rockdaboot/libpsl/releases/download/libpsl-$pkgver/libpsl-$pkgver.tar.gz"
	'libpsl-gtk-doc-1.30-fix.patch::https://github.com/rockdaboot/libpsl/commit/87d1add318b5e5d09977f7f374e923577b6ff3be.patch'
	'libpsl-0.21.0-build-fix.patch::https://github.com/rockdaboot/libpsl/commit/9347024221f4a9d63f9dcafcda13362a7c8d92fe.patch'
)
sha512sums=(
	'165c4f0b0640a813d512bd916e1532e32e43c8c81a5efd048f3a5b07b1b3c9129b4c4b5008b8b11a7c1b3914caea17564321389cd350bf1d687d53a97f2afa4d'
	'9cca44a70c7e26bc6016767bce8888e5902c97eff92d004ca2ab659ee289a9e079fab62820f0f684ac25922260175c74c08c1dfca6dd1db6333c37edf38b8048'
	'692d6dd6f64653f6e355df0b6cfe519a5c202d966deab066123e814591692a28a805fe794dc57dd6f9ab357b4e0dd8dfdf842239ec39f303fc33d98019e7f24e'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Apply patches
	patch -p1 -i $srcdir/libpsl-gtk-doc-1.30-fix.patch
	patch -p1 -i $srcdir/libpsl-0.21.0-build-fix.patch

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-gtk-doc'
		'--enable-man'
		'--with-psl-file=/usr/share/publicsuffix/effective_tld_names.dat'
		'--with-psl-testfile=/usr/share/publicsuffix/test_psl.txt'

	# 'libicu' related options
		#'--enable-runtime=libidn'
		'--disable-runtime'
		#'--enable-builtin=libidn'
		'--disable-builtin'

	# 'libicu2' related options
		#'--enable-runtime=libidn2'
		'--disable-runtime'
		#'--enable-builtin=libidn2'
		'--disable-builtin'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 COPYING $pkgdir/usr/share/licenses/libpsl/LICENSE
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
