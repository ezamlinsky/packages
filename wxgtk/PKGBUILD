################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgbase='wxgtk'
pkgname=('wxgtk-common' 'wxgtk3')
pkgver=3.0.4
pkgrel=2
url='https://wxwidgets.org'
license=('custom:wxWindows')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
makedepends=(
	'glu'
	'gst-plugins-base'
	'gtk2'
	'gvfs'
	'libnotify'
	'libxpm'
	#'webkit2gtk'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://github.com/wxWidgets/wxWidgets/releases/download/v$pkgver/wxWidgets-$pkgver.tar.bz2"
	'make-abicheck-non-fatal.patch'
)
sha256sums=(
	'96157f988d261b7368e5340afa1a0cad943768f35929c22841f62c25b17bf7f0'
	'46a1bb97d69163547da13d5e23a4c73e68de27ee601da5d2fb5bc5c417931453'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd wxWidgets-$pkgver

	# Apply patches
	patch -Np1 -i $srcdir/make-abicheck-non-fatal.patch

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd wxWidgets-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-debug'
		'--disable-debug_flag'
		'--disable-debug_info'
		'--disable-debug_gdb'
		'--disable-profile'
		'--disable-gtktest'
		'--disable-sdltest'
		'--disable-precomp-headers'
		'--enable-optimise'
		'--enable-visibility'
		'--enable-tls'
		'--enable-stl'
		'--enable-std_containers'
		'--enable-std_iostreams'
		'--enable-std_string'
		'--enable-all-features'
		'--enable-controls'
		'--enable-gui'
		'--enable-plugins'
		'--disable-no_rtti'
		'--disable-no_deps'
		'--disable-no_exceptions'
		'--disable-compat26'
		'--disable-compat28'
		'--disable-monolithic'
		'--disable-universal'
		'--enable-gpe'
		'--enable-unicode'
		'--enable-utf8'
		'--enable-utf8only'
		'--enable-vararg_macros'
		'--enable-intl'
		'--enable-xlocale'
		'--enable-config'
		'--enable-protocols'
		'--enable-ftp'
		'--enable-http'
		'--enable-fileproto'
		'--enable-sockets'
		'--enable-dataobj'
		'--enable-ipc'
		'--enable-baseevtloop'
		'--enable-epollloop'
		'--enable-selectloop'
		'--enable-any'
		'--enable-apple_ieee'
		'--enable-arcstream'
		'--enable-base64'
		'--enable-backtrace'
		'--enable-catch_segvs'
		'--enable-cmdline'
		'--enable-datetime'
		'--enable-debugreport'
		'--enable-dialupman'
		'--enable-dynlib'
		'--enable-dynamicloader'
		'--enable-exceptions'
		'--enable-ffile'
		'--enable-file'
		'--enable-filehistory'
		'--enable-filesystem'
		'--enable-fontenum'
		'--enable-fontmap'
		'--enable-fs_archive'
		'--enable-fs_inet'
		'--enable-fs_zip'
		'--enable-fsvolume'
		'--enable-fswatcher'
		'--enable-geometry'
		'--enable-log'
		'--enable-longlong'
		'--enable-mimetype'
		'--enable-printfposparam'
		'--enable-snglinst'
		'--enable-sound'
		'--enable-stdpaths'
		'--enable-stopwatch'
		'--enable-streams'
		'--enable-sysoptions'
		'--enable-tarstream'
		'--enable-textbuf'
		'--enable-textfile'
		'--enable-timer'
		'--enable-variant'
		'--enable-zipstream'
		'--enable-url'
		'--enable-protocol'
		'--enable-protocol-http'
		'--enable-protocol-ftp'
		'--enable-protocol-file'
		'--enable-threads'
		'--enable-docview'
		'--enable-help'
		'--enable-html'
		'--enable-htmlhelp'
		'--enable-xrc'
		'--enable-aui'
		'--enable-propgrid'
		'--enable-ribbon'
		'--enable-stc'
		'--enable-constraints'
		'--enable-loggui'
		'--enable-logwin'
		'--enable-logdialog'
		'--enable-mdi'
		'--enable-mdidoc'
		'--enable-richtext'
		'--enable-postscript'
		'--enable-printarch'
		'--enable-svg'
		'--enable-webview'
		'--enable-graphics_ctx'
		'--enable-clipboard'
		'--enable-dnd'
		'--enable-markup'
		'--enable-accel'
		'--enable-animatectrl'
		'--enable-bannerwindow'
		'--enable-artstd'
		'--enable-arttango'
		'--enable-bmpbutton'
		'--enable-bmpcombobox'
		'--enable-button'
		'--enable-calendar'
		'--enable-caret'
		'--enable-checkbox'
		'--enable-checklst'
		'--enable-choice'
		'--enable-choicebook'
		'--enable-collpane'
		'--enable-colourpicker'
		'--enable-combobox'
		'--enable-comboctrl'
		'--enable-commandlinkbutton'
		'--enable-dataviewctrl'
		'--enable-datepick'
		'--enable-detect_sm'
		'--enable-dirpicker'
		'--enable-display'
		'--enable-editablebox'
		'--enable-filectrl'
		'--enable-filepicker'
		'--enable-fontpicker'
		'--enable-gauge'
		'--enable-grid'
		'--enable-headerctrl'
		'--enable-hyperlink'
		'--enable-imaglist'
		'--enable-infobar'
		'--enable-listbook'
		'--enable-listbox'
		'--enable-listctrl'
		'--enable-notebook'
		'--enable-notifmsg'
		'--enable-odcombobox'
		'--enable-popupwin'
		'--enable-prefseditor'
		'--enable-radiobox'
		'--enable-radiobtn'
		'--enable-richmsgdlg'
		'--enable-richtooltip'
		'--enable-rearrangectrl'
		'--enable-sash'
		'--enable-scrollbar'
		'--enable-searchctrl'
		'--enable-slider'
		'--enable-spinbtn'
		'--enable-spinctrl'
		'--enable-splitter'
		'--enable-statbmp'
		'--enable-statbox'
		'--enable-statline'
		'--enable-stattext'
		'--enable-statusbar'
		'--enable-taskbaricon'
		'--enable-tbarnative'
		'--enable-textctrl'
		'--enable-timepick'
		'--enable-tipwindow'
		'--enable-togglebtn'
		'--enable-toolbar'
		'--enable-toolbook'
		'--enable-treebook'
		'--enable-treectrl'
		'--enable-treelist'
		'--enable-commondlg'
		'--enable-aboutdlg'
		'--enable-choicedlg'
		'--enable-coldlg'
		'--enable-filedlg'
		'--enable-finddlg'
		'--enable-fontdlg'
		'--enable-dirdlg'
		'--enable-msgdlg'
		'--enable-numberdlg'
		'--enable-splash'
		'--enable-textdlg'
		'--enable-tipdlg'
		'--enable-progressdlg'
		'--enable-wizarddlg'
		'--enable-menus'
		'--enable-miniframe'
		'--enable-tooltips'
		'--enable-splines'
		'--enable-mousewheel'
		'--enable-validators'
		'--enable-busyinfo'
		'--enable-hotkey'
		'--enable-joystick'
		'--enable-metafiles'
		'--enable-dragimage'
		'--enable-uiactionsim'
		'--enable-dctransform'
		'--enable-webviewwebkit'
		'--enable-palette'
		'--enable-image'
		'--enable-gif'
		'--enable-pcx'
		'--enable-tga'
		'--enable-iff'
		'--enable-pnm'
		'--enable-xpm'
		'--enable-ico_cur'
		'--enable-autoidman'
		'--disable-ipv6'
		'--without-subdirs'
		'--with-regex=builtin'
		'--with-libiconv=yes'
		'--with-gtkprint=yes'
		'--with-themes=all'
		'--with-x'

	# 'expat' related options
		'--with-expat=sys'
		#'--with-expat=no'

	# 'gst-plugins-base-libs' related options
		#'--enable-mediactrl'
		'--disable-mediactrl'
		'--disable-gstreamer8'

	# 'gtk2' related options
		#'--with-gtk=2'

	# 'gtk3' related options
		'--with-gtk=3'

	# 'gvfs' related options
		'--with-gnomevfs=yes'
		#'--with-gnomevfs=no'

	# 'libglvnd' related options
		'--with-opengl=yes'
		#'--with-opengl=no'

	# 'libjpeg' related options
		'--with-libjpeg=sys'
		#'--with-libjpeg=no'

	# 'libmspack' related options
		#'--with-libmspack=yes'
		'--with-libmspack=no'

	# 'libnotify' related options
		'--with-libnotify=yes'
		#'--with-libnotify=no'

	# 'libpng' related options
		'--with-libpng=sys'
		#'--with-libpng=no'

	# 'libtiff' related options
		'--with-libtiff=sys'
		#'--with-libtiff=no'
		'--with-libjbig=yes'
		#'--with-libjbig=no'
		'--with-liblzma=yes'
		#'--with-liblzma=no'

	# 'libx11' related options
		#'--with-x11'
		#'--without-x11'

	# 'libxpm' related options
		'--with-libxpm=sys'
		#'--with-libxpm=no'

	# 'sdl2' related options
		#'--with-sdl=yes'
		'--with-sdl=no'

	# 'zlib' related options
		'--with-zlib=sys'
		#'--with-zlib=no'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Wxgtk-common                                                           #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_wxgtk-common() {
	pkgdesc='Common libraries and headers for wxgtk2 and wxgtk3'
	depends=(
		'expat'
		'gcc-libs'
		'glibc'
		'zlib'
	)

	# Change directory to source directory
	cd wxWidgets-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 docs/licence.txt $pkgdir/usr/share/licenses/wxgtk-common/LICENSE

	# Change directory to target directory
	cd $pkgdir

	# Split 'wxgtk3' package
	mkdir -p $srcdir/wxgtk3/usr/{bin,lib}
	mv usr/bin/wx-config  $srcdir/wxgtk3/usr/bin/wx-config-gtk3
	mv usr/lib/libwx_gtk* $srcdir/wxgtk3/usr/lib
	mv usr/lib/wx $srcdir/wxgtk3/usr/lib
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Wxgtk                                                                  #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_wxgtk3() {
	pkgdesc='GTK+ implementation of wxWidgets API for GUI'
	depends=(
		'bash'
		'cairo'
		'gcc-libs'
		'gdk-pixbuf2'
		'glib2'
		'glibc'
		#'gst-plugins-base-libs'
		#'gtk2'
		'gtk3'
		'libglvnd'
		'libjpeg-turbo'
		#'libmspack'
		'libnotify'
		'libpng'
		'libsm'
		'libtiff'
		'libx11'
		'libxxf86vm'
		'pango'
		#'sdl2'
		'wxgtk-common'
	)
	optdepends=('webkit2gtk: for webview support')
	conflicts=('wxgtk<3.0.3.1-2')

	# Copy files
	mv $srcdir/wxgtk3/* $pkgdir

	# Install license file
	install -Dm644 $srcdir/wxWidgets-$pkgver/docs/licence.txt $pkgdir/usr/share/licenses/wxgtk3/LICENSE
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
