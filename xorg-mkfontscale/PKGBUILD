################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('xorg-mkfontscale')
pkgver=1.2.1
pkgrel=1
pkgdesc='Create an index of scalable font files for X'
url='https://xorg.freedesktop.org/'
license=('custom')
arch=('x86_64')
groups=('xorg-apps' 'xorg')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'bzip2'
	'freetype2'
	'gcc-libs'
	'glibc'
	'libfontenc'
	'zlib'
)
makedepends=(
	'xorg-util-macros'
	'xproto'
)

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
provides=('xorg-mkfontdir')
conflicts=('xorg-mkfontdir')
replaces=('xorg-mkfontdir')

#==============================================================================#
#       Package miscellaneous options                                          #
#==============================================================================#
install='xorg-mkfontscale.install'

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://xorg.freedesktop.org/archive/individual/app/mkfontscale-$pkgver.tar.bz2"
	"https://xorg.freedesktop.org/archive/individual/app/mkfontscale-$pkgver.tar.bz2.sig"
	'xorg-mkfontscale.hook'
	'xorg-mkfontscale.script'
)
sha512sums=(
	'4d243160e1f7f8dfa6a8f53349c1a42a55fc99426455ebdef58352c5e951fce8b4f1fbd1061a76c9a148095b002eac372db1ae5e2647d2ccb4886635b317b18c'
	'SKIP'
	'80e6995af4827e487d0a141760781d9d295a1c0d935ede53c4ba0291a4f0569686fcd7a6186d08953da1cf71d14a4c7c8f4092c731e17008941f014df3a4c917'
	'2a53d38c85b962eaee534f6f3fad4122412a7200c6787fd8216eb191904e2e3727400606a73d6f09017016f6c1360e5148afbb8fbe16e35c5e5cd55dec635387'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd mkfontscale-$pkgver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd mkfontscale-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'

	# 'bzip2' related options
		'--with-bzip2'
		#'--without-bzip2'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd mkfontscale-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 COPYING $pkgdir/usr/share/licenses/xorg-mkfontscale/COPYING

	# install scripts
	install -Dm644 $srcdir/xorg-mkfontscale.hook $pkgdir/usr/share/libalpm/hooks/xorg-mkfontscale.hook
	install -D $srcdir/xorg-mkfontscale.script $pkgdir/usr/share/libalpm/scripts/xorg-mkfontscale
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
