################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('alsa-lib')
pkgver=1.2.2
pkgrel=1
pkgdesc='An alternative implementation of Linux sound support'
url='https://www.alsa-project.org'
license=('LGPL2.1')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'glibc'
	#'python2'
)
makedepends=('python2')
optdepends=('python2: for python smixer plugin')

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
provides=(
	'libasound.so'
	'libatopology.so'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=("https://www.alsa-project.org/files/pub/lib/$pkgname-$pkgver.tar.bz2")
sha512sums=('d21adb3ff998918c7d1820f9ce2aaf4202dd45ccb87cb092d49da8b2402b6ddaad06325be0fd59f17393a5d9958e3743bfccb4b14bdb947a42e7d791d73c7033')

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-old-symbols'
		'--enable-mixer'
		'--enable-pcm'
		'--enable-rawmidi'
		'--enable-hwdep'
		'--enable-seq'
		'--enable-ucm'
		'--enable-topology'
		'--enable-old-symbols'
		'--enable-thread-safety'
		'--without-debug'
		'--without-wordexp'
		'--with-versioned'
		'--with-libdl'
		'--with-pthread'
		'--with-librt'
		'--with-pcm-plugins=all'
		'--with-ctl-plugins=all'

	# 'python2' related options
		#'--enable-python'
		'--disable-python'
		#'--enable-python2'
		'--disable-python2'
		#'--enable-mixer-pymods'
	)

	# Change compile flags
	export CFLAGS+=' -fno-lto'
	export CXXFLAGS="${CFLAGS}"

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
