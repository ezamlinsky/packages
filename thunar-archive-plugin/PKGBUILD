################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('thunar-archive-plugin')
pkgver=0.4.0
pkgrel=1
pkgdesc='Create and extract archives in Thunar'
url='http://goodies.xfce.org/projects/thunar-plugins/thunar-archive-plugin'
license=('GPL2')
arch=('x86_64')
groups=('xfce4-goodies')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'glib2'
	'glibc'
	'gtk3'
	'hicolor-icon-theme'
	'thunar'
)
makedepends=(
	'intltool'
	'xfce4-dev-tools'
)
optdepends=(
	'file-roller'
	'ark'
	'xarchiver'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=("https://archive.xfce.org/src/thunar-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2")
sha256sums=('bf82fa86a388124eb3c4854249c30712b2922e61789607268ee14548549b3115')

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 xdt-autogen
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-debug'
		'--enable-debug=no'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
