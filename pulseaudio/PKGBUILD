################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgbase='pulseaudio'
pkgname=('pulseaudio' 'libpulse' 'pulseaudio-zeroconf' 'pulseaudio-lirc' 'pulseaudio-jack' 'pulseaudio-bluetooth' 'pulseaudio-equalizer')
pkgver=13.0
pkgrel=3
url="https://www.freedesktop.org/wiki/Software/PulseAudio/"
license=('GPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
makedepends=(
	'attr'
	'autoconf-archive'
	'avahi'
	'bluez'
	'bluez-libs'
	'check'
	'dbus'
	'fftw'
	'git'
	'gtk3'
	'intltool'
	'jack2'
	'libasyncns'
	'libcap'
	'libsm'
	'libsndfile'
	'libsoxr'
	'libtool'
	'libxtst'
	'lirc'
	'meson'
	'openssl'
	'orc'
	'rtkit'
	'sbc'
	'speexdsp'
	'systemd'
	'tdb'
	'webrtc-audio-processing'
	'xmltoman'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_commit='200618b32f0964a479d69c9b6e5073e6931c370a'
source=(
	"git+https://gitlab.freedesktop.org/$pkgbase/$pkgbase.git#commit=$_commit"
	'0001-meson-Define-TUNNEL_SINK-for-module-tunnel-sink.patch'
)
sha256sums=(
	'SKIP'
	'4ff133e2847baad5bb6798b5816d67551cfba2efabb2f1f348628d7217abd07d'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgbase

	# Apply patches
	patch -p1 -i $srcdir/0001-meson-Define-TUNNEL_SINK-for-module-tunnel-sink.patch
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgbase

	# Default configure options
	config_opts=(
		'-Dman=true'
		'-Dtests=false'
		'-Dgsettings=enabled'
		'-Dgtk=enabled'
		'-Dipv6=true'
		'-Dadrian-aec=true'
		'-Dlegacy-database-entry-format=false'
		'-Drunning-from-build-tree=false'
		'-Dsystem_user=pulse'
		'-Dsystem_group=pulse'
		'-Daccess_group=pulse-access'
		'-Dpulsedsp-location=/usr/lib/pulseaudio'
		'-Dudevrulesdir=/usr/lib/udev/rules.d'

	# 'alsa-lib' related options
		'-Dalsa=enabled'
		#'-Dalsa=disabled'

	# 'avahi' related options
		'-Davahi=enabled'
		#'-Davahi=disabled'

	# 'dbus' related options
		'-Ddbus=enabled'
		#'-Ddbus=disabled'

	# 'fftw' related options
		'-Dfftw=enabled'
		#'-Dfftw=disabled'

	# 'gdbm' related options
		'-Ddatabase=gdbm'

	# 'glib2' related options
		'-Dglib=enabled'
		#'-Dglib=disabled'

	# 'jack2' related options
		'-Djack=enabled'
		#'-Djack=disabled'

	# 'libasyncns' related options
		#'-Dasyncns=enabled'
		'-Dasyncns=disabled'

	# 'libsamplerate' related options
		#'-Dsamplerate=enabled'
		'-Dsamplerate=disabled'

	# 'libsm & libxtst & libxcb & libice & libx11' related options
		'-Dx11=enabled'
		#'-Dx11=disabled'

	# 'libsoxr' related options
		#'-Dsoxr=enabled'
		'-Dsoxr=disabled'

	# 'libsystemd' related options
		'-Dsystemd=enabled'
		#'-Dsystemd=disabled'
		'-Dudev=enabled'
		#'-Dudev=disabled'
		#'-Dhal-compat=true'
		'-Dhal-compat=false'

	# 'lirc' related options
		'-Dlirc=enabled'
		#'-Dlirc=dusabled'

	# 'openssl' related options
		'-Dopenssl=enabled'
		#'-Dopenssl=disabled'

	# 'orc' related options
		#'-Dorc=enabled'
		'-Dorc=disabled'

	# 'sbc' related options
		'-Dbluez5=true'
		#'-Dbluez5=false'
		'-Dbluez5-ofono-headset=true'
		#'-Dbluez5-ofono-headset=false'
		'-Dbluez5-native-headset=true'
		#'-Dbluez5-native-headset=false'

	# 'speexdsp' related options
		#'-Dspeex=enabled'
		'-Dspeex=disabled'

	# 'tdb' related options
		#'-Ddatabase=tdb'

	# 'webrtc-audio-processing' related options
		#'-Dwebrtc-aec=enabled'
		'-Dwebrtc-aec=disabled'
	)

	# Run package configuration script
	arch-meson build "${config_opts[@]}"

	# Build package
	ninja -C build
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgbase

	# Run internal tests
	ninja -C build test
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Pulseaudio                                                             #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_pulseaudio() {
	pkgdesc="A featureful, general-purpose sound server"
	depends=(
		"libpulse=$pkgver-$pkgrel"
		'alsa-lib'
		'bash'
		'dbus'
		'gdbm'
		'glib2'
		'glibc'
		'libcap'
		'libice'
		#'libsamplerate'
		'libsm'
		'libsndfile'
		#'libsoxr'
		'libtool'
		'libx11'
		'libxcb'
		'libxtst'
		'openssl'
		#'orc'
		#'speexdsp'
		'systemd-libs'
		#'tdb'
		#'webrtc-audio-processing'
	)
	optdepends=('pulseaudio-alsa: ALSA configuration (recommended)')
	replaces=('pulseaudio-xen<=9.0')
	backup=(
		'etc/pulse/daemon.conf'
		'etc/pulse/default.pa'
		'etc/pulse/system.pa'
	)
	install="pulseaudio.install"

	# Change directory to source directory
	cd $pkgbase

	# Install files
	DESTDIR=$pkgdir ninja -C build install

	# Change directory to package directory
	cd $pkgdir

	# Remove dbus config file
	rm etc/dbus-1/system.d/pulseaudio-system.conf

	# Disable flat-volumes
	sed -e '/flat-volumes/iflat-volumes = no' -i etc/pulse/daemon.conf

	# Disable autospawn
	sed -e '/autospawn/iautospawn = no' -i etc/pulse/client.conf

	# Disable cork-request module
	sed -e 's|/usr/bin/pactl load-module module-x11-cork-request|#&|' -i usr/bin/start-pulseaudio-x11

	# Required by qpaeq
	sed -e '/Load several protocols/aload-module module-dbus-protocol' -i etc/pulse/default.pa

	# Split libpulse
	mkdir -p $srcdir/libpulse/{etc/pulse,usr/{bin,lib/pulseaudio,share/man/man{1,5}}}
	mv etc/pulse/client.conf $srcdir/libpulse/etc/pulse || true
	mv usr/bin/pa{cat,ctl,dsp,mon,play,rec,record} $srcdir/libpulse/usr/bin || true
	mv usr/lib/libpulse{,-simple,-mainloop-glib}.so* $srcdir/libpulse/usr/lib || true
	mv usr/lib/{cmake,pkgconfig} $srcdir/libpulse/usr/lib || true
	mv usr/lib/pulseaudio/libpulse{dsp,common-*}.so $srcdir/libpulse/usr/lib/pulseaudio || true
	mv usr/include $srcdir/libpulse/usr || true
	mv usr/share/man/man1/pa{cat,ctl,dsp,mon,play,rec,record}.1 $srcdir/libpulse/usr/share/man/man1 || true
	mv usr/share/man/man5/pulse-client.conf.5 $srcdir/libpulse/usr/share/man/man5 || true
	mv usr/share/vala $srcdir/libpulse/usr/share || true

	# Split modules
	mkdir -p $srcdir/{zeroconf,lirc,jack,bluetooth,equalizer}/usr/lib/pulse-$pkgver/modules \
	$srcdir/{gconf/usr/lib/pulse,equalizer/usr/bin}
	mv usr/lib/pulse-$pkgver/modules/{libavahi-wrap,module-{zeroconf-{publish,discover},raop-discover}}.so \
	$srcdir/zeroconf/usr/lib/pulse-$pkgver/modules || true
	mv usr/lib/pulse-$pkgver/modules/module-lirc.so \
	$srcdir/lirc/usr/lib/pulse-$pkgver/modules || true
	mv usr/lib/pulse-$pkgver/modules/module-jack{-sink,-source,dbus-detect}.so \
	$srcdir/jack/usr/lib/pulse-$pkgver/modules || true
	mv usr/lib/pulse-$pkgver/modules/{libbluez5-util,module-{bluetooth-{discover,policy},bluez5-{discover,device}}}.so \
	$srcdir/bluetooth/usr/lib/pulse-$pkgver/modules || true
	mv usr/lib/pulse-$pkgver/modules/module-equalizer-sink.so \
	$srcdir/equalizer/usr/lib/pulse-$pkgver/modules || true
	mv usr/bin/qpaeq \
	$srcdir/equalizer/usr/bin || true
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Libpulse                                                               #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_libpulse() {
	pkgdesc="$pkgdesc (client library)"
	license=('LGPL')
	depends=(
		'dbus'
		'glib2'
		'glibc'
		#'libasyncns'
		'libsndfile'
		#'libwrap'
		'libxcb'
		'systemd-libs'
	)
	backup=('etc/pulse/client.conf')

	# Copy files
	mv $srcdir/libpulse/* $pkgdir
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Pulseaudio-zeroconf                                                    #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_pulseaudio-zeroconf() {
	pkgdesc="Zeroconf support for PulseAudio"
	depends=(
		"libpulse=$pkgver-$pkgrel"
		"pulseaudio=$pkgver-$pkgrel"
		'avahi'
		'dbus'
		'glibc'
	)

	# Copy files
	mv $srcdir/zeroconf/* $pkgdir
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Pulseaudio-lirc                                                        #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_pulseaudio-lirc() {
	pkgdesc="IR (lirc) support for PulseAudio"
	depends=(
		"libpulse=$pkgver-$pkgrel"
		"pulseaudio=$pkgver-$pkgrel"
		'glibc'
		'lirc'
	)

	# Copy files
	mv $srcdir/lirc/* $pkgdir
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Pulseaudio-jack                                                        #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_pulseaudio-jack() {
	pkgdesc="Jack support for PulseAudio"
	depends=(
		"libpulse=$pkgver-$pkgrel"
		"pulseaudio=$pkgver-$pkgrel"
		'dbus'
		'glibc'
		'jack2'
	)

	# Copy files
	mv $srcdir/jack/* $pkgdir
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Pulseaudio-bluetooth                                                   #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_pulseaudio-bluetooth() {
	pkgdesc="Bluetooth support for PulseAudio"
	depends=(
		"libpulse=$pkgver-$pkgrel"
		"pulseaudio=$pkgver-$pkgrel"
		'dbus'
		'glibc'
		'sbc'
	)

	# Copy files
	mv $srcdir/bluetooth/* $pkgdir
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Pulseaudio-equalizer                                                   #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_pulseaudio-equalizer() {
	pkgdesc="Equalizer for PulseAudio"
	license=('AGPL3')
	depends=(
		"libpulse=$pkgver-$pkgrel"
		"pulseaudio=$pkgver-$pkgrel"
		'dbus'
		'fftw'
		'glibc'
	)

	# Copy files
	mv $srcdir/equalizer/* $pkgdir
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
