################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgbase='pulseaudio'
pkgname=('pulseaudio' 'libpulse' 'pulseaudio-gconf' 'pulseaudio-zeroconf' 'pulseaudio-lirc' 'pulseaudio-jack' 'pulseaudio-bluetooth' 'pulseaudio-equalizer')
pkgver=12.99.1
pkgrel=1
url="https://www.freedesktop.org/wiki/Software/PulseAudio/"
license=('GPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
makedepends=(
	'attr'
	'autoconf-archive'
	'avahi'
	'bluez'
	'bluez-libs'
	'check'
	'dbus'
	'fftw'
	'gconf'
	'git'
	'gtk3'
	'intltool'
	'libasyncns'
	'libcap'
	'libsm'
	'libsndfile'
	'libsoxr'
	'libtool'
	'libxtst'
	'lirc'
	'openssl'
	'orc'
	'sbc'
	'speexdsp'
	'systemd'
	'tdb'
	'webrtc-audio-processing'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_commit=0cd931dc808786bffa3b40f75097af43a217b880  # tags/v12.99.1^0
source=("git+https://gitlab.freedesktop.org/pulseaudio/pulseaudio.git#commit=$_commit")
sha256sums=('SKIP')

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgbase

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgbase

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--enable-manpages'
		'--enable-gtk3'
		'--disable-ipv6'
		'--disable-hal-compat'
		'--disable-oss-output'
		'--disable-oss-wrapper'
		'--disable-esound'
		'--disable-bluez4'
		'--disable-legacy-database-entry-format'
		'--disable-tests'
		'--disable-default-build-tests'

	# 'alsa-lib' related options
		'--enable-alsa'
		#'--disable-alsa'

	# 'avahi' related options
		'--enable-avahi'
		#'--disable-avahi'

	# 'dbus' related options
		'--enable-dbus'
		#'--disable-dbus'

	# 'fftw' related options
		'--with-fftw'
		#'--without-fftw'

	# 'gconf' related options
		'--enable-gconf'
		#'--disable-gconf'

	# 'gdbm' related options
		'--with-database=gdbm'

	# 'glib2' related options
		'--enable-glib2'
		#'--disable-glib2'

	# 'jack' related options
		'--enable-jack'
		#'--disable-jack'

	# 'libasyncns' related options
		#'--enable-asyncns'
		'--disable-asyncns'

	# 'libcap' related options
		'--with-caps'
		#'--without-caps'

	# 'libsamplerate' related options
		#'--enable-samplerate'
		'--disable-samplerate'

	# 'libsm & libxtst & libxcb & libice & libx11' related options
		'--enable-x11'
		#'--disable-x11'

	# 'libsoxr' related options
		#'--with-soxr'
		'--without-soxr'

	# 'libsystemd' related options
		'--with-udev-rules-dir=/usr/lib/udev/rules.d'
		#'--disable-udev'
		#'--disable-systemd-daemon'
		#'--disable-systemd-login'
		#'--disable-systemd-journal'

	# 'libwrap' related options
		#'--enable-tcpwrap'
		'--disable-tcpwrap'

	# 'lirc' related options
		'--enable-lirc'
		#'--disable-lirc'

	# 'openssl' related options
		'--enable-openssl'
		#'--disable-openssl'

	# 'orc' related options
		#'--enable-orc'
		'--disable-orc'

	# 'sbc' related options
		'--enable-bluez5'
		#'--disable-bluez5'
		'--enable-bluez5-ofono-headset'
		#'--disable-bluez5-ofono-headset'
		'--enable-bluez5-native-headset'
		#'--disable-bluez5-native-headset'

	# 'speexdsp' related options
		#'--with-speex'
		'--without-speex'

	# 'tdb' related options
		#'--with-database=tdb'

	# 'webrtc-audio-processing' related options
		#'--enable-webrtc-aec'
		'--disable-webrtc-aec'
	)

	# Change compile flags
	export CFLAGS+=' -fno-lto'
	export CXXFLAGS+="${CFLAGS}"

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Pulseaudio                                                             #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_pulseaudio() {
	pkgdesc="A featureful, general-purpose sound server"
	depends=(
		"libpulse=$pkgver-$pkgrel"
		'alsa-lib'
		'dbus'
		'gcc-libs'
		'gdbm'
		'glibc'
		'libcap'
		'libice'
		#'libsamplerate'
		'libsm'
		'libsndfile'
		#'libsoxr'
		'libtool'
		'libx11'
		'libxcb'
		'libxtst'
		'openssl'
		#'orc'
		#'speexdsp'
		'systemd-libs'
		#'tdb'
		#'webrtc-audio-processing'
	)
	optdepends=(
		'pulseaudio-alsa: ALSA configuration (recommended)'
	)
	replaces=('pulseaudio-xen<=9.0')
	backup=(
		'etc/pulse/daemon.conf'
		'etc/pulse/default.pa'
		'etc/pulse/system.pa'
	)
	install="pulseaudio.install"

	# Change directory to source directory
	cd $pkgbase

	# Install files
	make DESTDIR=$pkgdir install

	# Change directory to package directory
	cd $pkgdir

	# Remove dbus config file
	rm etc/dbus-1/system.d/pulseaudio-system.conf

	# Disable flat-volumes
	sed -e '/flat-volumes/iflat-volumes = no' -i etc/pulse/daemon.conf

	# Disable autospawn
	sed -e '/autospawn/iautospawn = no' -i etc/pulse/client.conf

	# Disable cork-request module
	sed -e 's|/usr/bin/pactl load-module module-x11-cork-request|#&|' -i usr/bin/start-pulseaudio-x11

	# Split libpulse
	mkdir -p $srcdir/libpulse/{etc/pulse,usr/{bin,lib/pulseaudio,share/man/man{1,5}}}
	mv etc/pulse/client.conf $srcdir/libpulse/etc/pulse || true
	mv usr/bin/pa{cat,ctl,mon,play,rec,record} $srcdir/libpulse/usr/bin || true
	mv usr/lib/libpulse{,-simple,-mainloop-glib}.so* $srcdir/libpulse/usr/lib || true
	mv usr/lib/{cmake,pkgconfig} $srcdir/libpulse/usr/lib || true
	mv usr/lib/pulseaudio/libpulsecommon-*.so $srcdir/libpulse/usr/lib/pulseaudio || true
	mv usr/include $srcdir/libpulse/usr || true
	mv usr/share/man/man1/pa{cat,ctl,dsp,mon,play,rec,record}.1 $srcdir/libpulse/usr/share/man/man1 || true
	mv usr/share/man/man5/pulse-client.conf.5 $srcdir/libpulse/usr/share/man/man5 || true
	mv usr/share/vala $srcdir/libpulse/usr/share || true

	# Split modules
	mkdir -p $srcdir/{gconf,zeroconf,lirc,jack,bluetooth,equalizer}/usr/lib/pulse-${pkgver%.*}/modules \
	$srcdir/{gconf/usr/lib/pulse,equalizer/usr/bin}
	mv usr/lib/pulse-${pkgver%.*}/modules/module-gconf.so \
	$srcdir/gconf/usr/lib/pulse-${pkgver%.*}/modules || true
	mv usr/lib/pulse/gconf-helper \
	$srcdir/gconf/usr/lib/pulse || true
	mv usr/lib/pulse-${pkgver%.*}/modules/{libavahi-wrap,module-{zeroconf-{publish,discover},raop-discover}}.so \
	$srcdir/zeroconf/usr/lib/pulse-${pkgver%.*}/modules || true
	mv usr/lib/pulse-${pkgver%.*}/modules/module-lirc.so \
	$srcdir/lirc/usr/lib/pulse-${pkgver%.*}/modules || true
	mv usr/lib/pulse-${pkgver%.*}/modules/module-jack{-sink,-source,dbus-detect}.so \
	$srcdir/jack/usr/lib/pulse-${pkgver%.*}/modules || true
	mv usr/lib/pulse-${pkgver%.*}/modules/{libbluez5-util,module-{bluetooth-{discover,policy},bluez5-{discover,device}}}.so \
	$srcdir/bluetooth/usr/lib/pulse-${pkgver%.*}/modules || true
	mv usr/lib/pulse-${pkgver%.*}/modules/module-equalizer-sink.so \
	$srcdir/equalizer/usr/lib/pulse-${pkgver%.*}/modules || true
	mv usr/bin/qpaeq \
	$srcdir/equalizer/usr/bin || true
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Libpulse                                                               #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_libpulse() {
	pkgdesc="$pkgdesc (client library)"
	license=('LGPL')
	depends=(
		'dbus'
		'gcc-libs'
		'glib2'
		'glibc'
		#'libasyncns'
		'libsndfile'
		#'libwrap'
		'libxcb'
		'systemd-libs'
	)
	backup=('etc/pulse/client.conf')

	# Copy files
	mv $srcdir/libpulse/* $pkgdir
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Pulseaudio-gconf                                                       #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_pulseaudio-gconf() {
	pkgdesc="GConf support for PulseAudio"
	depends=(
		"libpulse=$pkgver-$pkgrel"
		"pulseaudio=$pkgver-$pkgrel"
		'gcc-libs'
		'gconf'
		'glib2'
		'glibc'
	)

	# Copy files
	mv $srcdir/gconf/* $pkgdir
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Pulseaudio-zeroconf                                                    #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_pulseaudio-zeroconf() {
	pkgdesc="Zeroconf support for PulseAudio"
	depends=(
		"libpulse=$pkgver-$pkgrel"
		"pulseaudio=$pkgver-$pkgrel"
		'avahi'
		'dbus'
		'gcc-libs'
		'glibc'
	)

	# Copy files
	mv $srcdir/zeroconf/* $pkgdir
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Pulseaudio-lirc                                                        #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_pulseaudio-lirc() {
	pkgdesc="IR (lirc) support for PulseAudio"
	depends=(
		"libpulse=$pkgver-$pkgrel"
		"pulseaudio=$pkgver-$pkgrel"
		'gcc-libs'
		'glibc'
		'lirc'
	)

	# Copy files
	mv $srcdir/lirc/* $pkgdir
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Pulseaudio-jack                                                        #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_pulseaudio-jack() {
	pkgdesc="Jack support for PulseAudio"
	depends=(
		"libpulse=$pkgver-$pkgrel"
		"pulseaudio=$pkgver-$pkgrel"
		'dbus'
		'gcc-libs'
		'glibc'
		'jack'
	)

	# Copy files
	mv $srcdir/jack/* $pkgdir
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Pulseaudio-bluetooth                                                   #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_pulseaudio-bluetooth() {
	pkgdesc="Bluetooth support for PulseAudio"
	depends=(
		"libpulse=$pkgver-$pkgrel"
		"pulseaudio=$pkgver-$pkgrel"
		'dbus'
		'gcc-libs'
		'glibc'
		'sbc'
	)

	# Copy files
	mv $srcdir/bluetooth/* $pkgdir
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Pulseaudio-equalizer                                                   #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_pulseaudio-equalizer() {
	pkgdesc="Equalizer for PulseAudio"
	license=('AGPL3')
	depends=(
		"libpulse=$pkgver-$pkgrel"
		"pulseaudio=$pkgver-$pkgrel"
		'dbus'
		'fftw'
		'gcc-libs'
		'glibc'
	)

	# Copy files
	mv $srcdir/equalizer/* $pkgdir
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
