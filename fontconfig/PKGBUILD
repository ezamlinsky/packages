################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('fontconfig')
pkgver=2.13.91
pkgrel=1
epoch=2
pkgdesc='A library for configuring and customizing font access'
url='https://www.freedesktop.org/wiki/Software/fontconfig/'
license=('custom')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'expat'
	'freetype2'
	'glibc'
	'libutil-linux'
	#'libxml2'
)
makedepends=(
	'git'
	'gperf'
)

#==============================================================================#
#       Package miscellaneous options                                          #
#==============================================================================#
install='fontconfig.install'

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_commit='75eadca26648abf69497691ff0f4c7803b9ff23c'  # master
source=(
	"git+https://gitlab.freedesktop.org/fontconfig/fontconfig.git#commit=$_commit"
	'fontconfig.hook'
)
sha256sums=(
	'SKIP'
	'672f6a1c5e164671955ce807e670306194142a1794ce88df653aa717a972e274'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-docs'
		'--enable-iconv'
		'--with-xmldir=/etc/fonts'
		'--with-templatedir=/etc/fonts/conf.avail'
		'--with-default-fonts=/usr/share/fonts'
		'--with-add-fonts=/usr/local/share/fonts'
		'--with-default-hinting=slight'

	# 'expat' related options
		'--disable-libxml2'

	# 'libxml2' related options
		#'--enable-libxml2'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname

	# Install files
	make DESTDIR=$pkgdir install

	# Add vendor's hooks
	install -Dm644 $srcdir/fontconfig.hook $pkgdir/usr/share/libalpm/hooks/fontconfig.hook

	# Install license file
	install -Dm644 COPYING $pkgdir/usr/share/licenses/fontconfig/COPYING
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
