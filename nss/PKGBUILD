################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgbase='nss'
pkgname=('nss' 'ca-certificates-mozilla')
_nsprver=4.20
pkgver=3.46
pkgrel=1
url='https://developer.mozilla.org/en-US/docs/Mozilla/Projects/NSS'
license=('MPL' 'GPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
makedepends=(
	'gyp'
	'perl'
	'python'
	'python2'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://ftp.mozilla.org/pub/security/nss/releases/NSS_${pkgver//./_}_RTM/src/nss-${pkgver}.tar.gz"
	'certdata2pem.py'
	'bundle.sh'
)
sha256sums=(
	'6b699649d285602ba258a4b0957cb841eafc94eff5735a9da8da0adbb9a10cef'
	'0be02cecc27a6e55e1cad1783033b147f502b26f9fb1bb5a53e7a43bbcb68fa0'
	'3bfadf722da6773bdabdd25bdf78158648043d1b7e57615574f189a88ca865dd'
)

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgbase-$pkgver/$pkgbase

	# Create certificates
	mkdir -p certs && cd certs
	ln -s ../lib/ckfw/builtins/certdata.txt
	ln -s ../lib/ckfw/builtins/nssckbi.h
	python $srcdir/certdata2pem.py

	# Create 'ca-bundle.trust.p11-kit' file
	cd .. && sh ../../bundle.sh

	# Default configure options
	config_opts=(
		'--opt'
		'--gcc'
		'--target=x64'
		'--disable-tests'
		'--enable-libpkix'

	# 'nspr' related options
		'--system-nspr'

	# 'sqlite' related options
		'--system-sqlite'
	)

	# Change compile flags
	export CFLAGS+=' -fno-stack-protector'
	export CXXFLAGS="${CFLAGS}"

	# Run package configuration script
	./build.sh "${config_opts[@]}"
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Nss                                                                    #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_nss() {
	pkgdesc='Network Security Services'
	depends=(
		"nspr>=${_nsprver}"
		'gcc-libs'
		'glibc'
		'nspr'
		'p11-kit'
		'sh'
		'sqlite'
		'zlib'
	)

	# Change directory to source directory
	cd $pkgbase-$pkgver/$pkgbase

	# Install files
	mkdir -p $pkgdir/usr/{bin,lib,include/nss,share/man/man1}
	mv ../dist/Release/bin/* $pkgdir/usr/bin
	mv ../dist/Release/lib/*.so $pkgdir/usr/lib
	mv ../dist/Release/lib/*.chk $pkgdir/usr/lib
	mv ../dist/public/nss/*.h $pkgdir/usr/include/nss

	# Install documentation
	install -t $pkgdir/usr/share/man/man1 -m644 doc/nroff/*

	# Extract version info from nss header file
	{ read _vmajor; read _vminor; read _vpatch; } \
	< <(awk '/#define.*NSS_V(MAJOR|MINOR|PATCH)/ {print $3}' lib/nss/nss.h)

	# Install 'nss-config' script
	sed pkg/pkg-config/nss-config.in \
	-e "s,@libdir@,/usr/lib,g" \
	-e "s,@prefix@,/usr/bin,g" \
	-e "s,@exec_prefix@,/usr/bin,g" \
	-e "s,@includedir@,/usr/include/nss,g" \
	-e "s,@MOD_MAJOR_VERSION@,${_vmajor},g" \
	-e "s,@MOD_MINOR_VERSION@,${_vminor},g" \
	-e "s,@MOD_PATCH_VERSION@,${_vpatch},g" |
	install -D /dev/stdin $pkgdir/usr/bin/nss-config

	# Install pkg-config files for nss
	sed pkg/pkg-config/nss.pc.in \
	-e "s,%libdir%,/usr/lib,g" \
	-e "s,%prefix%,/usr,g" \
	-e "s,%exec_prefix%,/usr/bin,g" \
	-e "s,%includedir%,/usr/include/nss,g" \
	-e "s,%NSPR_VERSION%,${_nsprver},g" \
	-e "s,%NSS_VERSION%,${pkgver},g" |
	install -Dm644 /dev/stdin $pkgdir/usr/lib/pkgconfig/nss.pc
	ln -s nss.pc $pkgdir/usr/lib/pkgconfig/mozilla-nss.pc

	# Replace built-in trust with p11-kit connection
	ln -sf libnssckbi-p11-kit.so $pkgdir/usr/lib/libnssckbi.so

	# Remove non required files
	rm -f $pkgdir/usr/bin/{hw-support,pwdecrypt}
	rm -f $pkgdir/usr/share/man/man1/{derdump.1,pp.1,vfychain.1,vfyserv.1}
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Ca-certificates-mozilla                                                #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_ca-certificates-mozilla() {
	pkgdesc="Mozilla's set of trusted CA certificates"
	depends=('ca-certificates-utils')

	# Install files
	install -Dm644 $pkgbase-$pkgver/$pkgbase/ca-bundle.trust.p11-kit \
	$pkgdir/usr/share/ca-certificates/trust-source/mozilla.trust.p11-kit
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
