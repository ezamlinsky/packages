################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('libburn')
pkgver=1.5.2
pkgrel=1
pkgdesc='Library for reading, mastering and writing optical discs'
url='https://dev.lovelyhq.com/libburnia'
license=('GPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'glibc'
	#'libcdio'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"http://files.libburnia-project.org/releases/$pkgname-$pkgver.tar.gz"
	"http://files.libburnia-project.org/releases/$pkgname-$pkgver.tar.gz.sig"
)
sha256sums=(
	'7b32db1719d7f6516cce82a9d00dfddfb3581725db732ea87d41ea8ef0ce5227'
	'SKIP'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-debug'
		'--disable-dvd-obs-64k'
		'--disable-dvd-obs-pad'
		'--disable-track-src-odirect'
		'--enable-versioned-libs'
		'--enable-libdir-pkgconfig'
		'--enable-ldconfig-at-install'

	# 'libcdio' related options
		#'--enable-libcdio'
		'--disable-libcdio'
		#'--enable-pkg-check-modules
		'--disable-pkg-check-modules'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
