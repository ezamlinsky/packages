################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('lzo')
pkgver=2.10
pkgrel=3
pkgdesc='Portable lossless data compression library'
url='http://www.oberhumer.com/opensource/lzo'
license=('GPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=('glibc')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=("http://www.oberhumer.com/opensource/lzo/download/lzo-$pkgver.tar.gz")
sha1sums=('4924676a9bae5db58ef129dc1cebce3baa3c4b5d')

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd lzo-$pkgver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd lzo-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--enable-asm'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make

	# Build minilzo
	gcc $CFLAGS -fpic -Iinclude/lzo -o minilzo/minilzo.o -c minilzo/minilzo.c
	gcc $LDFLAGS -shared -o libminilzo.so.0 -Wl,-soname,libminilzo.so.0 minilzo/minilzo.o
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd lzo-$pkgver

	# Run internal checks
	make -k check
	make -k test
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd lzo-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Install minilzo
	install -m 755 libminilzo.so.0 $pkgdir/usr/lib
	install -p -m 644 minilzo/minilzo.h $pkgdir/usr/include/lzo
	cd $pkgdir/usr/lib && ln -s libminilzo.so.0 libminilzo.so
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
