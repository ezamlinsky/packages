################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('make')
pkgver=4.2.1
pkgrel=3
pkgdesc='GNU make utility to maintain groups of programs'
url='http://www.gnu.org/software/make'
license=('GPL3')
arch=('x86_64')
groups=('base-devel')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'glibc'
	#'guile'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"ftp://ftp.gnu.org/gnu/make/make-$pkgver.tar.bz2"
	"ftp://ftp.gnu.org/gnu/make/make-$pkgver.tar.bz2.sig"
	'guile-2.2.patch'
	'make-4.2.1-glob1.patch'
	'make-4.2.1-glob2.patch'
	'make-4.2.1-test-driver.patch'
)
md5sums=(
	'15b012617e7c44c0ed482721629577ac'
	'SKIP'
	'89bbbe3f806f208608e117665feb562b'
	'05a3b10e0da01d84cd078905a11437ed'
	'6f5dbcb368728582b34bac197f0d3197'
	'af63eff24206e1fa06af64b8ec7b07b9'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Apply patches
	patch -p1 -i $srcdir/guile-2.2.patch
	patch -p1 -i $srcdir/make-4.2.1-glob1.patch
	patch -p1 -i $srcdir/make-4.2.1-glob2.patch
	patch -p1 -i $srcdir/make-4.2.1-test-driver.patch

	# Run automatic configure script
	autoreconf -f -i

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'

	# 'guile' related options
		#'--with-guile'
		'--without-guile'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
