################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('x265')
pkgver=3.2.1
pkgrel=1
pkgdesc='Open Source H265/HEVC video encoder'
url='https://bitbucket.org/multicoreware/x265'
license=('GPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'gcc-libs'
	'glibc'
	#'numactl'
)
makedepends=(
	'cmake'
	'mercurial'
	'nasm'
)

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
provides=('libx265.so')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_commit='7fa570ead8d361bf6055cd2a881a8e15f12110ae'
source=("hg+https://bitbucket.org/multicoreware/x265#revision=$_commit")
sha256sums=('SKIP')

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname

	# Default configure options
	config_opts=(
		"-DCMAKE_C_FLAGS='$CFLAGS'"
		"-DCMAKE_CXX_FLAGS='$CXXFLAGS'"
		"-DCMAKE_LD_FLAGS='$LDFLAGS'"
		'-DCMAKE_INSTALL_PREFIX=/usr'
		'-DCMAKE_BUILD_TYPE=Release'
		'-DWARNINGS_AS_ERRORS=OFF'
		'-DNATIVE_BUILD=ON'
		'-DENABLE_ASSEMBLY=ON'
		'-DENABLE_SHARED=ON'
		'-DENABLE_PIC=ON'
		'-DENABLE_HDR10_PLUS=ON'
		'-DENABLE_SVT_HEVC=OFF'
		'-DENABLE_CLI=ON'
		'-DENABLE_TESTS=OFF'
		'-DENABLE_AGGRESSIVE_CHECKS=OFF'
		'-DCHECKED_BUILD=OFF'
		'-DHIGH_BIT_DEPTH=ON'
		'-DDETAILED_CU_STATS=OFF'

	# 'numactl' related options
		#'-DENABLE_LIBNUMA=ON'
		'-DENABLE_LIBNUMA=OFF'

	# 'vmaf' related options
		#'-DENABLE_LIBVMAF=ON'
		'-DENABLE_LIBVMAF=OFF'
	)

	# Create build directory
	mkdir -p build && cd build

	# Run package configuration script
	cmake ../source "${config_opts[@]}"

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname/build

	# Install files
	make DESTDIR=$pkgdir install
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
