################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('libsoup')
pkgver=2.68.2
pkgrel=1
pkgdesc='HTTP client/server library for GNOME'
url='https://wiki.gnome.org/Projects/libsoup'
license=('LGPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'brotli'
	'glib2'
	#'glib-networking'
	'glibc'
	'libpsl'
	'libxml2'
	'sqlite'
	#'krb5'
	'zlib'
)
makedepends=(
	'git'
	'gtk-doc'
	'intltool'
	'python'
	'vala'
	'meson'
)
checkdepends=(
	'apache'
	'php-apache'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_commit='5b2cf0eab0386322ed3f25d9b671c00dfbb93471'	# tags/2.68.2^0
source=("git+https://gitlab.gnome.org/GNOME/libsoup.git#commit=$_commit")
sha256sums=('SKIP')

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname

	# Default configure options
	config_opts=(
		'-Dntlm=disabled'
		'-Dgnome=true'
		'-Dgtk_doc=true'
		'-Dtests=false'
		'-Dinstalled_tests=false'
		'-Dintrospection=disabled'

	# 'brotli' related options
		'-Dbrotli=enabled'
		#'-Dbrotli=disabled'

	# 'glib-networking' related options
		#'-Dtls_check=true'
		'-Dtls_check=false'

	# 'krb5' related options
		#'-Dgssapi=enabled'
		'-Dgssapi=disabled'

	# 'vala' related options
		#'-Dvapi=enabled'
		'-Dvapi=disabled'
	)

	# Run package configuration script
	arch-meson build ${config_opts[@]}

	# Build package
	ninja -C build
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname

	# Run internal tests
	ninja -C build test
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname

	# Install files
	DESTDIR=$pkgdir ninja -C build install
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
