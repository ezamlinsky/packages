################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('x264')
pkgver=0.159.r2991.1771b55
pkgrel=1
epoch=3
pkgdesc='Open Source H264/AVC video encoder'
url='https://www.videolan.org/developers/x264.html'
license=('GPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	#'ffms2'
	#'gpac'
	'glibc'
	'libavformat.so'
	'libavutil.so'
	#'liblsmash.so'
	'libswscale.so'
)
makedepends=(
	'ffmpeg'
	'git'
	'nasm'
)

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
provides=(
	'libx264'
	'libx264.so'
)
conflicts=(
	'libx264'
	'libx264-10bit'
	'libx264-all'
)
replaces=(
	'libx264'
	'libx264-10bit'
	'libx264-all'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_commit='1771b556ee45207f8711744ccbd5d42a3949b14c'
source=("git+https://code.videolan.org/videolan/x264.git#commit=$_commit")
sha256sums=('SKIP')

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgbase

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-debug'
		'--enable-thread'
		'--enable-cli'
		'--enable-interlaced'
		'--enable-opencl'
		'--enable-asm'
		'--enable-pic'
		'--enable-lto'
		'--enable-strip'
		'--bit-depth=all'
		'--chroma-format=all'

	# 'ffms2' related options
		#'--enable-ffms'
		'--disable-ffms'
		#'--enable-avs'
		'--disable-avs'

	# 'gpac' related options
		#'--enable-gpac'
		'--disable-gpac'

	# 'libavformat.so' related options
		'--enable-lavf'
		#'--disable-lavf'

	# 'liblsmash.so' related options
		#'--enable-lsmash'
		'--disable-lsmash'

	# 'libswscale.so' related options
		'--enable-swscale'
		#'--disable-swscale'
	)

	# Change compile flags
	export CFLAGS+=' -fno-lto'
	export CXXFLAGS="${CFLAGS}"

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgbase

	# Install files
	make DESTDIR=$pkgdir install-cli install-lib-shared
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
