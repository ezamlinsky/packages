################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgbase='elfutils'
pkgname=('elfutils' 'libelf')
pkgver=0.178
pkgrel=2
url='https://sourceware.org/elfutils/'
license=('LGPL3' 'GPL' 'GPL3')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
makedepends=(
	'bzip2'
	'curl'
	'gcc-libs'
	'glibc'
	'libarchive'
	'libmicrohttpd'
	'sh'
	'sqlite'
	'xz'
	'zlib'
)

#==============================================================================#
#       Package miscellaneous options                                          #
#==============================================================================#
options=('staticlibs')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"ftp://sourceware.org/pub/$pkgbase/$pkgver/$pkgbase-$pkgver.tar.bz2"
	"ftp://sourceware.org/pub/$pkgbase/$pkgver/$pkgbase-$pkgver.tar.bz2.sig"
)
sha1sums=(
	'5f52d04105a89e50caf69cea40629c323c1eccd9'
	'SKIP'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgbase-$pkgver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgbase-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--program-prefix=eu-'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--enable-thread-safety'
		'--enable-deterministic-archives'
		'--enable-symbol-versioning'

	# 'bzip2' related options
		'--with-bzlib'
		#'--without-bzlib'

	# 'curl && ibarchive && libmicrohttpd && sqlite' related options
		#'--enable-debuginfod'
		'--disable-debuginfod'

	# 'xz' related options
		'--with-lzma'
		#'--without-lzma'

	# 'zlib' related options
		'--with-zlib'
		#'--without-zlib'
	)

	# Change compile flags
	export CFLAGS+=' -fno-lto -Wno-error'
	export CXXFLAGS="${CFLAGS}"

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgbase-$pkgver

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Elfutils                                                               #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_elfutils() {
	pkgdesc='Utilities to handle ELF object files and DWARF debugging information'
	depends=(
		"libelf=$pkgver-$pkgrel"
		'gcc-libs'
		'glibc'
		#'libarchive'
		#'libmicrohttpd'
		'sh'
		#'sqlite'
	)

	# Change directory to source directory
	cd $pkgbase-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Change directory to target directory
	cd $pkgdir

	# Split 'libelf' package
	mkdir -p $srcdir/libelf/usr
	mv $pkgdir/usr/{lib,include} $srcdir/libelf/usr || true
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Libelf                                                                 #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_libelf() {
	pkgdesc='Libraries to handle ELF object files and DWARF debugging information'
	depends=(
		'bzip2'
		#'curl'
		'glibc'
		'xz'
		'zlib'
	)

	# Copy files
	mv $srcdir/libelf/* $pkgdir
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
