################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgbase='gtkmm3'
pkgname=('gtkmm3' 'gtkmm3-docs')
pkgver=3.24.2
pkgrel=1
pkgdesc='C++ bindings for GTK+ 3'
url='http://www.gtkmm.org/'
license=('LGPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
makedepends=(
	'atkmm-docs'
	'cairomm-docs'
	'git'
	'glibmm-docs'
	'mm-common'
	'pangomm-docs'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_commit='72c50bb8629558e1f0df3d86c16e4290cc94fd5e'  # tags/3.24.2^0
source=("git+https://gitlab.gnome.org/GNOME/gtkmm.git#commit=$_commit")
sha256sums=('SKIP')

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd gtkmm

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd gtkmm

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--enable-documentation'
		'--enable-maintainer-mode'
		'--enable-api-atkmm'
		'--enable-x11-backend=yes'
		'--enable-wayland-backend=no'
		'--enable-broadway-backend=no'
		'--enable-warnings=no'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd gtkmm

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Gtkmm3                                                                 #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_gtkmm3() {
	pkgdesc='C++ bindings for gtk3'
	depends=(
		'atkmm'
		'cairomm'
		'gcc-libs'
		'gdk-pixbuf2'
		'glib2'
		'glibc'
		'glibmm'
		'gtk3'
		'libsigc++'
		'pangomm'
	)

	# Change directory to source directory
	cd gtkmm

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 COPYING $pkgdir/usr/share/licenses/gtkmm3/LICENSE

	# Change directory to target directory
	cd $pkgdir

	# Split 'gtkmm-docs' package
	mkdir -p $srcdir/gtkmm3-docs/usr
	mv usr/share $srcdir/gtkmm3-docs/usr || true
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Gtkmm3-docs                                                            #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_gtkmm3-docs() {
	pkgdesc='C++ bindings for gtk3 (documentation)'
	depends=()
	options=('docs')

	# Copy files
	mv $srcdir/gtkmm3-docs/* $pkgdir
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
