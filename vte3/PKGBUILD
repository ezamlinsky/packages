################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgbase='vte3'
pkgname=('vte3' 'vte-common')
pkgver=0.60.0
pkgrel=1
url='https://wiki.gnome.org/Apps/Terminal/VTE'
license=('LGPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
makedepends=(
	'git'
	'glade'
	'gobject-introspection'
	'gperf'
	'gtk-doc'
	'intltool'
	'meson'
	'vala'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_commit='bf56194f78fc13a99023358efb6f7af0f228240c'
source=("git+https://gitlab.gnome.org/GNOME/vte.git#commit=$_commit")
sha256sums=('SKIP')

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd vte

	# Default configure options
	config_opts=(
		'-Db_lto=false'
		'-Ddebugg=false'
		'-Ddocs=true'
		'-Dgir=false'
		'-Diconv=true'
		'-Da11y=true'

	# 'fribidi' related options
		'-Dfribidi=true'
		#'-Dfribidi=false'

	# 'gnutls' related options
		'-Dgnutls=true'
		#'-Dgnutls=false'

	# 'gtk3' related options
		'-Dgtk3=true'
		#'-Dgtk3=false'

	# 'gtk4' related options
		#'-Dgtk4=true'
		'-Dgtk4=false'

	# 'icu' related options
		'-Dicu=true'
		#'-Dicu=false'

	# 'systemd-libs' related options
		'-D_systemd=true'
		#'-D_systemd=false'

	# 'vala' related options
		#'-Dvapi=true'
		'-Dvapi=false'
	)

	# Run package configuration script
	arch-meson build "${config_opts[@]}"

	# Build package
	ninja -C build
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd vte

	# Run internal tests
	ninja -C build test
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Vte3                                                                   #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_vte3() {
	pkgdesc='Virtual Terminal Emulator widget for use with GTK3'
	depends=(
		'atk'
		'cairo'
		'fribidi'
		'gcc-libs'
		'gdk-pixbuf2'
		'glib2'
		'glibc'
		'gnutls'
		'gtk3'
		#'gtk4'
		'icu'
		'pango'
		'pcre2'
		'systemd-libs'
		#'vala'
		'vte-common'
		'zlib'
	)

	# Change directory to source directory
	cd vte

	# Install files
	DESTDIR=$pkgdir ninja -C build install

	# Change directory to target directory
	cd $pkgdir

	# Split 'vte-common' package
	mkdir -p $srcdir/vte-common/usr/lib
	mv etc $srcdir/vte-common || true
	mv usr/lib/vte-urlencode-cwd $srcdir/vte-common/usr/lib || true
	mv usr/lib/systemd $srcdir/vte-common/usr/lib || true
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Vte-common                                                             #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_vte-common() {
	pkgdesc='Files shared by VTE libraries'
	depends=('sh')

	# Copy files
	mv $srcdir/vte-common/* $pkgdir
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
