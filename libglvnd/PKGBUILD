################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('libglvnd')
pkgver=1.3.1
pkgrel=1
pkgdesc='The GL Vendor-Neutral Dispatch library'
url='https://github.com/NVIDIA/libglvnd'
license=('custom:BSD-like')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'glibc'
	'libx11'
)
makedepends=(
	'libxext'
	'meson'
	'python'
	'xorgproto'
)

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
provides=(
	'libegl'
	'libgl'
	'libgles'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"$pkgname-$pkgver.tar.gz::https://gitlab.freedesktop.org/glvnd/$pkgname/-/archive/v${pkgver}/$pkgname-v${pkgver}.tar.gz"
	'LICENSE'
)
sha512sums=(
	'f10b24ea983ee27813b7b64f13419d309479e34f3a1e4304f57ed01732d66df8d9d2c1e4f76577fc0827305d1b0a2fcea18445ce2953641f1d8452aa65b22e14'
	'bf0f4a7e04220a407400f89226ecc1f798cc43035f2538cc8860e5088e1f84140baf0d4b0b28f66e4b802d4d6925769a1297c24e1ba39c1c093902b2931781a5'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-v$pkgver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-v$pkgver

	# Default configure options
	config_opts=(
		'-Dasm=enabled'
		'-Dtls=enabled'
		'-Degl=true'
		'-Dglx=enabled'
		'-Dgles1=true'
		'-Dgles2=true'
		'-Dheaders=true'

	# 'libx11' related options
		'-Dx11=enabled'
		#'-Dx11=disabled'
	)

	# Run package configuration script
	arch-meson build "${config_opts[@]}"

	# Build package
	ninja -C build
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-v$pkgver

	# Install files
	DESTDIR=$pkgdir ninja -C build install

	# Install license file
	install -Dm644 ../LICENSE $pkgdir/usr/share/licenses/libglvnd/COPYING
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
