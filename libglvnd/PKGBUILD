################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('libglvnd')
pkgver=1.2.0
pkgrel=5
pkgdesc='The GL Vendor-Neutral Dispatch library'
url='https://github.com/NVIDIA/libglvnd'
license=('custom:BSD-like')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'glibc'
	'libx11'
)
makedepends=(
	'glproto'
	'libx11'
	'libxext'
	'python'
)

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
provides=(
	'libegl'
	'libgl'
	'libgles'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"$pkgname-$pkgver.tar.gz::https://github.com/NVIDIA/libglvnd/archive/v$pkgver.tar.gz"
	'0001-Add-a-configure-option-to-disable-glesv1-or-glesv2.patch'
	'update-gl-h-to-match-mesa.patch'
	'egl-sync-with-khronos.patch'
	'EGL-Set-EGL_NO_X11-macro.patch'
	'LICENSE'
)
sha512sums=(
	'0e0e9c3f303739c73f5bee4c2aaef9eff56440345f2f1ce285ee98eb94c1e5e740774dd501aeeed1299aa25a449d813a850e95282999a3ca4648d2381d6c8fa6'
	'b2d2e8fd7ea92bfc913d0b808d15c801e7c3ceb484bdd60700fd1c9ce27b4fbd4b68e568e967599f2e5132cc1ed382ec87a07c16bd3bfa5c4d920f2d9600e689'
	'46c73df9ae0b730eedcaf44b5c4ea8e70d2fa5652d9e410b757eede3e6defb334e07ce6652cc54d65653521b1e244e3e206eedb2c7c61efd6ae6409774fd9229'
	'2099ba851e32cbae79e234e5b695de2441c27aa213bc192f20a02be124179a2d2f4bf098e185715befd4a6a548262a7fced06b57dcfc4eaac375cd7915e27da8'
	'ce11a4ef523c26a650fa926826b430ea561d5b3fef5e61b388258616b7d87e017f2537035e48a5b177b36ca6dc99d4d88b26d6de7135f49fb4150fe1d2f3b3a9'
	'bf0f4a7e04220a407400f89226ecc1f798cc43035f2538cc8860e5088e1f84140baf0d4b0b28f66e4b802d4d6925769a1297c24e1ba39c1c093902b2931781a5'
)
#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Apply patches
	patch -Np1 -i $srcdir/0001-Add-a-configure-option-to-disable-glesv1-or-glesv2.patch
	patch -Np1 -i $srcdir/update-gl-h-to-match-mesa.patch
	patch -Np1 -i $srcdir/egl-sync-with-khronos.patch
	patch -Np1 -i $srcdir/EGL-Set-EGL_NO_X11-macro.patch

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--enable-debug=no'
		'--enable-egl'
		'--enable-glx'
		'--disable-gles1'
		'--enable-gles2'
		'--enable-asm'
		'--enable-tls'

	# 'libx11' related options
		'--enable-x11'
		#'--disable-x11'

	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 ../LICENSE $pkgdir/usr/share/licenses/libglvnd/LICENSE
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
