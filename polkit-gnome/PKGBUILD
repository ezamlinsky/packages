################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('polkit-gnome')
pkgver=0.105
pkgrel=4
pkgdesc='Legacy polkit authentication agent for GNOME'
url='https://www.freedesktop.org/wiki/Software/polkit/'
license=('LGPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'gdk-pixbuf2'
	'glib2'
	'glibc'
	'gtk3'
	'polkit'
)
makedepends=('intltool')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://download.gnome.org/sources/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"
	'polkit-gnome-authentication-agent-1.desktop'
)
sha512sums=(
	'eae2504959bca6f2b53dbad1b743fc0414b82897e62623af80ee74c4080a10d6b07a4c90151ee1264891f27373321b8697ac8a747cba5254c765b4cd9161bd42'
	'8b24b7a39556e6a17284e052baa7af81237401e4010049839871ea5487643c92b2600f513213b9003a1a452492540e1d1cf6a5faf7a2ce5a28ad4f00e4b835aa'
)

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libexecdir=/usr/lib/polkit-gnome'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-debug'
	)

	# Run package configuration script
	./configure ${config_opts[@]}

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Install 'polkit-gnome-authentication-agent-1.desktop' file
	install -Dm644 $srcdir/polkit-gnome-authentication-agent-1.desktop \
	$pkgdir/usr/share/applications/polkit-gnome-authentication-agent-1.desktop
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
