################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('pcre2')
pkgver=10.33
pkgrel=1
pkgdesc='A library that implements Perl 5-style regular expressions. 2nd version'
url='http://www.pcre.org/'
license=('BSD')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'bash'
	'bzip2'
	'glibc'
	#'libedit'
	'readline'
	'zlib'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://ftp.pcre.org/pub/pcre/$pkgname-$pkgver.tar.bz2"
	"https://ftp.pcre.org/pub/pcre/$pkgname-$pkgver.tar.bz2.sig"
)
sha512sums=(
	'c6aa52cdbb0c906b02e9a9f255697a1b9b99d9c7dccdaa484ffdb04fe3582d232ba3cb3c421d6df589a387b415dedb0817f58ec5391488b25b4e7bd9da6bab8f'
	'SKIP'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-static'
		'--disable-debug'
		'--enable-pcre2-16'
		'--enable-pcre2-32'
		'--enable-unicode'
		'--enable-jit'
		'--enable-jit-sealloc'
		'--enable-pcre2grep-jit'
		'--enable-pcre2grep-callout'
		'--enable-pcre2grep-callout-fork'
		'--enable-fuzz-support'

	# 'bzip2' related options
		'--enable-pcre2grep-libbz2'
		#'--disable-pcre2grep-libbz2'

	# 'libedit' related options
		#'--enable-pcre2test-libedit'
		'--disable-pcre2test-libedit'

	# 'readline' related options
		'--enable-pcre2test-libreadline'
		#'--disable-pcre2test-libreadline'

	# 'zlib' related options
		'--enable-pcre2grep-libz'
		#'--disable-pcre2grep-libz'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 LICENCE $pkgdir/usr/share/licenses/pcre2/LICENCE
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
