################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname='nodejs'
pkgver=12.10.0
pkgrel=1
pkgdesc='Evented I/O for V8 javascript'
url='https://nodejs.org/'
license=('MIT')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'c-ares'
	'gcc-libs'
	'glibc'
	#'http-parser'
	'icu'
	'libnghttp2'
	'libuv'
	'openssl'
	'zlib'
)
makedepends=(
	'procps-ng'
	'python2'
)
optdepends=('npm: nodejs package manager')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=("nodejs-$pkgver.tar.gz::https://github.com/nodejs/node/archive/v${pkgver}.tar.gz")
sha512sums=('dcca3776f6794523313de5770c6a696b6354b567b960053e400ed5f7c3313d16720f4ef23e783c10b38a641346fdcc80a560d3434805b75f732559f9a00da5b3')

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd node-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--enable-lto'
		'--dest-os=linux'
		'--dest-cpu=x86_64'
		'--without-npm'
		'--experimental-http-parser'

	# 'c-ares' related options
		'--shared-cares'

	# 'http-parser' related options
		#'--shared-http-parser'

	# 'icu' related options
		'--with-intl=system-icu'
		'--without-intl'

	# 'libnghttp2' related options
		'--shared-nghttp2'

	# 'libuv' related options
		'--shared-libuv'

	# 'openssl' related options
		'--shared-openssl'
		#'--without-ssl'

	# 'zlib' related options
		'--shared-zlib'
	)

	# Change compile flags
	export CFLAGS+=' -fno-stack-protector'
	export CXXFLAGS+="${CFLAGS}"

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd node-$pkgver

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd node-$pkgver

	# Change compile flags
	export CFLAGS+=' -fno-stack-protector'
	export CXXFLAGS+="${CFLAGS}"

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 LICENSE $pkgdir/usr/share/licenses/nodejs/LICENSE
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
