################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('openssl')
pkgoname=('openssl')
pkgver=1.1.1.c
pkgrel=1
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security'
url='https://www.openssl.org'
license=('custom:BSD')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'glibc'
	'perl'
	#'lksctp-tools'
	'zlib'
)
optdepends=('ca-certificates')

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
provides=('openssl')
conflicts=('openssl')
replaces=('openssl')

#==============================================================================#
#       Package miscellaneous options                                          #
#==============================================================================#
backup=('etc/ssl/openssl.cnf')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_ver=1.1.1c
source=(
	"https://www.openssl.org/source/$pkgoname-$_ver.tar.gz"
	"https://www.openssl.org/source/$pkgoname-$_ver.tar.gz.asc"
	'ca-dir.patch'
)
sha256sums=(
	'f6fb3079ad15076154eda9413fed42877d668e7069d9b87396d0804fdb3f4c90'
	'SKIP'
	'0938c8d68110768db4f350a7ec641070686904f2fe7ba630ac94399d7dc8cc5e'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgoname-$_ver

	# Apply patches
	patch -p0 -i $srcdir/ca-dir.patch
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgoname-$_ver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--libdir=lib'
		'--openssldir=/etc/ssl'
		'--release'
		'threads'
		'shared'
		'enable-ec_nistp_64_gcc_128'
		'enable-heartbeats'
		'no-ssl3-method'

	# 'lksctp-tools' related options
		#'sctp'
		'no-sctp'

	# 'zlib' related options
		'zlib-dynamic'
		#'no-zlib'
	)

	# Run package configuration script
	./config "${config_opts[@]}"

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgoname-$_ver

	# The test fails due to missing write permissions in /etc/ssl
	# Revert this patch for make test
	patch -p0 -R -i $srcdir/ca-dir.patch

	# Run internal checks
	make -k test

	# Apply patches again
	patch -p0 -i $srcdir/ca-dir.patch
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgoname-$_ver

	# Install files
	make DESTDIR=$pkgdir MANSUFFIX=ssl install

	# Install license file
	install -Dm644 LICENSE $pkgdir/usr/share/licenses/openssl/LICENSE
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
