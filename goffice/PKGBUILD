################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('goffice')
pkgver=0.10.46
pkgrel=1
pkgdesc='A GLib/GTK+ set of document-centric objects and charting library'
url='https://git.gnome.org/browse/goffice'
license=('GPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'atk'
	'cairo'
	'gdk-pixbuf2'
	'glib2'
	'glibc'
	'gtk3'
	#'lasem'
	'libgsf'
	'librsvg'
	'libxml2'
	'libxslt'
	'pango'
)
makedepends=(
	'autoconf-archive'
	'git'
	'gobject-introspection'
	'gtk-doc'
	'intltool'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_commit='0da5282a9179cf63f8f0e6637f05aef595fec3e4'  # tags/GOFFICE_0_10_46^0
source=("git+https://gitlab.gnome.org/GNOME/goffice.git#commit=$_commit")
sha256sums=('SKIP')

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--enable-gtk-doc'
		'--enable-introspection=no'
		'--with-config-backend=keyfile'
		'--with-long-double'

	# 'gtk3' related options
		'--with-gtk'
		#'--without-gtk'

	# 'lasem' related options
		#'--with-lasem=yes'
		'--with-lasem=no'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname

	# Install files
	make DESTDIR=$pkgdir install
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
