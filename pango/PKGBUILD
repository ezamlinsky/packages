################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('pango')
pkgver=1.44.8
pkgrel=1
epoch=1
pkgdesc='A library for layout and rendering of text'
url='https://www.pango.org/'
license=('LGPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'cairo'
	'fontconfig'
	'freetype2'
	'fribidi'
	'glib2'
	'glibc'
	'harfbuzz'
	'libx11'
	'libxft'
	'libxrender'
)
makedepends=(
	'git'
	'gobject-introspection'
	'gtk-doc'
	'help2man'
	'meson'
)
checkdepends=(
	'ttf-dejavu'
	'cantarell-fonts'
)

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
provides=(
	'libpangocairo-1.0.so'
	'libpangoft2-1.0.so'
	'libpangoxft-1.0.so'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_commit='73b46b04c724335ad73534cc69955baa2388b861'
source=(
	"git+https://gitlab.gnome.org/GNOME/pango.git#commit=$_commit"
	'0001-Use-shape-flags-for-tab-width.patch'
	'test.diff'
)
sha256sums=(
	'SKIP'
	'd87bf1bb8dcd7edba0d8f49ddc7fd6bd9965f21a38f186fbfb297828ad53f4d1'
	'401b51f3aee44510fc94878b5549ff4a4a6d79584b5d6df2dd7e0978b9d574ce'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname

	# Apply patches
	git apply -3 $srcdir/0001-Use-shape-flags-for-tab-width.patch
	git apply -3 $srcdir/test.diff
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname

	# Default configure options
	config_opts=(
		'-Dgtk_doc=true'
		'-Dintrospection=false'
		'-Dinstall-tests=false'
		'-Duse_fontconfig=true'
	)

	# Run package configuration script
	arch-meson build "${config_opts[@]}"

	# Build package
	ninja -C build
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname

	# Run internal tests
	ninja -C build test
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname

	# Install files
	DESTDIR=$pkgdir ninja -C build install
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
