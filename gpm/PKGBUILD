################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Ɔ) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('gpm')
pkgver=1.20.8
pkgrel=1
pkgdesc='A mouse server for the console and xterm'
url='http://www.nico.schottelius.org/software/gpm/'
license=('GPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'glibc'
	'sh'
	'ncurses'
)
makedepends=('git')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_commit=1fd19417b8a4dd9945347e98dfa97e4cfd798d77
source=(
	"git+https://github.com/telmich/gpm.git#commit=$_commit"
	'0001-glibc-sigemptyset.patch'
	'gpm.sh'
	'gpm.service'
)
sha256sums=(
	'SKIP'
	'61f901aae46ff79679a058758151dc93901dcd9ea938fabb0765554993b8cb09'
	'f41e90dcf6c0c6c4b8eff1c69039a20eb6b38ea851ffd1fa47ba311bf83d6ed8'
	'dc7d2463f6670ff2c1646a571ffad51f7c603793c25c6f685efad13cbb444034'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
options=('!makeflags')	# TODO: Удалить
prepare() {

	# Change directory to source directory
	cd $pkgname

	# Apply patches
	patch -p1 -i $srcdir/0001-glibc-sigemptyset.patch

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'

	# 'ncurses' related options
		'--with-curses'
		#'--without-curses'
	)

	# Change compile flags
	export CFLAGS+=' -fno-stack-protector'
	export CXXFLAGS="${CFLAGS}"

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname

	# Install files
	make DESTDIR=$pkgdir install

	# Install script file
	install -Dm755 ../gpm.sh $pkgdir/etc/profile.d/gpm.sh

	# Install systemd service files
	install -Dm644 ../gpm.service $pkgdir/usr/lib/systemd/system/gpm.service

	# Install additional symlinks to shared library
	cd $pkgdir/usr/lib/
	ln -s libgpm.so.2.* libgpm.so
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
