################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgbase='zlib'
pkgname=('zlib' 'minizip')
pkgver=1.2.11
pkgrel=4
epoch=1
url='http://www.zlib.net/'
license=('custom')
arch=('x86_64')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"http://zlib.net/zlib-$pkgver.tar.gz"
	"http://zlib.net/zlib-$pkgver.tar.gz.asc"
)
md5sums=(
	'1c9f62f0778697a09d36121ead88e08e'
	'SKIP'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgbase-$pkgver

	# Extract package license info
	grep -A 24 '^  Copyright' zlib.h > LICENSE

	# Change directory to minizip source directory
	cd contrib/minizip

	# Create configure script
	autoreconf --install
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgbase-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Build package
	make

	# Change directory to minizip source directory
	cd contrib/minizip

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgbase-$pkgver

	# Run internal checks
	make -k check

	# Change directory to minizip source directory
	cd contrib/minizip

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Zlib                                                                   #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_zlib() {
	pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'
	depends=('glibc')

	# Change directory to source directory
	cd $pkgbase-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 LICENSE $pkgdir/usr/share/licenses/zlib/LICENSE
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Minizip                                                                #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_minizip() {
	pkgdesc='Mini zip and unzip based on zlib'
	depends=(
		'glibc'
		'zlib'
	)

	# Change directory to source directory
	cd $pkgbase-$pkgver/contrib/minizip

	# Install files
	make DESTDIR=$pkgdir install

	# Remove file 'crypt.h'
	rm $pkgdir/usr/include/minizip/crypt.h

	# Install license file
	install -Dm644 ../../LICENSE $pkgdir/usr/share/licenses/minizip/LICENSE
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
