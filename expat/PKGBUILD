################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('expat')
pkgver=2.2.9
pkgrel=3
pkgdesc='An XML parser library'
url='https://libexpat.github.io/'
license=('custom')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'glibc'
	#'libbsd'
)
makedepends=('cmake')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://github.com/libexpat/libexpat/releases/download/R_${pkgver//./_}/expat-${pkgver}.tar.bz2"
	"https://github.com/libexpat/libexpat/releases/download/R_${pkgver//./_}/expat-${pkgver}.tar.bz2.asc"
)
sha256sums=(
	'f1063084dc4302a427dabcca499c8312b3a32a29b7d2506653ecc8f950a9a237'
	'SKIP'
)

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		"-DCMAKE_C_FLAGS='$CFLAGS'"
		"-DCMAKE_CXX_FLAGS='$CXXFLAGS'"
		"-DCMAKE_LD_FLAGS='$LDFLAGS'"
		'-DCMAKE_INSTALL_PREFIX=/usr'
		'-DCMAKE_BUILD_TYPE=Release'
		'-DEXPAT_SHARED_LIBS=ON'
		'-DEXPAT_BUILD_TOOLS=ON'
		'-DEXPAT_BUILD_DOCS=OFF'
		'-DEXPAT_BUILD_EXAMPLES=OFF'
		'-DEXPAT_BUILD_FUZZERS=OFF'
		'-DEXPAT_BUILD_TESTS=ON'
		'-DEXPAT_WARNINGS_AS_ERRORS=OFF'
		'-DEXPAT_ENABLE_INSTALL=ON'
		'-DEXPAT_CHAR_TYPE=char'
		'-DEXPAT_LARGE_SIZE=OFF'
		'-DEXPAT_MIN_SIZE=OFF'
		'-DEXPAT_DTD=ON'
		'-DEXPAT_NS=ON'
		'-DEXPAT_ATTR_INFO=OFF'
		'-DEXPAT_DEV_URANDOM=ON'
		'-DEXPAT_WITH_GETRANDOM=ON'
		'-DEXPAT_WITH_SYS_GETRANDOM=ON'

	# 'libbsd' related options
		#'-DEXPAT_WITH_LIBBSD=ON'
		'-DEXPAT_WITH_LIBBSD=OFF'
	)

	# Create build directory
	mkdir -p build && cd build

	# Run package configuration script
	cmake .. "${config_opts[@]}"

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname-$pkgver/build

	# Run internal checks
	make -k tests
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver/build

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 ../COPYING $pkgdir/usr/share/licenses/expat/COPYING
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
