################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('libass')
pkgver=0.14.0
pkgrel=1
pkgdesc='A portable library for SSA/ASS subtitles rendering'
url='https://github.com/libass/libass/'
license=('BSD')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'fontconfig'
	'fribidi'
	'glibc'
	'harfbuzz'
	'libfreetype.so'
)
makedepends=('nasm')

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
provides=('libass.so')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=("https://github.com/$pkgname/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('881f2382af48aead75b7a0e02e65d88c5ebd369fe46bc77d9270a94aa8fd38a2')

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-test'
		'--disable-profile'
		'--disable-require-system-font-provider'
		'--enable-asm'
		'--disable-large-tiles'

	# 'fontconfig' related options
		'--enable-fontconfig'
		#'--disable-fontconfig'

	# 'harfbuzz' related options
		'--enable-harfbuzz'
		#'--disable-harfbuzz'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 COPYING $pkgdir/usr/share/licenses/libass/COPYING
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
