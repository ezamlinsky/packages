################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('kbd')
pkgver=2.0.4
pkgrel=2
pkgdesc='Keytable files and keyboard utilities'
url='http://www.kbd-project.org'
license=('GPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'gcc-libs'
	'glibc'
	'pam'
	'sh'
)
makedepends=('check')

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
provides=('vlock')
conflicts=('vlock')
replaces=('vlock')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://www.kernel.org/pub/linux/utils/kbd/kbd-$pkgver.tar.gz"
	"https://www.kernel.org/pub/linux/utils/kbd/kbd-$pkgver.tar.sign"
	'fix-euro2.patch'
)
md5sums=(
	'c69fce737a5fd0fa686cbfc4f611a220'
	'SKIP'
	'd869200acbc0aab6a9cafa43cb140d4e'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Rename keymap files with the same names
	mv data/keymaps/i386/qwertz/cz{,-qwertz}.map
	mv data/keymaps/i386/olpc/es{,-olpc}.map
	mv data/keymaps/i386/olpc/pt{,-olpc}.map
	mv data/keymaps/i386/dvorak/no{,-dvorak}.map
	mv data/keymaps/i386/fgGIod/trf{,-fgGIod}.map
	mv data/keymaps/i386/colemak/{en-latin9,colemak}.map

	# Apply patches
	patch -Np1 -i $srcdir/fix-euro2.patch

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--datadir=/usr/share/kbd'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--enable-optional-progs'
		'--disable-tests'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
