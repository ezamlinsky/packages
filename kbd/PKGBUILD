################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('kbd')
pkgver=2.2.0
pkgrel=5
pkgdesc='Keytable files and keyboard utilities'
url='http://www.kbd-project.org'
license=('GPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'glibc'
	'pam'
	'sh'
)
makedepends=(
	'check'
	'git'
)

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
provides=('vlock')
conflicts=('vlock')
replaces=('vlock')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"git+https://git.kernel.org/pub/scm/linux/kernel/git/legion/$pkgname.git#tag=v$pkgver"
	'fix-euro2.patch'
	'kbd-fix-loadkmap-compat.patch'
)
md5sums=(
	'SKIP'
	'd869200acbc0aab6a9cafa43cb140d4e'
	'730b1054fbd88b87c27c5565bd7d4fc6'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname

	# Rename keymap files with the same names
	mv data/keymaps/i386/qwertz/cz{,-qwertz}.map
	mv data/keymaps/i386/olpc/es{,-olpc}.map
	mv data/keymaps/i386/olpc/pt{,-olpc}.map
	mv data/keymaps/i386/fgGIod/trf{,-fgGIod}.map
	mv data/keymaps/i386/colemak/{en-latin9,colemak}.map

	# Apply patches
	patch -Np1 -i ../fix-euro2.patch
	git cherry-pick -n 15a74479
	git cherry-pick -n acf93e44
	git cherry-pick -n 7e27102b
	patch -p1 -i ../kbd-fix-loadkmap-compat.patch

	# Create configure script
	autoreconf -fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--datadir=/usr/share/kbd'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-tests'
		'--enable-vlock'
		'--enable-libkeymap'
		'--enable-optional-progs'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname

	# Install files
	make DESTDIR=$pkgdir install
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
