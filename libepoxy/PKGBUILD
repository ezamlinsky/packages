################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('libepoxy')
pkgver=1.5.4
pkgrel=1
pkgdesc='Library handling OpenGL function pointer management'
url='https://github.com/anholt/libepoxy'
license=('MIT')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=('glibc')
makedepends=(
	'git'
	'meson'
	'doxygen'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_commit='09edbe01d901c0f01e866aa08455c6d9ee6fd0ac'
source=("git+https://github.com/anholt/libepoxy#commit=$_commit")
sha256sums=('SKIP')

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname

	# Default configure options
	config_opts=(
		'-Dx11=true'
		'-Dglx=yes'
		'-Degl=yes'
		'-Ddocs=false'
		'-Dtests=false'
	)

	# Run package configuration script
	arch-meson build "${config_opts[@]}"

	# Build package
	ninja -C build
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname

	# Run internal tests
	ninja -C build test
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname

	# Install files
	DESTDIR=$pkgdir ninja -C build install

	# Install license file
	install -Dm644 COPYING $pkgdir/usr/share/licenses/libepoxy/COPYING
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
