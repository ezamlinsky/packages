################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname='icu'
pkgver=65.1
pkgrel=3
pkgdesc='International Components for Unicode library'
url='http://www.icu-project.org/'
license=('custom:icu')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'gcc-libs'
	'glibc'
	'sh'
)
makedepends=('python')

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
provides=(
	'libicudata.so'
	'libicui18n.so'
	'libicuio.so'
	'libicutest.so'
	'libicutu.so'
	'libicuuc.so'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://github.com/unicode-org/icu/releases/download/release-${pkgver//./-}/icu4c-${pkgver//./_}-src.tgz"
	"https://github.com/unicode-org/icu/releases/download/release-${pkgver//./-}/icu4c-${pkgver//./_}-src.tgz.asc"
	'icu-65.1-initialized-buffer-uloc_getKeywordValue.patch::https://github.com/unicode-org/icu/commit/fab4c3c719.patch'
	'icu-65.1-prevent-SEGV_MAPERR-in-append.patch::https://github.com/unicode-org/icu/commit/b7d08bc04a.patch'
)
sha512sums=(
	'8f1ef33e1f4abc9a8ee870331c59f01b473d6da1251a19ce403f822f3e3871096f0791855d39c8f20c612fc49cda2c62c06864aa32ddab2dbd186d2b21ce9139'
	'SKIP'
	'8898fe0fa9805304cd2fc02c00ab1131861836f4d11887f82450c2378666cb03bce0c5038d3f0bdcdd1c1cdee2a00a61ef85602ed0e0c74ad2e58578b1940123'
	'580283cdd95fb7b8410cb3a6c0f47a6c8e53e0fdc9c213b04cd133ba4120381533ff2aef89ddab968150754bd9ca3a536d5c592c6881e625eb8ee6de8723de1b'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname

	# Apply patches
	patch -p2 -i ../icu-65.1-initialized-buffer-uloc_getKeywordValue.patch
	patch -p2 -i ../icu-65.1-prevent-SEGV_MAPERR-in-append.patch

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname/source

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--enable-release'
		'--disable-debug'
		'--disable-tracing'
		'--disable-tests'
		'--disable-fuzzer'
		'--disable-samples'
		'--enable-tools'
		'--enable-extras'
		'--enable-icuio'
		'--enable-layoutex'
		'--enable-plugins'
		'--enable-renaming'
		'--enable-draft'
		'--enable-auto-cleanup'
		'--enable-icu-config'
		'--disable-dyload'
		'--disable-weak-threads'
		'--with-library-bits=64'
		'--with-data-packaging=library'
	)

	# Change compile flags
	export CFLAGS+=' -fno-lto'
	export CXXFLAGS+=' -fno-lto'

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname/source

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname/source

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 $srcdir/icu/LICENSE $pkgdir/usr/share/licenses/icu/LICENSE
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
