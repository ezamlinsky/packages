################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgbase='fuse3'
pkgname=('fuse3' 'fuse-common')
pkgver=3.7.0
pkgrel=1
url='https://github.com/libfuse/libfuse'
license=('GPL2')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
makedepends=(
	'pkgconfig'
	'meson'
	'udev'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://github.com/libfuse/libfuse/releases/download/fuse-$pkgver/fuse-$pkgver.tar.xz"
	"https://github.com/libfuse/libfuse/releases/download/fuse-$pkgver/fuse-$pkgver.tar.xz.asc"
)
sha1sums=(
	'baa0c174d1ecad85be5cd7caea37c5bee083ab2f'
	'SKIP'
)

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd fuse-$pkgver

	# Default configure options
	config_opts=(
		'-Ddisable-mtab=false'
		'-Dutils=true'
		'-Duseroot=true'
		'-Dexamples=false'
	)

	# Change compile flags
	export CFLAGS+=' -fno-lto'
	export CXXFLAGS="${CFLAGS}"

	# Run package configuration script
	arch-meson build "${config_opts[@]}"

	# Build package
	ninja -C build
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Fuse3                                                                  #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_fuse3() {
	pkgdesc='A library that makes it possible to implement a filesystem in a userspace program.'
	depends=(
		'fuse-common'
		'glibc'
	)

	# Change directory to source directory
	cd fuse-$pkgver

	# Install files
	DESTDIR=$pkgdir ninja -C build install

	# Change directory to target directory
	cd $pkgdir

	# Remove static devices
	rm -fr dev

	# Split 'fuse-common' package
	mkdir -p $srcdir/fuse-common/etc
	mv etc/fuse.conf $srcdir/fuse-common/etc
	rm -fr etc
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Fuse-common                                                            #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_fuse-common() {
	pkgdesc='Common files for fuse2/3 packages'
	depends=()
	backup=('etc/fuse.conf')

	# Copy files
	mv $srcdir/fuse-common/* $pkgdir
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
