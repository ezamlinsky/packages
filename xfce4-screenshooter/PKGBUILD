################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('xfce4-screenshooter')
pkgver=1.9.6
pkgrel=1
pkgdesc='Plugin that makes screenshots for the Xfce panel'
url='http://goodies.xfce.org/projects/applications/xfce4-screenshooter'
license=('GPL2')
arch=('x86_64')
groups=('xfce4-goodies')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'cairo'
	'exo'
	'gdk-pixbuf2'
	'glib2'
	'glibc'
	'gtk3'
	'hicolor-icon-theme'
	'libsoup'
	'libx11'
	'libxext'
	'libxfce4ui'
	'libxfce4util'
	'libxfixes'
	'libxml2'
	'xfce4-panel'
)
makedepends=(
	'intltool'
	'python'
)

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
conflicts=('xfce4-screenshooter-plugin')
replaces=('xfce4-screenshooter-plugin')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=("http://archive.xfce.org/src/apps/xfce4-screenshooter/${pkgver%.*}/xfce4-screenshooter-$pkgver.tar.bz2")
sha256sums=('88c174ce687d1f7ba5470d6ab5784d33dc215f8f220211e892e268189dbea658')

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-debug'
		'--enable-debug=no'
		'--with-x'

	# 'libxfixes' related options
		'--enable-xfixes'
		#'--disable-xfixes'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
