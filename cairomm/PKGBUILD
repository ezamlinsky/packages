################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgbase='cairomm'
pkgname=('cairomm' 'cairomm-docs')
pkgver=1.12.2
pkgrel=3
url='https://www.cairographics.org/cairomm/'
license=('LGPL' 'MPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
makedepends=(
	'git'
	'mm-common'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_commit=e9ef515b7b8db5b4f024ddfefe5dfc03f2b8ccea  # tags/v1.12.2^0
source=("git+https://anongit.freedesktop.org/git/cairomm#commit=$_commit")
sha256sums=('SKIP')

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgbase

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgbase

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--enable-maintainer-mode'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Cairomm                                                                #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_cairomm() {
	pkgdesc='C++ bindings for Cairo'
	depends=(
		'cairo'
		'gcc-libs'
		'glibc'
		'libsigc++'
	)

	# Change directory to source directory
	cd $pkgbase

	# Install files
	make DESTDIR=$pkgdir install

	# Change directory to target directory
	cd $pkgdir

	# Split 'cairomm-docs' package
	mkdir -p $srcdir/cairomm-docs/usr
	mv usr/share $srcdir/cairomm-docs/usr || true
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Cairomm-docs                                                           #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_cairomm-docs() {
	pkgdesc='C++ bindings for Cairo (documentation)'
	depends=()
	options=('docs')

	# Copy files
	mv $srcdir/cairomm-docs/* $pkgdir
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
