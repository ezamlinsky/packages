################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('libsecret')
pkgver=0.19.1
pkgrel=1
pkgdesc='Library for storing and retrieving passwords and other secrets'
url='https://wiki.gnome.org/Projects/Libsecret'
license=('LGPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'gcc-libs'
	'glib2'
	'glibc'
	'libgcrypt'
	#'vala'
)
makedepends=(
	'docbook-xsl'
	'git'
	'gobject-introspection'
	'gtk-doc'
	'meson'
)
checkdepends=(
	'dbus-glib'
	'gjs'
	'python-dbus'
	'python-gobject'
)
optdepends=(
	'gnome-keyring: key storage service, or use any other service implementing org.freedesktop.secrets'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_commit=f576dfbe50f9aed22802ad0db0356d081f467d7a  # tags/0.19.1^0
source=("git+https://gitlab.gnome.org/GNOME/libsecret.git#commit=$_commit")
sha256sums=('SKIP')

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname

	# Default configure options
	config_opts=(
		'-Ddebugging=false'
		'-Dmanpage=true'
		'-Dgtk_doc=true'

	# 'libgcrypt' related options
		'-Dgcrypt=true'
		#'-Dgcrypt=false'

	# 'vala' related options
		#'-Dvapi=true'
		'-Dvapi=false'
	)

	# Run package configuration script
	arch-meson build "${config_opts[@]}"

	# Build package
	ninja -C build
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname

	# Run internal checks
	dbus-run-session ninja -C build test
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname

	# Install files
	DESTDIR=$pkgdir ninja -C build install
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
