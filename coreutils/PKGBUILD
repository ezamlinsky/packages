################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('coreutils')
pkgver=8.31
pkgrel=1
pkgdesc='The basic file, shell and text manipulation utilities of the GNU operating system'
url='https://www.gnu.org/software/coreutils/'
license=('GPL3')
arch=('x86_64')
groups=('base')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'acl'
	'attr'
	'glibc'
	'gmp'
	'libcap'
	#'libselinux'
	#'libsmack'
	'openssl'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://ftp.gnu.org/gnu/coreutils/coreutils-$pkgver.tar.xz"
	"https://ftp.gnu.org/gnu/coreutils/coreutils-$pkgver.tar.xz.sig"
)
md5sums=(
	'0009a224d8e288e8ec406ef0161f9293'
	'SKIP'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-assert'
		'--enable-threads=posix'
		'--enable-no-install-program=groups,hostname,kill,uptime'
		'--with-linux-crypto'
		'--without-included-regex'

	# 'acl' related options
		'--enable-acl'
		#'--disable-acl'

	# 'attr' related options
		'--enable-xattr'
		#'--disable-xattr'

	# 'gmp' related options
		'--with-gmp'
		#'--without-gmp'

	# 'libcap' related options
		'--enable-libcap'
		#'--disable-libcap'

	# 'libselinux' related options
		#'--with-selinux'
		'--without-selinux'

	# 'libsmack' related options
		#'--enable-libsmack'
		'--disable-libsmack'

	# 'openssl' related options
		'--with-openssl'
		#'--without-openssl'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
