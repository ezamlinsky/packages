################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('zip')
pkgver=3.0
pkgrel=8
pkgdesc='Compressor/archiver for creating and modifying zipfiles'
url='http://www.info-zip.org/Zip.html'
license=('custom')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'bzip2'
	'glibc'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_pkgver=${pkgver/./}
source=(
	"http://downloads.sourceforge.net/infozip/zip$_pkgver.tar.gz"
	'zipnote.patch'
)
sha256sums=(
	'f0e8bb1f9b7eb0b01285495a2699df3a4b766784c1765a8f1aeedf63c0806369'
	'89249a3f1fdf838b795ce432a2d763fdbe913d6a146541e41b7a2e2769291ba0'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname$_pkgver

	# Apply patches
	patch -p1 -i $srcdir/zipnote.patch

	# Change compile flags
	export CFLAGS+=' -fno-stack-protector'
	export CXXFLAGS+="${CFLAGS}"

	# Set correct CFLAGS
	sed -e "s/CFLAGS_NOOPT =/CFLAGS_NOOPT = $CFLAGS/" -i unix/Makefile

	# Set correct LDFLAGS
	sed -e "s/LFLAGS1=\'\'/LFLAGS1=$LDFLAGS/" -i unix/configure
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname$_pkgver

	# Build package
	make -f unix/Makefile generic_gcc
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname$_pkgver

	# Install files
	make -f unix/Makefile prefix=$pkgdir/usr MANDIR=$pkgdir/usr/share/man/man1 install

	# Install license file
	install -Dm644 LICENSE $pkgdir/usr/share/licenses/zip/LICENSE
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
