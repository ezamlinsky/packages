################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Ɔ) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('djvulibre')
pkgver=3.5.27
pkgrel=5
pkgdesc='Suite to create, manipulate and view DjVu ("déjà vu") documents'
url='http://djvu.sourceforge.net/'
license=('GPL2')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'bash'
	'gcc-libs'
	'glibc'
	'hicolor-icon-theme'
	'libjpeg'
	'libtiff'
)
makedepends=('librsvg')

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
provides=("libdjvu=${pkgver}")
conflicts=('libdjvu')
replaces=('libdjvu')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://downloads.sourceforge.net/project/djvu/DjVuLibre/$pkgver/$pkgname-$pkgver.tar.gz"
	'0001-always-assume-that-cpuid-works-on-x86_64.patch'
)
sha256sums=(
	'e69668252565603875fb88500cde02bf93d12d48a3884e472696c896e81f505f'
	'35654b433a7bbf0bf7e039385a80034776d5134bc59ff82ef74170ef4bbc86ce'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Apply patches
	patch -p1 -i $srcdir/0001-always-assume-that-cpuid-works-on-x86_64.patch

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-debug'
		'--enable-xmltools'
		'--enable-desktopfiles'

	# 'libjpeg' related options
		'--with-jpeg'
		#'--without-jpeg'

	# 'libtiff' related options
		'--with-tiff'
		#'--without-tiff'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install

	# Install icons
	for size in 22 32 48 64; do
		install -Dm644 desktopfiles/prebuilt-hi${size}-djvu.png \
		$pkgdir/usr/share/icons/hicolor/${size}x${size}/mimetypes/image-vnd.djvu.mime.png
	done
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
