################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname='gnome-themes-extra'
pkgver=3.28
pkgrel=1
pkgdesc='Extra Themes for GNOME Applications'
url='https://gitlab.gnome.org/GNOME/gnome-themes-extra'
license=('LGPL2.1')
arch=('x86_64')
groups=('gnome')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'cairo'
	'gcc-libs'
	'glib2'
	'gtk2'
)
makedepends=(
	'intltool'
	'gtk3'
	'gnome-common'
	'git'
)
optdepends=('gtk-engines: HighContrast GTK2 theme')

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
provides=("gnome-themes-standard=$pkgver")
conflicts=('gnome-themes-standard')
replaces=('gnome-themes-standard<3.27.92')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_commit=9f581269243dd7e76b3eb8cec9cf4128ab17da9e  # tags/3.28^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-themes-extra.git#commit=$_commit")
sha256sums=('SKIP')

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname

	# Create configure script
	mkdir m4
	intltoolize
	autoreconf -fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-glibtest'
		'--enable-gtk3-engine'
		'--enable-gtk2-engine'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname

	# Install files
	make DESTDIR=$pkgdir install
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
