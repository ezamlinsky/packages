################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('libogg')
pkgver=1.3.4
pkgrel=1
pkgdesc='Ogg bitstream and framing library'
url='http://www.xiph.org/ogg/'
license=('BSD')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'gcc-libs'
	'glibc'
)
makedepends=(
	'cmake'
	'git'
	'ninja'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=('git+https://github.com/xiph/ogg.git?signed#tag=3328abd152508614f7ce4cd491dc98d14eba7ffc')
sha256sums=('SKIP')

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd ogg

	# Default configure options
	config_opts=(
		"-DCMAKE_C_FLAGS='$CFLAGS'"
		"-DCMAKE_CXX_FLAGS='$CXXFLAGS'"
		"-DCMAKE_LD_FLAGS='$LDFLAGS'"
		'-DCMAKE_INSTALL_PREFIX=/usr'
		'-DCMAKE_BUILD_TYPE=Release'
		'-DBUILD_SHARED_LIBS=ON'
		'-DBUILD_TESTING=OFF'
		'-DINSTALL_DOCS:BOOL=ON'
		'-DINSTALL_PKG_CONFIG_MODULE=ON'
		'-DINSTALL_CMAKE_PACKAGE_MODULE=ON'
	)

	# Create build directory
	mkdir -p build && cd build

	# Run package configuration script
	cmake .. -G Ninja "${config_opts[@]}"

	# Build package
	ninja
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd ogg/build

	# Install files
	DESTDIR=$pkgdir ninja install

	# Install license file
	install -Dm644 ../COPYING $pkgdir/usr/share/licenses/libogg/COPYING
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
