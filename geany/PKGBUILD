################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('geany')
pkgver=1.36
pkgrel=1
pkgdesc='Fast and lightweight IDE'
url='https://www.geany.org/'
license=('GPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'atk'
	'cairo'
	'gcc-libs'
	'gdk-pixbuf2'
	'glib2'
	'glibc'
	'gtk3'
	'pango'
)
makedepends=(
	'doxygen'
	'intltool'
	'python-lxml'
)
optdepends=(
	'geany-plugins: additional functionality'
	'vte3: embedded terminal support'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=("https://download.geany.org/$pkgname-${pkgver/.0}.tar.bz2")
sha256sums=('9184dd3dd40b7b84fca70083284bb9dbf2ee8022bf2be066bdc36592d909d53e')

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-${pkgver/.0}

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-${pkgver/.0}

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--enable-plugins'
		'--enable-socket'
		'--enable-vte'
		'--disable-html-docs'
		'--disable-pdf-docs'
		'--disable-api-docs'
		'--disable-gtkdoc-header'

	# 'gtk3' related options
		'--enable-gtk3'
		#'--disable-gtk3'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-${pkgver/.0}

	# Install files
	make DESTDIR=$pkgdir install
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
