################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('libdrm')
pkgver=2.4.99
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
url="https://dri.freedesktop.org/"
license=('custom')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'gcc-libs'
	'glibc'
	'libpciaccess'
)
makedepends=(
	'cmake'
	'docbook-xsl'
	'libxslt'
	'meson'
	'udev'
	'valgrind'
)
checkdepends=('cairo')

#==============================================================================#
#        Package relations                                                     #
#==============================================================================#
replaces=(
	'libdrm-new'
	'libdrm-nouveau'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://dri.freedesktop.org/$pkgname/$pkgname-$pkgver.tar.bz2"
	'no-drmdevice-test.diff'
	'COPYING'
)
sha512sums=(
	'04702eebe8dca97fac61653623804fdcb0b8b3714bdc6f5e72f0dfdce9c9524cf16f69d37aa9feac79ddc1c11939be44a216484563a612414668ea5eaeadf191'
	'f1dd5d8c2270c092ccb8e4f92a0da9ab27706dfa22dcedd3fb2414b968ced9333c8bf62baf0219b822e43dce0d804d1dd5cc27d09b0afe8c01967c1784d4a4bb'
	'b0ca349b882a4326b19f81f22804fabdb6fb7aef31cdc7b16b0a7ae191bfbb50c7daddb2fc4e6c33f1136af06d060a273de36f6f3412ea326f16fa4309fda660'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Apply patches
	patch -Np1 -i ../no-drmdevice-test.diff
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'-Dman-pages=true'
		'-Dudev=true'
		'-Dvalgrind=false'
		'-Dcairo-tests=false'
		'-Dinstall-test-programs=false'
		'-Dlibkms=true'
		'-Dintel=true'
		'-Dradeon=true'
		'-Damdgpu=true'
		'-Dnouveau=true'
		'-Dvmwgfx=true'
		'-Domap=false'
		'-Dexynos=false'
		'-Dfreedreno=false'
		'-Dtegra=false'
		'-Dvc4=false'
		'-Detnaviv=false'
		'-Dfreedreno-kgsl=false'
	)

	# Run package configuration script
	arch-meson build "${config_opts[@]}"

	# Build package
	ninja -C build
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run internal tests
	ninja -C build test
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	DESTDIR=$pkgdir ninja -C build install

	# Install license file
	install -Dm644 ../COPYING $pkgdir/usr/share/licenses/libdrm/COPYING
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
