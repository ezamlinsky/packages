################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname='xdg-utils'
pkgver=1.1.4
pkgrel=1
pkgdesc='Command line tools that assist applications with a variety of desktop integration tasks'
url='https://www.freedesktop.org/wiki/Software/xdg-utils/'
license=('MIT')
arch=('any')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'sh'
	'xorg-xset'
)
makedepends=(
	'docbook-xsl'
	'git'
	'lynx'
	'xmlto'
)
optdepends=(
	'exo: for Xfce support in xdg-open'
	'kde-cli-tools: for KDE Plasma5 support in xdg-open'
	'pcmanfm: for LXDE support in xdg-open'
	'perl-file-mimeinfo: for generic support in xdg-open'
	'perl-net-dbus: Perl extension to dbus used in xdg-screensaver'
	'perl-x11-protocol: Perl X11 protocol used in xdg-screensaver'
	'xorg-xprop: for Xfce support in xdg-open'
)

#==============================================================================#
#       Package miscellaneous options                                          #
#==============================================================================#
options=('emptydirs')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
_commit='0547886c0a7ae79145998495a6e3af6a1450d0c7' # master # 2019-04-19
source=("git+https://gitlab.freedesktop.org/xdg/xdg-utils.git#commit=$_commit")
sha256sums=('SKIP')

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname

	# Run internal checks
	make -k check
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 LICENSE $pkgdir/usr/share/licenses/xdg-utils/LICENSE

	# Install empty directory to prevent xdg-desktop-menu install *.desktop to fail
	mkdir -p $pkgdir/usr/share/desktop-directories
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
