################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('re2')
_re2ver=2019-08-01
pkgver=${_re2ver//-}
pkgrel=1
pkgdesc='Fast, safe, thread-friendly regular expression engine'
url='https://github.com/google/re2'
license=('BSD')
arch=('x86_64')
#groups=()

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'gcc-libs'
	'glibc'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=("re2-$pkgver.tar.gz::https://github.com/google/re2/archive/${_re2ver}.tar.gz")
sha256sums=('38bc0426ee15b5ed67957017fd18201965df0721327be13f60496f2b356e3e01')

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$_re2ver

	# Default configure options
	config_opts=(
		"-DCMAKE_C_FLAGS='$CFLAGS'"
		"-DCMAKE_CXX_FLAGS='$CXXFLAGS'"
		"-DCMAKE_LD_FLAGS='$LDFLAGS'"
		'-DCMAKE_INSTALL_PREFIX=/usr'
		'-DCMAKE_INSTALL_LIBDIR=lib'
		'-DCMAKE_BUILD_TYPE=Release'
		'-DBUILD_SHARED_LIBS=ON'
		'-DBUILD_TESTING=ON'
		'-DRE2_BUILD_TESTING=ON'
		'-DUSEPCRE=OFF'
	)

	# Create build directory
	mkdir -p build && cd build

	# Run package configuration script
	cmake .. "${config_opts[@]}"

	# Build package
	make
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname-$_re2ver

	# Run internal checks
	make test
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$_re2ver/build

	# Install files
	make DESTDIR=$pkgdir install

	# Install license file
	install -Dm644 ../LICENSE $pkgdir/usr/share/licenses/re2/LICENSE
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
