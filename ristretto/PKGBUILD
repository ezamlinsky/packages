################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('ristretto')
pkgver=0.10.0
pkgrel=2
pkgdesc='Fast and lightweight picture-viewer for Xfce4'
url='http://docs.xfce.org/apps/ristretto/start'
license=('GPL')
arch=('x86_64')
groups=('xfce4-goodies')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'cairo'
	'dbus-glib'
	'file'
	'gcc-libs'
	'gdk-pixbuf2'
	'glib2'
	'glibc'
	'gtk3'
	'libexif'
	'libx11'
	'libxfce4ui'
	'libxfce4util'
	'pango'
	'xfconf'
)
makedepends=(
	'intltool'
	'xfce4-dev-tools'
)
optdepends=(
	'librsvg: SVG support'
	'tumbler: thumbnailing support'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=("https://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2")
sha256sums=('16225dd74245eb6e0f82b9c72c6112f161bb8d8b11f3fc77277b7bc3432d4769')

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Run autogen script if exists
	if [ -x autogen.sh ]; then
		NOCONFIGURE=1 ./autogen.sh
	fi
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Default configure options
	config_opts=(
		'--prefix=/usr'
		'--bindir=/usr/bin'
		'--sbindir=/usr/bin'
		'--libdir=/usr/lib'
		'--libexecdir=/usr/lib'
		'--sysconfdir=/etc'
		'--localstatedir=/var'
		'--enable-silent-rules'
		'--enable-shared'
		'--disable-static'
		'--disable-debug'
		'--enable-debug=no'
		'--with-x'
	)

	# Run package configuration script
	./configure "${config_opts[@]}"

	# Set correct LDFLAGS to build a shared archive
	if [ -f libtool ]; then
		sed -e "s/ -shared / $LDFLAGS\0/g" -i libtool
	fi

	# Build package
	make
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname-$pkgver

	# Install files
	make DESTDIR=$pkgdir install
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
