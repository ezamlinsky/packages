################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('p11-kit')
pkgver=0.23.17
pkgrel=2
pkgdesc='Provides a way to load and enumerate PKCS#11 modules'
url='https://p11-glue.freedesktop.org'
license=('BSD')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'gcc-libs'
	'glibc'
	'libffi'
	'libtasn1'
	#'nss'
	'systemd-libs'
)
makedepends=(
	'git'
	'gtk-doc'
	'meson'
)

#==============================================================================#
#       Package miscellaneous options                                          #
#==============================================================================#
install='p11-kit.install'

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"git+https://github.com/p11-glue/p11-kit?signed#tag=$pkgver"
	'0001-meson-Fix-dictionary-syntax.patch'
	'0002-meson-Install-p11-kit-client-module.patch'
	'0003-meson-PKCS-11-modules-should-only-export-C_GetFuncti.patch'
	'0004-meson-Don-t-prefix-p11_user_config.patch'
	'0005-meson-Build-and-install-man-pages.patch'
	'0006-meson-Install-p11-kit-server-units.patch'
	'0007-meson-Add-missing-prefix-to-system-path-defines.patch'
	'0008-Build-and-install-libnssckbi-p11-kit.so.patch'
)
sha256sums=(
	'SKIP'
	'e255ae1650eaf236b89038175b9f4799e1ac7dd0820724f96a96be1a1c3d8c51'
	'423fdf1014eb8952cb3d0315c2cd0be164d38674b76a8480b2b8e804af62f4a5'
	'1498bfb9e1570750106a75219313b6de5e1d5377197e26e900864aadb0e5ebf0'
	'147e206d360df72812f989dd8ab630138e3b171e6c1b8c40291b48870c0086e3'
	'1b5ef2e0ad4bd80eee14aed1be9f789ab7df0a27341b79aa09ca39f2d020411a'
	'30b0f6d6ab50566d003bb96ab841203e94064b5d3940e6c7fc7302ca105e70f6'
	'531598f5c94bc83f2a3ebd83e6d2bc42c495f1d999504d695553e8e4b709f307'
	'2605f0de616a75f98459e984b8db2dca8757aac05f03514caa7838ef23c90078'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd $pkgname

	# Apply patches
	patch -Np1 -i $srcdir/0001-meson-Fix-dictionary-syntax.patch
	patch -Np1 -i $srcdir/0002-meson-Install-p11-kit-client-module.patch
	patch -Np1 -i $srcdir/0003-meson-PKCS-11-modules-should-only-export-C_GetFuncti.patch
	patch -Np1 -i $srcdir/0004-meson-Don-t-prefix-p11_user_config.patch
	patch -Np1 -i $srcdir/0005-meson-Build-and-install-man-pages.patch
	patch -Np1 -i $srcdir/0006-meson-Install-p11-kit-server-units.patch
	patch -Np1 -i $srcdir/0007-meson-Add-missing-prefix-to-system-path-defines.patch
	patch -Np1 -i $srcdir/0008-Build-and-install-libnssckbi-p11-kit.so.patch
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname

	# Default configure options
	config_opts=(
		'-Dstrict=false'
		'-Dgtk_doc=true'
		'-Dman=true'
		'-Dtrust_module=enabled'
		'-Dmodule_path=/usr/lib/pkcs11'
		'-Dtrust_paths=/etc/ca-certificates/trust-source:/usr/share/ca-certificates/trust-source'

	# 'libffi' related options
		'-Dlibffi=enabled'
		#'-Dlibffi=disabled'

	# 'nss' related options
		#'-Dhash_impl=freebl'
		'-Dhash_impl=internal'

	# 'systemd' related options
		'-Dsystemd=enabled'
		#'-Dsystemd=disabled'
	)

	# Run package configuration script
	arch-meson build "${config_opts[@]}"

	# Build package
	ninja -C build
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd $pkgname

	# Install files
	DESTDIR=$pkgdir ninja -C build install

	# Install license file
	install -Dm644 COPYING $pkgdir/usr/share/licenses/p11-kit/LICENSE

	# Set correct symlink for 'trust-extract-compat'
	ln -srf $pkgdir/usr/bin/update-ca-trust $pkgdir/usr/lib/p11-kit/trust-extract-compat
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
