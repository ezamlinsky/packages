################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgbase='p11-kit'
pkgname=('p11-kit' 'libp11-kit')
pkgver=0.23.20
pkgrel=3
pkgdesc='Provides a way to load and enumerate PKCS#11 modules'
url='https://p11-glue.freedesktop.org'
license=('BSD')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
makedepends=(
	'git'
	'gtk-doc'
	'libffi'
	'libtasn1'
	'meson'
	'systemd'
)

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=("git+https://github.com/p11-glue/${pkgbase}?signed#tag=$pkgver")
sha256sums=('SKIP')

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd $pkgname

	# Default configure options
	config_opts=(
		'-Dstrict=false'
		'-Dman=true'
		'-Dgtk_doc=true'
		'-Dtrust_module=enabled'
		'-Dmodule_path=/usr/lib/pkcs11'
		'-Dtrust_paths=/etc/ca-certificates/trust-source:/usr/share/ca-certificates/trust-source'

	# 'libffi' related options
		'-Dlibffi=enabled'
		#'-Dlibffi=disabled'

	# 'nss' related options
		#'-Dhash_impl=freebl'
		'-Dhash_impl=internal'

	# 'systemd' related options
		'-Dsystemd=enabled'
		#'-Dsystemd=disabled'
	)

	# Run package configuration script
	arch-meson build "${config_opts[@]}"

	# Build package
	ninja -C build
}

#==============================================================================#
#       Package self testing sequence                                          #
#==============================================================================#
check() {

	# Change directory to source directory
	cd $pkgname-stable

	# Run internal tests
	ninja -C build test
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       P11-kit                                                                #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_p11-kit() {
	pkgdesc='Loads and enumerates PKCS#11 modules'
	depends=(
		"libp11-kit=$pkgver-$pkgrel"
		'glibc'
		'libsystemd.so'
		'libtasn1'
		#'nss'
	)
	install='p11-kit.install'

	# Change directory to source directory
	cd $pkgname

	# Install files
	DESTDIR=$pkgdir ninja -C build install

	# Install license file
	install -Dm644 COPYING $pkgdir/usr/share/licenses/p11-kit/LICENSE

	# Set correct symlink for 'trust-extract-compat'
	ln -srf $pkgdir/usr/bin/update-ca-trust $pkgdir/usr/lib/p11-kit/trust-extract-compat

	# Change directory to target directory
	cd $pkgdir

	# Split 'libp11-kit' package
	mkdir -p $srcdir/libp11-kit/usr/{lib,share}
	mv usr/include $srcdir/libp11-kit/usr || true
	mv usr/lib/{p11-kit-proxy.so,libp11-kit.*} $srcdir/libp11-kit/usr/lib || true
	mv usr/lib/{pkcs11,pkgconfig} $srcdir/libp11-kit/usr/lib || true
	mv usr/share/{locale,p11-kit} $srcdir/libp11-kit/usr/share || true
}

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
#       Libp11-kit                                                             #
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~#
package_libp11-kit() {
	pkgdesc='Loads and enumerates PKCS#11 modules (library)'
	depends=(
		'glibc'
		'libffi'
		'libtasn1'
	)
	provides=('libp11-kit.so')

	# Copy files
	mv $srcdir/libp11-kit/* $pkgdir
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
