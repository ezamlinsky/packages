################################################################################
# Encoding: UTF-8                                                  Tab size: 4 #
#                                                                              #
#                  ARCH LINUX PACKAGE BUILD DESCRIPTION FILE                   #
#                                                                              #
# Ordnung muss sein!                             Copyleft (Æ†) 2019, Arch Linux #
################################################################################
# Maintainer: Jack Black <ezamlinsky@gmail.com>

#==============================================================================#
#       Package information                                                    #
#==============================================================================#
pkgname=('lm_sensors')
pkgver=3.6.0
pkgrel=1
pkgdesc='Collection of user space tools for general SMBus access and hardware monitoring'
url='https://hwmon.wiki.kernel.org/lm_sensors'
license=('GPL' 'LGPL')
arch=('x86_64')

#==============================================================================#
#       Package dependencies                                                   #
#==============================================================================#
depends=(
	'bash'
	'glibc'
	'perl'
)
makedepends=(
	'git'
	'sed'
)
optdepends=('rrdtool: for logging with sensord')

#==============================================================================#
#       Package miscellaneous options                                          #
#==============================================================================#
backup=(
	'etc/conf.d/sensord'
	'etc/healthd.conf'
	'etc/sensors3.conf'
)
options=('emptydirs')

#==============================================================================#
#       Package sources and integrity                                          #
#==============================================================================#
source=(
	"https://github.com/lm-sensors/lm-sensors/archive/V${pkgver//./-}.tar.gz"
	'healthd'
	'healthd.conf'
	'healthd.service'
)
sha256sums=(
	'0591f9fa0339f0d15e75326d0365871c2d4e2ed8aa1ff759b3a55d3734b7d197'
	'0ac9afb2a9155dd74ab393756ed552cd542dde1081149beb2ab4ec7ff55b8f4a'
	'5d17a366b175cf9cb4bb0115c030d4b8d91231546f713784a74935b6e533da9f'
	'2638cd363e60f8d36bcac468f414a6ba29a1b5599f40fc651ca953858c8429d7'
)

#==============================================================================#
#       Package preparation sequence                                           #
#==============================================================================#
prepare() {

	# Change directory to source directory
	cd lm-sensors-${pkgver//./-}

	# Change config directory
	sed -e 's|/etc/sysconfig|/etc/conf.d|' -i prog/{detect/sensors-detect,init/{sensord,lm_sensors}.service}

	# Reset environment file
	sed -e 's/EnvironmentFile=/EnvironmentFile=-/' -i prog/init/lm_sensors.service

	# Set correct CFLAGS
	sed -e "s/-O2/$CFLAGS/" -i Makefile

	# Set correct LDFLAGS
	sed -e "s/EXLDFLAGS :=/EXLDFLAGS := $LDFLAGS /g" -i Makefile
}

#==============================================================================#
#       Package building sequence                                              #
#==============================================================================#
build() {

	# Change directory to source directory
	cd lm-sensors-${pkgver//./-}

	# Build package
	make PREFIX=/usr
}

#==============================================================================#
#       Package installation sequence                                          #
#==============================================================================#
package() {

	# Change directory to source directory
	cd lm-sensors-${pkgver//./-}

	# Install files
	make PREFIX=/usr SBINDIR=/usr/bin MANDIR=/usr/share/man DESTDIR=$pkgdir install

	# Install 'healthd' daemon
	install -Dm755 $srcdir/healthd $pkgdir/usr/bin/healthd

	# Install config files
	install -Dm644 $srcdir/healthd.conf $pkgdir/etc/healthd.conf

	# Install systemd *.service files
	install -Dm644 $srcdir/healthd.service $pkgdir/usr/lib/systemd/system/healthd.service
	install -Dm644 prog/init/*.service $pkgdir/usr/lib/systemd/system/

	# Install license file
	install -Dm644 COPYING $pkgdir/usr/share/licenses/lm_sensors/LICENSE
}

################################################################################
#                                 END OF FILE                                  #
################################################################################
